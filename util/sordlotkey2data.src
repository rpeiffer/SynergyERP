! -- sordlotkey2data
!
! loadsave -w -n 100,10 -o prog/util/sordlotkey2data.dl4 src/sordlotkey2data.src
!
! sordlotkey2data - COMPARE ALL SORDLOT KEYS to DATA IN FILE
!
Option Default Dialect IRIS1
Declare Intrinsic Function FindChannel

External Lib "utillib.lib"
Declare External Function ParseArguments
Declare External Function OpenEmail

try

Dim 1%,mflg,ch[20],ol1[3]
Dim 2%,ol2[3]
Dim 3%,X2,R
Dim F1$[16],C$[16],S0$[36],OL2$[16]
Dim EMAIL_FROM$[50],EMAIL_TO$[50],SUBJECT$[50]
Dim CC1$[50],CC2$[50],nam$[30]
dim args$[5,80],ksord$[52],message$[80]

argc = ParseArguments(args$[], msc$(4))
! First arg is company
intCo = args$[1]
if intCo < 1 or intCo > 99
	print "Invalid company number.  Exiting."
	end
end if

gosub OpenFiles:
Mat Read #ch_ctrl,3,0;nam$
gosub SearchKey1:
gosub SearchKey2:
gosub SearchKey3:
goto OutEnd:

!--------------------------------------------------------------------------
! S U B R O U T I N E S
!--------------------------------------------------------------------------

OpenFiles:
	C$ = intCo Using "cntrl/CNTRL#"
	If intCo > 9 Let C$ = intCo Using "cntrl/CNTRL##"
	ch[0] = FindChannel()
	ch_ctrl = ch[0]
	Ropen #ch_ctrl,C$
	For I = 2 To 2
		Read X2
		Mat Read #ch_ctrl,88,X2;F1$;
		ch[I] = FindChannel()
		Ropen #ch[I],F1$
	Next I
	Data "2320"
	ch_sord = ch[2]
Return

SearchKey1: !
	keyNo = 1
	ksord$ = " ",ksord$
	do
		Search #ch_sord,3,1;ksord$,R,E
		if E>2 Gosub Err_search:
		if e exit do
		gosub ReadSordlot:
		if ksord$[1,12] <> S0$[1,12]
			message$ = "Product in data file: "+s0$[1,12]
			gosub EmailIt:
		End If
		i = ksord$[13,18]
		if i <> ol2[0]
			message$ = "Order # in data file: "+str$(ol2[0])
			gosub EmailIt:
		End If
		i = ksord$[19,21]
		if i <> ol1[0]
			message$ = "Line # in data file: "+str$(ol1[0])
			gosub EmailIt:
		End If
		i = ksord$[22,23]
		if i <> ol1[2]
			message$ = "Warehouse in data file: "+str$(ol1[2])
			gosub EmailIt:
		End If
		if ksord$[24,29] <> ol2$[11,16]
			message$ = "Location in data file: "+ol2$[11,16]
			gosub EmailIt:
		End If
		if ksord$[30,41] <> s0$[13,24]
			message$ = "Lot in data file: "+s0$[13,24]
			gosub EmailIt:
		End If
		if ksord$[42,51] <> ol2$[1,10]
			message$ = "Stock in data file: "+ol2$[1,10]
			gosub EmailIt:
		End If
	loop
Return

SearchKey2: !
	keyNo = 2
	ksord$ = " ",ksord$
	do
		Search #ch_sord,3,2;ksord$,R,E
		if E>2 Gosub Err_search:
		if e exit do
		gosub ReadSordlot:
		if ksord$[1,12] <> s0$[13,24]
			message$ = "Lot in data file: "+s0$[13,24]
			gosub EmailIt:
		End If
		if ksord$[13,22] <> ol2$[1,10]
			message$ = "Stock in data file: "+ol2$[1,10]
			gosub EmailIt:
		End If
		i = ksord$[23,28]
		if i <> ol2[0]
			message$ = "Order # in data file: "+str$(ol2[0])
			gosub EmailIt:
		End If
		i = ksord$[29,31]
		if i <> ol1[0]
			message$ = "Line # in data file: "+str$(ol1[0])
			gosub EmailIt:
		End If
		i = ksord$[32,33]
		if i <> ol1[2]
			message$ = "Warehouse in data file: "+str$(ol1[2])
			gosub EmailIt:
		End If
		if ksord$[34,39] <> ol2$[11,16]
			message$ = "Location in data file: "+ol2$[11,16]
			gosub EmailIt:
		End If
		if ksord$[40,51] <> S0$[1,12]
			message$ = "Product in data file: "+s0$[1,12]
			gosub EmailIt:
		End If
	loop
Return

SearchKey3: !
	keyNo = 3
	ksord$ = " ",ksord$
	do
		Search #ch_sord,3,3;ksord$,R,E
		if E>2 Gosub Err_search:
		if e exit do
		gosub ReadSordlot:
		i = ksord$[1,6]
		if i <> ol2[0]
			message$ = "Order # in data file: "+str$(ol2[0])
			gosub EmailIt:
		End If
		i = ksord$[7,9]
		if i <> ol1[0]
			message$ = "Line # in data file: "+str$(ol1[0])
			gosub EmailIt:
		End If
		i = ksord$[10,11]
		if i <> ol1[2]
			message$ = "Warehouse in data file: "+str$(ol1[2])
			gosub EmailIt:
		End If
		if ksord$[12,17] <> ol2$[11,16]
			message$ = "Location in data file: "+ol2$[11,16]
			gosub EmailIt:
		End If
		if ksord$[18,29] <> s0$[13,24]
			message$ = "Lot in data file: "+s0$[13,24]
			gosub EmailIt:
		End If
		if ksord$[30,39] <> ol2$[1,10]
			message$ = "Stock in data file: "+ol2$[1,10]
			gosub EmailIt:
		End If
	loop
Return

ReadSordlot: !
	mat read #ch_sord,R,0;S0$;
	mat read #ch_sord,R,36;OL1;
	mat read #ch_sord,R,44;OL2;
	mat read #ch_sord,R,96;OL2$;
Return

EmailIt: Rem mail problems to ubs 
	If mflg = 0
		EMAIL_FROM$ = "sordlotkey2data@test.com"
		EMAIL_TO$ = "rich@ubsys.com"
		SUBJECT$ = "sordlot key/data issue " + nam$
		CC1$ = "txc@ubsys.com"
		cc2$ = "rce@ubsys.com"
		outChan = OpenEmail(EMAIL_FROM$, EMAIL_TO$, SUBJECT$,cc1$,cc2$)
		Print #outChan;"Compare SORDLOT Key to Data for company " + nam$
		print #outChan;""
		print #outChan;"Key#  Key Value"
		print #outChan;"Rec #";tab 12;"Message"
		print #outChan;"-------------------"
	else
		print #outChan;"****"
	End If 
	Print #outChan;keyNo;"     ";ksord$
	print #outChan;r;tab 12;message$
	mflg = 1
Return 


OutEnd: !
close
chain ""
Stop 

include "src/utilerr.i"

else
	goto Err_Trap:
end try