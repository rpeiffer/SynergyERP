! loadsave -w -n 100,10 -o prog/util/chksprodlot.dl4 src/chksprodlot.src

! check for mismatches in sprodlot record - email back

Option Default Dialect IRIS1
Declare Intrinsic Function FindChannel

External Lib "utillib.lib"
Declare External Function ParseArguments
Declare External Function OpenEmail

try
Dim K1$[60],K2$[50],P0$[66],MSG$[100],C$[16],f1$[16]
Dim 1%,P1[1],2%,P2[2],3%,P3[4],PLTR

Dim EMAIL_FROM$[50],EMAIL_TO$[50],SUBJECT$[50]
Dim CC1$[50],CC2$[50],nam$[30]
dim args$[5,80],message$[80]


argc = ParseArguments(args$[], msc$(4))
! First arg is company
intCo = args$[1]
if intCo < 1 or intCo > 99
	print "Invalid company number.  Exiting."
	end
end if

gosub OpenFiles:
Mat Read #ch_ctrl,3,0;nam$
gosub SearchKey1:
goto OutEnd:

!--------------------------------------------------------------------------
! S U B R O U T I N E S
!--------------------------------------------------------------------------

OpenFiles:
	C$ = intCo Using "cntrl/CNTRL#"
	If intCo > 9 Let C$ = intCo Using "cntrl/CNTRL##"
	ch_cntrl = FindChannel()
	Ropen #ch_ctrl,C$
	Mat Read #ch_ctrl,88,2528;F1$;
	ch_sprod = FindChannel()
	Ropen #ch_sprod,F1$
Return

SearchKey1: !
	K1$ = " ",K1$
	do
		Search #ch_sprod,3,1;K1$,R,E \ If E>2 gosub err_search
		if E exit do
		Mat Read #ch_sprod,R,0;P0$;
		Mat Read #ch_sprod,R,66;P1;
		Mat Read #ch_sprod,R,70;P2;
		Mat Read #ch_sprod,R,82;P3;
		mat read #ch_sprod,r,120;pltr;
		If K1$[1,5] = "STOCK" ! CHK LABEL
			WH = K1$[6,7]
			KWH = K1$[23,24]
			If WH <> KWH Let MSG$ = "LOT VS. WH " + K1$ + " REC " + Str$(R) \ Gosub LogIt
		End If 
		If K1$[31,42] <> P0$[1,12]
			MSG$ = "PRODUCT MISMATCH " + K1$ + " REC " + Str$(R) + " FILE " + P0$[1,12]
			Gosub LogIt
		End If 
		If K1$[1,12] <> P0$[13,24]
			MSG$ = "LOT MISMATCH " + K1$ + " REC " + Str$(R) + " FILE " + P0$[13,24]
			Gosub LogIt
		End If 
		WH = K1$[23,24]
		If WH <> P1[0]
			MSG$ = "WHSE MISMATCH " + K1$ + " REC " + Str$(R) + " FILE " + Str$(P1[0])
			Gosub LogIt
		End If 
	loop
return



EmailOpen: !
	EMAIL_FROM$ = "chksprodlot@test.com"
	EMAIL_TO$ = "rich@ubsys.com"
	SUBJECT$ = "srpodlot key/data issue " + nam$
	! CC1$ = "txc@ubsys.com"
	outChan = OpenEmail(EMAIL_FROM$, EMAIL_TO$, SUBJECT$)
	Print #outChan;"Compare SPRODLOT Key to Data for company " + nam$
	print #outChan;""
Return 


LogIt:
	if not(outOpen)
		gosub EmailOpen
		OutOpen = 1
	end if
	Rem write log
	print #outChan;"****"
	MSG$ = MSG$ + " DATER " + Str$(P2[0])
	Print #outChan;MSG$
	nonZero = 0
	for i = 0 to 4
		if i <> 2 ! don't check average cost
			if p3[i] <> 0 nonZero = 1
		end if
	next i
	if pltr <> 0 nonZero = 1
	if nonZero
		print #outChan;"   ***";
		if p3[0] print #outChan;"  Recv'd: "+str$(p3[0]);
		if p3[1] print #outChan;"  Used: "+str$(p3[1]);
		if p3[3] print #outChan;"  Allocated: "+str$(p3[3]);
		if p3[4] print #outChan;"  Back Ord: "+str$(p3[4]);
		if pltr print #outChan;"  Xfer: "+str$(PLTR);
		print #outChan;""
	end if
Return


OutEnd:
Close
chain ""
stop


include "src/utilerr.i"

else
	goto Err_Trap:
end try