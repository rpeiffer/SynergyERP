!
! loadsave -w -n 100,10 -o prog/util/ea_stub.dl4 src/ea_stub.src
!
!-------------------------------------------------------------------
! ERP Assistant Stub
!
! This progam prepares the environment for programs that are to be run
! via the ERP assistant
!
! 1. Open the printer on channel 0 if required
! 2. Prepare passed parameters via globals
!
!-------------------------------------------------------------------

include "src/utilcopyright.inc"
External Lib "utillib.lib"
Declare External Sub AddSessionParameter

External Lib "libgeneral.lib"
Declare External Function GetParameter$,OpenPrinter

External Lib "ubsfunc.dl4"
Declare External Sub LogAccess

Declare Sub SetOption

try
dim e$[300],tmp$[100]

!
! M A I N   L O G I C
!
call env(1,"ERP_COMPANY",tmp$)
intCo = tmp$
if intCo < 1 or intCo > 99
	print "Invalid company number.  Exiting."
	goto OutEnd
end if
! before we get any input / run anything, must
! set session info up which also gets parameters
! passed in
call SetUtilSession(e$,intCo,1)
call SetOption()
if GetParameter$("PRINTER") <> ""
	printchan = OpenPrinter(e$,0,1)
end if

OUTEND: Rem"========= exit
chain GetParameter$("PROGRAM")
End
else ! MAIN try statment
	include "src/utilcallmainerr.inc"
end try

!-----------------------------------------------------------------------
! subroutines and functions
!-----------------------------------------------------------------------

sub SetOption()
try
	select case GetParameter$("PROGRAM")
		case "FMMENU.DL4","POMENU.DL4","OEMENU.DL4","FORMRNT.DL4","PRODM.DL4","PRODWH.DL4"
			tmp$ = GetParameter$("PROGRAM") to "."
			call AddSessionParameter(e$,"OPTION",tmp$)
		case "SERPAP.DL4","SERPCPI.DL4","SERPGL.DL4","SERPMRTS.DL4","SERPORDH.DL4","SERPORDL.DL4"
			tmp$ = GetParameter$("PROGRAM") to "."
			call AddSessionParameter(e$,"OPTION",tmp$)
		case "SERPPOH.DL4","SERPPOL.DL4","SERPQTEH.DL4","SERPQTEL.DL4","SERPRBT.DL4","SERPSPCT.DL4"
			tmp$ = GetParameter$("PROGRAM") to "."
			call AddSessionParameter(e$,"OPTION",tmp$)
		case "SERPSPR.DL4","SLMENU.DL4","SNMENU.DL4","USRCTRLM.DL4","VENDM.DL4","VENDWH.DL4","WHMENU.DL4"
			tmp$ = GetParameter$("PROGRAM") to "."
			call AddSessionParameter(e$,"OPTION",tmp$)
		case "MYREPORTS.DL4"
			call AddSessionParameter(e$,"OPTION","MYREPORT")
		case "LINUX"
			! non-DL4 programs - log anyway
			call AddSessionParameter(e$,"OPTION","LINUX")
			Call LogAccess(e$,"LINUX PROGRAM: "+GetParameter$("ACTION"))
		case else
			! assume block ID
			call AddSessionParameter(e$,"OPTION","DXBLOCKX")
			call AddSessionParameter(e$,"ACTION1","PRINT")
			! by convention, action is block ID
			tmp$ = GetParameter$("ACTION")
			if tmp$ = "" ! not set, just use the program being run
				tmp$ = GetParameter$("PROGRAM") to "."
			end if
			call AddSessionParameter(e$,"BLOCKID",tmp$)
			! ERP Scheduler progams that call block IDs
			! do not pass through dxblockx, so they
			! bypass program logging (not good)
			! so log 'em here
			Call LogAccess(e$,"ERP SCHEDULER: ASSUMED BLOCK ID")
	end select
else
	include "src/utilcallsuberr.inc"
end try
end sub
