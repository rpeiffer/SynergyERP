! loadsave -w -n 100,10 -o prog/util/task19432.dl4 src/task19432.src

! check for mismatches in salesrep key vs. data for credit memos

Option Default Dialect IRIS1
Declare Intrinsic Function FindChannel

External Lib "utillib.lib"
Declare External Function ParseArguments
Declare External Function OpenEmail

! Local Subroutines
Declare Sub GetCommandParameters,OpenFilesReadCntrl,SearchCreditMemos
Declare Sub OpenOutputChan,LogIt,PrintOutput

try
Dim K_ROH$[60],K_ROHS$[60],f1$[16]
Dim 2%,H0[12],intCo,noEmail
Dim 3%,R_ROH,R_ROHS

Dim EMAIL_FROM$[50],EMAIL_TO$[50],SUBJECT$[50]
Dim CC1$[50],CC2$[50],nam$[30]
dim args$[5,80],message$[80]


call GetCommandParameters()
if not(intCo) goto OutEnd:
call OpenFilesReadCntrl()
call SearchCreditMemos()
OutEnd:
Close
chain ""
stop

!--------------------------------------------------------------------------
! S U B R O U T I N E S
!--------------------------------------------------------------------------

Sub GetCommandParameters()
	argc = ParseArguments(args$[], msc$(4))
	! First arg is company
	intCo = args$[1]
	if intCo < 1 or intCo > 99
		print "Invalid company number.  Exiting."
		intCo = 0
		exit sub
	end if
	if argc>1 noEmail = args$[2]
End Sub

Sub OpenFilesReadCntrl()
	F1$ = intCo Using "cntrl/CNTRL#"
	If intCo > 9 Let F1$ = intCo Using "cntrl/CNTRL##"
	ch_cntrl = FindChannel()
	Ropen #ch_ctrl,F1$
	Mat Read #ch_ctrl,88,1840;F1$;
	ch_roh = FindChannel()
	Ropen #ch_roh,F1$
	Mat Read #ch_ctrl,3,0;nam$
End Sub

sub SearchCreditMemos() !
	K_ROH$ = " ",K_ROH$
	K_ROH$[1,2] = 9 using "##"
	do
		Search #ch_roh,3,1;K_ROH$,R_ROH,E
		if E exit do
		status = val(k_roh$[1,2])
		if status <> 9 and status <> 10 and status <> 11 exit do
		Mat Read #ch_roh,r_roh,0;h0;
		K_ROHS$ = " ",K_ROHS$
		K_ROHS$[1,3] = H0[11] using "###"
		K_ROHS$[4,9] = K_ROH$[3,8]
		search #ch_roh,2,3;k_rohs$,R_ROHS,E
		if e
			call LogIt("Missing Salesrep Key: " + K_ROH$ + " REC: " + Str$(R_ROH) + " REP: " + Str$(h0[11]))
		else if R_ROH <> R_ROHS
			call LogIt("Salesrep Key Record # Mismatch: " + K_ROH$ + " REC: " + Str$(R_ROH)+" SKEY REC: "+Str$(R_ROHS))
		end if
	loop
End Sub

sub SearchForRecord()
	K_ROHS$=" ",K_ROHS$
	do
		search #ch_roh,3,3;k_rohs$,R_ROHS,E
		if E exit do
		if R_ROHS = R_ROH exit do
	loop
End Sub


sub OpenOutputChan()
	if not(noEmail)
		EMAIL_FROM$ = "task19432@test.com"
		EMAIL_TO$ = "bill@ubsys.com"
		SUBJECT$ = "credit memo salesrep key issue " + nam$
		CC1$ = "rich@ubsys.com"
		CC2$ = "rce@ubsys.com"
		outChan = OpenEmail(EMAIL_FROM$, EMAIL_TO$, SUBJECT$, CC1$, CC2$)
	end if
	call PrintOutput("Compare Salesrep Creit Memo Data for company " + nam$)
	call PrintOutput("System Date: "+msc$(0))
	call PrintOutput("")
End Sub

sub PrintOutput(outStr$)
	if noEmail
		print outStr$
	else
		print #outChan;outStr$
	end if
End Sub

sub LogIt(msg$)
	if not(outOpen)
		call OpenOutputChan()
		OutOpen = 1
	end if
	Rem write log
	call PrintOutput("****")
	call PrintOutput(msg$)
	call SearchForRecord()
	if R_ROH = R_ROHS ! found a matching record number
		call PrintOutput("Found Salesrep Key with Same Rec# - KEY: " + K_ROHS$)
	else
		call PrintOutput("NO KEY IN SALESREP DIRECTORY HAS THIS REC #")
	end if
End Sub


include "src/utilerr.i"

else
	goto Err_Trap:
end try