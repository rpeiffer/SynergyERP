! Rebuild / Expand scashfle and scashfle File from 86w to 125w --
!
! loadsave -w -n 100,10 -o prog/util/pmu_expcashfle.dl4 src/pmu_expcashfle.src
!
! PMUPGRADER ROUTINE

external lib "ubsfunc.dl4"
declare intrinsic sub programdump
Declare Intrinsic Sub InpBuf,String,Time
! Option Default Dialect IRIS1
include "src/utilcopyright.inc"
dim e$[300]

ENTER COID,GROUPID,DBAK$,CH_LOG,COREDUMP,E,...
let manflag=0 ! if manually running set manflag to 1 and remove enter line
if manflag<>0 
	dim dbak$[500],logname$[500]
	let dbak$="files/databak/" \ let coid =1 \ let groupid=0
	let logname$="/tmp/expcashlog"
	let learnflag=0
	if learnflag<>0 let logname$=logname$+"lrn"
	build #0,+logname$
	close #0
	open #0,logname$
endif
! if "fromUpgrade" don't send print statements with
! control strings as this is being executed via
! a job not attached to terminal.  Be sure to send
! all output to ch_log as this will be reviewed and
! sent back to software repository
try
	enter fromUpgrade
else
	dim 1%,fromUpgrade
end try
try ! main try
LET E=99
!
include "src/dim.pmupgrade.i"

DIM ANS$[20],FILE$[20],FT$[2],X$[20],CMD$[256]
DIM K1$[30],LU$[2],NK1$[30],NK2$[50],K2$[50]
DIM 1%,FLAG,K1SIZE,K2SIZE,DSLS$[200]
DIM Ftemp$[16],F$[16]
DIM 1%,FFILE$[200],WORK$[200]
DIM 2%,D3[1],X2,CR[1],cpiholddate
DIM 3%,R1,R2,R3

DIM 2%,ARDT,DATE
DIM 3%,EMONTH,EYEAR,EDATE,ARDATE,ARYEAR,ARMONTH,CHKDATE,MONTHS[12]
DIM LONGYEAR$[10],SHORTYEAR$[10]
DIM BLANK$[60]
DIM 1%,BNK[1],3%,CSINVN0,SPARE1
! CASHFLE VARIABLES
dim 1%,c1[2],c4,currid
dim 2%,c2[8],c5[3]
dim 3%,c3[6],currfact,currvar
dim c$[70],spareme$[63]


DIM 3%,E
dim list$[1],listline$[100]

LET BLANK$=" ",BLANK$
LET TRUE=1;FALSE=NOT(TRUE)
try close #1 else rem
ROPEN #1,"cntrl/cntrl"+STR$(COID)
LET ROW=23;COL=0

! ***** cashfle file ****
mat read #1,88,1536;ftemp$;
LET ORIG$=""
LET OWRITE$="ne 'multcurr'"
LET ROW=23;COL=0
Open #2,Ftemp$
Let X2=CHF(302) ! "rec size in words
Close #2
UPDATE=0
If X2>=125 ! "it's already done
	Let LOGMSG$="File "+Ftemp$+" is already expanded - bypassing"
	Gosub DO_WRITE_LOG:
	Goto DO_EXIT
Endif
LET FCODE$="CASHFLE"
LET MODE=0
GOSUB DO_BACKUP:
ffile$=ftemp$


CONVERT_FILE: REM"Convert the given file
LET ROW=23;COL=0
if not(fromUpgrade) PRINT @COL,ROW;"Converting "+FFILE$;
LET COL=COL+13+LEN(FFILE$)
OPEN #2,DFILE$ ! open file in new location
LET CH=2 \ GOSUB GET_ACTIVE_RECS:
LET LOGMSG$="Estimated number of records to process: "+STR$(ACTRECS)
GOSUB DO_WRITE_LOG:
LET FCODE$="CASHFLE"
LET PTH$=""
LET OWRITE$="y"
LET WORK$=DFILE$
GOSUB DO_BUILD:
LET FFILE$=DFILE$
LET DFILE$=WORK$
system "chmod go+rw files/2/scashfle"+str$(coid) ! all -- for permissions
system "chmod go+rw files/2/scashfle"+str$(coid)+".idx" ! all -- for permissions
OPEN #3,FFILE$
LET K1$=" ",K1$;REC=0

OLDFLELOOP: REM "Loop thru all old Dir 1
	SEARCH #2,3,1;K1$,R2,E \ IF E=2 GOTO Copydone:
	IF E Error 11000
	LET ch_cash=2;rec_cash=R2
	GOSUB READ_rec:	
	LET CURRID=0
	LET CURRFACT=0;currvar=0
	LET SPAREME$=" ",SPAREME$
	LET NK1$=" ",NK1$
	LET NK1$=K1$
	SEARCH #3,2,1;NK1$,R3,E \ IF E=0 GOTO OLDFLELOOP:
	IF E>1 error 11000
	LET E=2;R3=0
	SEARCH #3,1,0;NK1$,R3,E \ IF E error 11000
	SEARCH #3,4,1;NK1$,R3,E \ IF E error 11000
	LET ch_cash=3;rec_cash=R3
	GOSUB WRITE_rec:
	LET REC=REC+1
	IF NOT(FRA(REC/100)) GOSUB DISPLAY_PROGRESS:
GOTO OLDFLELOOP:







READ_rec: REM Read File
	MAT  READ #CH_CASH,REC_CASH;C1;
	MAT  READ #CH_CASH,REC_CASH,6;C2;
	MAT  READ #CH_CASH,REC_CASH,42;C4;
	MAT  READ #CH_CASH,REC_CASH,44;C3; 
	MAT  READ #CH_CASH,REC_CASH,86;C$;    
	MAT  READ #CH_CASH,REC_CASH,156;C5;		
RETURN 

WRITE_rec: REM Write
	MAT  WRITE #CH_CASH,REC_CASH;C1;
	MAT  WRITE #CH_CASH,REC_CASH,6;C2;
	MAT  WRITE #CH_CASH,REC_CASH,42;C4;
	MAT  WRITE #CH_CASH,REC_CASH,44;C3; 
	MAT  WRITE #CH_CASH,REC_CASH,86;C$;    
	MAT  WRITE #CH_CASH,REC_CASH,156;C5;
	MAT  WRITE #CH_CASH,REC_CASH,172;CURRID;
	MAT  WRITE #CH_CASH,REC_CASH,174;CURRFACT;
	Mat  Write #ch_cash,Rec_cash,180;currvar;
	MAT  WRITE #CH_CASH,REC_CASH,186;SPAREME$
RETURN 


COPYDONE: REM "Copy is Done 
XferDone: ! "
LET LOGMSG$="Finished with file expand & transfer"
GOSUB DO_WRITE_LOG:
CLOSE #2,#3
Goto DO_EXIT:



DO_EXIT: REM---------------- Exit the upgrade -----------------------------------
LET E=0
GOTO OUTEND:

DO_BACKUP: REM"Backup the given file
CALL "sysfbackup",FCODE$,COID,GROUPID,ORIG$,DBAK$,OWRITE$,MODE,ROW,COL,CH_LOG,DFILE$,REASON$,E,fromUpgrade
IF E GOTO OUTEND:
RETURN 

DO_BUILD: REM"Build the given file
CALL "Sysfbuild",FCODE$,COID,GROUPID,PTH$,OWRITE$,ROW,COL,CH_LOG,DFILE$,REASON$,E,fromUpgrade
IF E GOTO OUTEND:
RETURN 

include "src/rtn.pmupgrade.i"


OUTEND: REM"Final exit (be sure to set e=0 if successful before branching here)
try CLOSE #1 Else Rem
try Close #2 Else Rem
try Close #3 Else Rem
END 
else ! main try
	e = spc(8)
	If Spc(8) <> 10000
		dim msc4$[100] \ msc4$=msc$(4)
		if Pos(msc4$, = "/",-1) let msc4$=msc4$[pos(msc4$,="/",-1)+1]
		call env(2,"PROGNAME",msc4$)
		if spc(8) = 11000
			call utilsearcherr(e$, e, Spc(10))
		else
			Call utilsuberr(e$,Spc(8),Spc(10),Msc$(2))
		end if
		call programdump()
	End If
	Call utilmainerr(e$,Spc(8))
	If ch_log <> -1
		Print #ch_log;e$
	End If
end try