! CustShpSales.dl4
!
! Check / Delete CSLSALES for Customer & Shiplist call
!
! 1.0 03/02/2009
!
! loadsave -w -n 100,10 -o prog/dxport/custshpsales.dl4 src/custshpsales.src
!
!
include "src/copyright.inc"
Rem CustShpSales - call for customer/shiplist sales
Rem cct 201662 - Leonard paper
Rem CALL "CUSTSHPSALES",CUST,SHIP,MODE,CHAN,FLAG
! Cust=customer
! SHIP=Shiplist # (-1 means ALL FOR THE CUSTOMER)
! MODES: 0=check if sales present (flag=0 NO, =1 YES)
!        9=Delete any records in 4/CSLSALES# file
! CHAN=Channel 4/cslsales is opened on (0=none)
! FLAG= return value to calling program (mode dependent)
Rem BEDIT Version: 2.2 OBJECT CODE     Created: FEB 19, 2008  12:39:07
Rem /w2/papermaster/r2005//usr/acct/jcs/work/custom/nelson.r9/custshpsales.src (univ2) [jcs] {06098}
Rem  Copyright (c) 2008 Universal Business Systems, Inc.
Rem    **** ALL CHANGES MUST BE MADE IN SOURCE!!! ****
Rem
Declare Intrinsic Sub InpBuf,String
! Option Default Dialect IRIS1

Enter CUST,SHIPLIST,MODE,CSCHAN,FLAG,e$,IntCo
Rem {begin src/inc/dim.getchan.i}
Dim 1%,CHNL
Dim 3%,CHNLSCR,CHNLFREE
Rem {end dim.getchan.i}
Dim buttonlist$[5,50],nextlist$[5,100],Msg$[100] !error handling variables
Dim K$[60],K1$[60],K2$[60]
Dim FLE$[20]
Dim 1%,WHSE,COID,X[9],CH_CSL
Dim 2%,C1,SHPTO,X2[9]
Dim 3%,S1[12,2],S2[12,2],R[99]
COID = IntCo ! Int((Spc(5) - Int(Spc(5) / 16384) * 16384) / 64)
If CSCHAN = 0 ! "no channel passed - open 1
  CHNL = 89 \ Gosub GETCHAN
  CH_CSL = CHNL
  FLE$ = "4/CSLSALES" + Str$(COID)
  Open #CH_CSL,FLE$
Else 
  CH_CSL = Abs(CSCHAN)
End If 
If CUST = 0 Goto OUTEND
If MODE = 0 ! "just look for any sales in past 24 months
  FLAG = 0
  K$ = " ",K$;K$[1,6] = CUST Using "######"
  If SHIPLIST >= 0 ! "use shiplist
    K$[7,12] = SHIPLIST Using "######"
  End If 
  K$[13] = ""
CHKSLS_LOOP: Rem "til find one or diff cust/shiplist
  Search #CH_CSL,3,1;K$,R[89],E \ If E = 2 Goto CCSL_DONE
  If E Gosub ERR_SEARCH
  X2 = K$[1,6] \ If X2 <> CUST Goto CCSL_DONE
  If SHIPLIST >= 0 ! "used shiplist
    X2[1] = K$[7,12] \ If X2[1] <> SHIPLIST Goto CCSL_DONE
  End If 
  Mat Read #CH_CSL,R[89],16;S1;
  Mat Read #CH_CSL,R[89],256;S2;
  For X = 1 To 12
    If S1[X,0] Or S1[X,1] Or S1[X,2] Let FLAG = 1
    If S2[X,0] Or S2[X,1] Or S2[X,2] Let FLAG = 1
  Next X
  If FLAG Goto CCSL_DONE
  Goto CHKSLS_LOOP
CCSL_DONE: Rem "finished
End If 
If MODE = 9 ! "delete all for customer & shiplist #
  FLAG = 0
  K$ = " ",K$;K$[1,6] = CUST Using "######"
  If SHIPLIST >= 0 ! "use shiplist
    K$[7,12] = SHIPLIST Using "######"
  End If 
  K$[13] = ""
DELSLS_LOOP: Rem "til diff cust/shiplist
  Search #CH_CSL,3,1;K$,R[89],E \ If E = 2 Goto DCSL_DONE
  If E Gosub ERR_SEARCH
  X2 = K$[1,6] \ If X2 <> CUST Goto DCSL_DONE
  If SHIPLIST >= 0 ! "for this shiplist
    X2[1] = K$[7,12] \ If X2[1] <> SHIPLIST Goto DCSL_DONE
  End If ! "all for the customer
  K2$ = " ",K2$
  K2$[1,12] = K$[13,24]
  K2$[13,24] = K$[1,12]
  K2$[25,26] = K$[25,26]
  Search #CH_CSL,5,2;K2$,R,E \ If E > 1 Gosub ERR_SEARCH
  K1$ = " ",K1$;K1$ = K$
  Search #CH_CSL,5,1;K1$,R,E \ If E Gosub ERR_SEARCH
  E = 3;R = R[89]
  Search #CH_CSL,1,0;K$,R,E \ If E Gosub ERR_SEARCH
  FLAG = FLAG + 1
  Goto DELSLS_LOOP
DCSL_DONE: Rem "finished
End If 
OUTEND: Rem "done - goodbye
If Not(CSCHAN)
  Close #CH_CSL
End If 
End 
Rem {begin src/inc/rtn.getchan.i}
GETCHAN: Rem search for open channel (counts down from supplied chnl #)
For SCR = CHNL To 1 Step -1
  CHNL = SCR
  Gosub CHNLFREE
  If CHNLFREE
    SCR = 1
  Else 
    CHNL = 0
  End If 
Next SCR
Return 
CHNLFREE: Rem check to see if a channel is being used
If Err 0 Let CHNLFREE = Spc(8)
CHNLFREE = 0
CHNLSCR = Chf(CHNL)
If Err 0 Gosub ERR_TRAP
If CHNLFREE And CHNLFREE <> 49 Gosub ERR_TRAP
END_CHNLFREE: Rem
Return 
Rem {end rtn.getchan.i}
ERR_SEARCH: Rem
ENUM = E ;ELINE = Spc(16);CTERR = 0
If E = 5 Let CTERR = Err(8)
msg$ = "RETURN STATUS",(E + (CTERR * .001))
msg$ = msg$,"/ STATEMENT",ELINE," IN PROGRAM "+Msc$(4)
E$=msg$
Call programdump("tmp/custshpslsse!","")
goto err_main
ERR_MAIN: Rem
e$ = msg$
Err_trap: ! 
! If Err 0 Rem

include "src/callsuberr.inc"
End
Rem {end rtn.error.s}