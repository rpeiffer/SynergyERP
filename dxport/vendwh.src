! --- vendwh
! description Vendor warehouse main programs 
!
! loadsave -w -n 100,10 -o prog/dxport/vendwh.dl4 src/vendwh.src
!
! 1.0  10/10/07 new
!
!include copyright, common libs, common intrinsics
include "src/copyright.inc"
!
!include structure,filehandler libs & default structure dims
!
include "src/inc/filevendwhse.inc" ! Vendor warehouse
include "src/inc/filevendtag.inc" ! vendor add'l
include "src/inc/filebuyer.inc" ! buyer
include "src/inc/filea80vm.inc" ! vendor
include "src/inc/filewhinfoz.inc" ! whse info

! *declare additional dl4 intrinsic subs & functions
Declare Intrinsic Function FindChannel
Declare Intrinsic Sub programdump
!
! *declare additional external libs & subs & functions
!
External Lib "ubsfunc.dl4"
Declare External Function OpenFile,GetUIDRec
Declare External Sub UserCntrl
!
External Lib "ubsprconv.dl4"
Declare External Function XAmount, Xunit$
!
!
External Lib "libgeneral.lib"
Declare External Sub SubErr,CreateNetStatus
!
!
Declare Sub OpenFiles,whedlist
Declare Sub VWMain,vwhsesave,VWhseDel
Declare Sub VWhseCopy,sndlists
Declare Function Vendzip9$
!
!--------------------------------------------------------------------
!
!
! **main procedure
!
!
Try
  Option String Redim is legal ! in case subs dim/use same names
  !
  dim e$[500],buttonlist$[5,50],nextlist$[5,100] !error handling variables
  !
  Dim 1%,X1[9],tmp1,UCA5[10],status,yy
  dim 2%,X2[9],tmp2,maxcnt \ maxcnt=50 ! init max records in arrays for list
  Dim 2%,CurrDate
  Dim 3%,amount,COST_LEV[3],COMM_LEV,COLLSEC,HIDECOST,VendRec
  dim 3%,X3[9],tmp3,WHRec
  !
  dim 3%,vendid,vendid$[10],mode$[4]
  dim action$[30],options$[30],userid$[8],b$[200],action1$[30]
  Dim bsdel$[20],esdel$[20],rdel$[20],fdel$[20],rstr$[2000]
  dim Field$[1,30],action2$[30],TUID$[10]
  dim tmp$[1200],tmp1$[300],P9$[50],P60$[50],P61$[256]
  Dim Vend$[6],WH$[2],UDAN$[56]
  dim Message$[200],WebStr$[1200]
  dim SearKey$[64],VendKey$[64],Key1$[60],Key2$[60]
  dim Blank$[200] \ Blank$=" ",Blank$
  dim M2$[2] \ M2$="##"
  dim M32$[6] \ M32$="###.##"
  dim M52$[8] \ M52$="#####.##"
  dim M6$[6] \ M6$="######"
  dim M92$[12] \ M92$="#########.##"
  dim blank$[200] \ blank$=" ",blank$
  !
  dim List$[maxcnt,1000] ! for .net 
  !
  dim WH. as vendwhse ! Vendor warehouse
  dim BYR. as buyer ! buyer
  dim VM. as a80vm ! vendor
  dim vtag. as vendtag ! for new - for defaults
  dim whi. as whinfo
  !
  call dxopen()
  !
  call GetSession(e$,CTLC,options$,action$,UserID$,intCo,intSls,fdel$,rstr$,bsdel$,esdel$,rdel$,action1$,action2$)
  !
  call dxget("vendid",vendid$) \ vendid=vendid$
  
  call OpenFiles()

  MAT READ #CTLC,19,50;P9$;
  MAT READ #CTLC,60,50;P60$;
  MAT READ #CTLC,61,0;P61$;
  MAT  READ #ctlc,68,56;UDAN$;
  tmp$=tim(8) using "&&"
  tmp$=tmp$+tim(9) using "&&"
  tmp$=tmp$+tim(10) using "&&" ! yymmdd
  currdate=tmp$
  tmp$=UserID$+Blank$
  If RTRIM$(TMP$)=""
	Call DXGet("S_USER.ID",tmp$) ! get from system variable
  Endif
	Let UserID$=UCase$(tmp$) ! make sure it's UPPERCASE as that's what PM uses
	let TUID$=UserID$+"          " ! set up temp var
	If Len(TUID$)>10 Let TUID$[11]="" ! Cut it at max as usercntrl is 10 chars vs 8 for .net
  UCRec=GetUIDRec(e$,IntCo,TUID$) ! for edit by
  !
  ! get vendor record
  vend$=vendid using "######"
  VendKey$=vendid using "######"
  VendRec=filegeta80vm(e$,vmc,"=",1,VendKey$,vm.) ! vendor mode=2 dir=1
  vtR = filegetvendtag(e$,vtC,"=",1,vend$,vtag.)
	if vtR<=0 ! "not found
		Clear vtag.
	end if
  !
  action$=UCase$(action$) ! make sure UPPERCASE
  action$=RTrim$(action$) ! trim trailing spaces
  Action1$=ucase$(Action1$) ! get action1 - make UPPERCASE
  Action1$=RTrim$(action1$) ! trim trailing spaces
  action2$=UCase$(action2$) ! make sure UPPERCASE
  action2$=RTrim$(action2$) ! trim trailing spaces
  !
  IF P61$[63,63]<>"Y" ! NOT ON
	returnstatus=-1
	Message$="Vendor Whse screen is not active"
	goto WHPDone
  Endif
  !Call dxsave(0,"/tmp/vwhsein.txt!") !   Save data sent from web
  !Call programdump("tmp/vwlog1in!","") ! dump program vars
  if action$<>"INPUT"
    call dxget("warehouse",WH$) \ tmp1=WH$ \ WH$=tmp1 using "##"
	SearKey$=Vend$,WH$
	WhseNo=tmp1
	WHRec=filegetVendwhse(e$,WHC,"=",1,SearKey$,WH.)
  endif
  If action2$<>"" ! not inquiry - is ADD,EDIT,COPY,SAVE
	if vendrec<0 ! MUST BE VALID VENDOR
		returnstatus=-1
		Message$="VENDOR NOT FOUND"
		goto WHPDone
	Endif
	
	close #WHC ! close read-only
	WHC = OpenFile(9961,intCo) \ If WHC = -1 Error 42 ! vendwhse as read/write
  Endif
  !
  Returnstatus=-1 ! flag program error
  Message$="Action not found"
  Select Case action$
    Case "INPUT" ! get whse list							doc=VendorWarehouseInput.doc
	  !
	  returnstatus=1
	  Message$="OK"
	  Field$[0]="Warehouse" ! warehouse 
	  SearKey$=Vend$
	  !
	  if action2$="" ! inquiry
	    call filedroplistVendwhse(e$,list$[],maxcnt,WHC,"WHDropList",Field$[],1,SearKey$)
	  Else ! send all available								doc=Vendwh-GetVWhse.doc
		call whedlist()
	  Endif
	  call AddToStr(e$,rstr$,List$[])
	  call SetOutPut(e$,rstr$)
	  !
    Case "MAIN" !	Inquiry									doc=VendWhseMain.doc
      !		entry/edit										doc=Vendwh-GetMain.doc
	  returnstatus=1
	  Message$="OK"
	  !
	  ! vend information section, ID and desc
	  clear List$[]
	  row=0
	
	  call VWMain()
	  call AddToStr(e$,rstr$,List$[])
	  !
      If action2$<>"" Goto VMainDone ! not inquiry mode
      !
	  If WHRec<0 or VendRec<0
	    ReturnStatus=0
		tmp$="Can't get "
		If WHRec<0 let tmp$=tmp$,"Warehouse record "
		If VendRec<0 let tmp$=tmp$,"Vendor record "
		Message$=tmp$
	  else
	    ReturnStatus=1
		Message$="OK"
	  endif
	  VMainDone: ! bypass whserec & vendrec check on ADD,EDIT,COPY,DELETE
	  !
	  call CreateNetStatus(e$,ReturnStatus,Message$,WebStr$)
	  call AddToStr(e$,rstr$,WebStr$)
	  !
	  call SetOutPut(e$,rstr$)
	Case "DLISTS" ! Lists for entry/edit
		returnstatus=1
		Message$="OK"
		call sndlists()
		call AddToStr(e$,rstr$,List$[])
		call CreateNetStatus(e$,ReturnStatus,Message$,WebStr$)
	  call AddToStr(e$,rstr$,WebStr$)
	  !
	  call SetOutPut(e$,rstr$)
	  ! end of dlists

  End Select
  WHPdone: ! finito a place for bypass
  If returnstatus=-1 ! no action found here or a problem
	returnstatus=0
	call CreateNetStatus(e$,ReturnStatus,Message$,WebStr$)
	call AddToStr(e$,rstr$,WebStr$)
	!
	call SetOutPut(e$,rstr$)
  Endif
else
 include "src/callmainerrnet.inc"
end try
end  ! end of Main
!
!--------------------------------------------------------------------
sub OpenFiles()
  !
  ! Open files for vend warehouse inquiry
  !
  Try
    !
	CTLC = OpenFile(-9999,intCo) \ If CTLC = -1 Error 42 !control
	WHC = OpenFile(-9961,intCo) \ If WHC = -1 Error 42 ! vendwhse
 	VMC = OpenFile(-2400,intCo) \ If VMC = -1 Error 42 ! vendor file
	VtC = OpenFile(-1120,intCo) \ If vtc = -1 Error 42 !vendtag
	BYC = OpenFile(-9991,intco) \ If BYC = -1 Error 42 ! buyer
	WIC = OpenFile(-2768,intco) \ if WIC=-1 Error 42 ! whse info
	!
  else
    include "src/callsuberr.inc"
  end try
end sub ! openfiles
! 
!--------------------------------------------------------------------
Sub whedlist()
! send list of whses(all) with status
  Try
	Clear List$[]
	tmpcnt=maxcnt
	List$[0]=bsdel$,"WHSELIST",fdel$
	List$[1]="ID",fdel$,"WHSE",fdel$,"WHREC",fdel$
	row=2
	for r=0 to 98
		try 
			read record #WIC,R,0;whi.;
		Else
			clear whi.
		End try
		tmp$=RTrim$(whi.WhName$)
		if tmp$<>"" ! ok has name
			searkey$=vend$,(R+1) using "##"
			WHrec=filegetvendwhse(e$,WHC,"=",1,searkey$,wh.)
			! set string
			if whrec<0 let whrec=0
			List$[row]=SearKey$,fdel$,Str$(R+1),fdel$,Str$(whrec),fdel$
			row=row+1
			if row>tmpcnt let tmpcnt=expandarray(e$,List$[]) 
		Endif
	Next R
	list$[row]=esdel$
  else
    include "src/callsuberr.inc"
  end try
end sub ! whedlist
! 
!--------------------------------------------------------------------
Sub VWMain()
! main Whse data sub
  Try
	if action2$="ADD" if WHRec>0 !requesting to add vendid, already on file, error
	  !e$="msgvendAOF" \error 10000 !Vendor already on file!
		returnstatus=0
		Message$="Warehouse exists!"
		goto VWDone
	 end if
	 if Vendrec<0 ! normal mode - no vend found
		returnstatus=0
		Message$="Vendor NOT FOUND"
		goto VWDone
	Endif
	if vendid=0 ! NO BLANK VENDOR CODES!
		!E$="msgvendinvalid" \ error 10000
		ReturnStatus=0
		Message$="ZERO VENDOR CODE NOT ALLOWED"
		goto VWDone
	Endif
	if whseno=0 ! no blank Warehouses!
		ReturnStatus=0
		Message$="ZERO WAREHOUSE CODE NOT ALLOWED"
		goto VWDone
	Endif

	!!Start!Dci2

	If action2$="SAVE" ! send data								doc=Vendwh-SubmitVWhse.doc
		close #whc
		WHC = OpenFile(9961,intCo) \ If whc = -1 Error 42 !vend
		call vwhsesave()
		goto VWDone ! don't redisplay?
	Endif

	If action2$="DELETE" ! delete								doc=Vendwh-DeleteVWhse.doc
		! close read only
		close #whc
		
		whC = OpenFile(9961,intCo) \ If whc = -1 Error 42 !vend
	   If WHRec>0  ! only if onfile!
		  tmp1=0 ! is status
		  Call VWhseDel()
		  If tmp1<>0
			Returnstatus=0
			message$=tmp1$
		  Else
			returnstatus=1
			Message$="Warehouse Deleted"
		  Endif
		  Goto VWDone ! NO REDISPLAY!
	   Else
		  !e$="file no record" \error 10000 !file no record!
		  Returnstatus=1
		  Message$="Deleted - no record"
		  goto VWDone
	   Endif
	Endif
	If action2$="ADD" ! add new									doc=Vendwh-AddVWhse.doc
		! put in defaults
		clear wh.
		wh.Vendor=vendid
		wh.Warehouse=WhseNo
		Whrec=0 ! instead of <0
		wh.SuplCust$=vtag.SupplierCust$
		wh.SuplBillto$=vtag.SupplierBillto$
		wh.MinOrder=vtag.MinOrder
		wh.MinWghtOrder=vtag.MinWtOrder
		wh.MinCubeOrder=vtag.MinCubeOrd
		wh.MinQtyOrder=vtag.MinQtyOrd
		wh.FrgtPaid=vtag.FrtPaid
		wh.FrgtPaidWght=vtag.FrtPaidWt
		wh.FrgtPaidCube=vtag.FrtPaidCubes
		wh.FrgtPaidQty=vtag.FrtPaidQty
		! no review cycle in whse
		wh.VendContact$=vm.Contact$
		wh.SlsmName$=vtag.SalesmanName$
		wh.SlsmAddress$=vtag.SlsmAddress$
		wh.SlsmCity$=vtag.SlsmCity$
		wh.SlsmState$=vtag.SlsmState$
		wh.SlsmZip$=vtag.SlsmZip$
		wh.SlsmCountry$=vtag.SlsmCountry$
		wh.SlsmPhone$=vtag.SlsmPhone$
		wh.VendorFax$=vtag.VendorFax$
		wh.ExpeditePh$=vtag.ExpidtPhone$
		wh.TruckSize=vtag.TruckSize
		wh.TrkSizeInLbCb=vtag.TruckSizInLbCube
		! drop on thru to display
	Endif
	if action2$="COPY"	! copy old to new					doc=Vendwh-CopyVWhse.doc
		call vwhsecopy()
		if returnstatus=0 goto VWDone ! bad data or result
		! drop thru and display
	Endif
	  !-----------------Section 1---------------------------------------------------
 	  List$[row]=bsdel$,"VendorInfo",fdel$ \ row=row+1
	  WebStr$="Vendor Code",fdel$,"Vendor Name",fdel$
	  List$[row]=WebStr$ \ row=row+1
	  WebStr$=Vend$,fdel$ ! Vendor Code
	  WebStr$=WebStr$,rtrim$(vm.Name$),fdel$ ! vendor name
	  List$[row]=WebStr$ \ row=row+1
	  List$[row]=esdel$ \ row=row+1
	  call AddToStr(e$,rstr$,List$[])
	  !-----------------Section 2---------------------------------------------------
      clear List$[]
	  row=0
	!
	! setup page headings
	
    List$[row]=bsdel$,"VendWHDetail",fdel$ \ row=row+1
	WebStr$="Vendor Code",fdel$
	WebStr$=WebStr$,"Warehouse",fdel$
	WebStr$=WebStr$,"Supplier Cust#",fdel$,"Supplier Bill To",fdel$
	WebStr$=WebStr$,"Bill To Cust#",fdel$,"Salesman Name",fdel$
	WebStr$=WebStr$,"Address",fdel$,"City",fdel$
	WebStr$=WebStr$,"State",fdel$,"Zip Code",fdel$
	WebStr$=WebStr$,"Country",fdel$
	WebStr$=WebStr$,"Vendor Contact",fdel$,"Fax #",fdel$
	WebStr$=WebStr$,"Expediting Phone",fdel$,"Vendor E-mail",fdel$
	WebStr$=WebStr$,"Purchasing Fax#",fdel$,"Truck Size",fdel$
	WebStr$=WebStr$,"Buyer",fdel$,"Contact #",fdel$
	WebStr$=WebStr$,"Email",fdel$,"Slsm Phone",fdel$
	Webstr$=Webstr$,"Slsm Email",fdel$
	! where's page 2?? - rebate to, mins, etc?
	! we'll send at end
	Webstr$=Webstr$,"Rebate Name",fdel$,"Rebate Attn",fdel$
	Webstr$=webstr$,"Rebate Addr",fdel$,"Rebate City",fdel$
	Webstr$=Webstr$,"Rebate State",fdel$,"Rebate Zip",fdel$
	Webstr$=webstr$,"Rebate Phone",fdel$,"Rebate Fax",fdel$
	Webstr$=webstr$,"Rebate Email1",fdel$,"Rebate Email2",fdel$
	WEBSTR$=WEBSTR$,"VWUDA1",FDEL$,"VWUDA2",FDEL$,"VWUDA3",FDEL$,"VWUDA4",FDEL$
	Webstr$=Webstr$,"Min Dol",fdel$,"Min Dol Frt",fdel$
	Webstr$=webstr$,"Min Wgt",fdel$,"Min Wgt Frt",fdel$
	Webstr$=Webstr$,"Min Cube",fdel$,"Min Cube Frt",fdel$
	Webstr$=Webstr$,"Min Qty",fdel$,"Min Qty Frt",fdel$
 	if action2$<>"" ! entry/edit
		webstr$=webstr$,"Trk Type",fdel$
	Endif
    List$[row]=WebStr$ \ row=row+1
	!
    if WHRec>=0 ! Vendor warehouse record found - allow NEW/ADD
	  !
	           
	  !
	  if WH.Buyer read record #BYC,WH.Buyer;BYR.; else clear BYR.
	  !
	  WebStr$=Vend$,fdel$ 
	  WebStr$=WebStr$,wh.warehouse using "##",fdel$
      WebStr$=WebStr$,rtrim$(wh.suplcust$),fdel$ 
	  WebStr$=WebStr$,rtrim$(wh.suplbillto$),fdel$
	  WebStr$=WebStr$,rtrim$(wh.suplbilltocust$),fdel$
	  WebStr$=WebStr$,rtrim$(wh.slsmname$),fdel$
	  WebStr$=WebStr$,rtrim$(wh.slsmaddress$),fdel$
	  WebStr$=WebStr$,rtrim$(wh.slsmcity$),fdel$
	  WebStr$=WebStr$,rtrim$(wh.slsmstate$),fdel$
	  WebStr$=WebStr$,rtrim$(wh.slsmzip$),fdel$
	  WebStr$=WebStr$,rtrim$(wh.slsmcountry$),fdel$

	  WebStr$=WebStr$,rtrim$(wh.vendcontact$),fdel$
	  WebStr$=WebStr$,rtrim$(wh.vendorfax$),fdel$
	  WebStr$=WebStr$,rtrim$(wh.expediteph$),fdel$
	  WebStr$=WebStr$,rtrim$(wh.vendoremail$),fdel$
	  WebStr$=WebStr$,rtrim$(wh.purchasingfax$),fdel$
	  let tmp$="    "
	  if wh.trkSizeInLbCb<=0 let wh.trkSizeInLbCb=1
	  if wh.trkSizeInLbCb=1
		let tmp$="/LBS  "
	  else
		if wh.trkSizeInLbCb=2
			let tmp$="/CUBES"
		endif
	  endif
	  If Action2$="" ! inquiry
		  WebStr$=WebStr$,wh.trucksize using "#######",tmp$,fdel$
		  WebStr$=WebStr$,WH.Buyer using "###"," ",rtrim$(BYR.BuyerName$),fdel$ ! buyer # & name
	  Else ! entry/edit
		WebStr$=WebStr$,wh.trucksize using "#######",fdel$ ! just size
		WebStr$=WebStr$,WH.Buyer using "###",fdel$ ! just buyer #
	  Endif
	  WebStr$=WebStr$,rtrim$(Wh.BuyerContact$),fdel$
	  WebStr$=WebStr$,rtrim$(wh.buyeremailaddr$),fdel$
	  WebStr$=WebStr$,rtrim$(wh.SlsmPhone$),fdel$
	  Webstr$=Webstr$,rtrim$(wh.SlspersonEmail$),fdel$
	  ! now screen 2
	  Webstr$=Webstr$,rtrim$(wh.RebateToName$),fdel$
	  Webstr$=Webstr$,rtrim$(wh.RebateToAttn$),fdel$
	  Webstr$=Webstr$,rtrim$(wh.RebateToAddress$),fdel$
	  Webstr$=Webstr$,rtrim$(wh.RebateToCity$),fdel$
	  Webstr$=Webstr$,rtrim$(wh.RebateToState$),fdel$
	  Webstr$=Webstr$,rtrim$(wh.RebateToZip$),fdel$
	  Webstr$=Webstr$,rtrim$(wh.RebateToPhone$),fdel$
	  Webstr$=Webstr$,rtrim$(wh.RebateToFax$),fdel$
	  Webstr$=Webstr$,rtrim$(wh.RebateToEmail1$),fdel$
	  Webstr$=Webstr$,rtrim$(wh.RebateToEmail2$),fdel$
	  Webstr$=Webstr$,rtrim$(wh.Uda1$),fdel$
	  Webstr$=Webstr$,rtrim$(wh.Uda2$),fdel$
	  Webstr$=Webstr$,rtrim$(wh.Uda3$),fdel$
	  webstr$=webstr$,ltrim$(wh.Uda4 using "-------#.####"),fdel$
	  Webstr$=webstr$,LTrim$(wh.MinOrder using "######.##"),fdel$
	  webstr$=webstr$,LTrim$(wh.FrgtPaid using "######.##"),fdel$
	  webstr$=webstr$,Str$(wh.MinWghtOrder),fdel$
	  webstr$=webstr$,Str$(wh.FrgtPaidWght),fdel$
	  webstr$=webstr$,Str$(wh.MinCubeOrder),fdel$
	  webstr$=webstr$,Str$(wh.FrgtPaidCube),fdel$
	  webstr$=webstr$,Str$(wh.MinQtyOrder),fdel$
	  webstr$=webstr$,Str$(wh.FrgtPaidQty),fdel$
	  if action2$<>"" ! entry/edit - truck size in field
		if wh.trkSizeInLbCb<=0 let wh.trkSizeInLbCb=1
		webstr$=webstr$,Str$(wh.trkSizeInLbCb),fdel$
	  Endif
	  !
	  List$[row]=WebStr$ \ row=row+1
	endif
	!
    List$[row]=esdel$ \ row=row+1 ! end of section
	! row=row+1
	List$[row]=bsdel$,"UDANAMES",fdel$
	row=row+1
	List$[row]="VWUDA1",FDEL$,"VWUDA2",FDEL$,"VWUDA3",FDEL$,"VWUDA4",FDEL$
	ROW=ROW+1
	tmp$=UDAN$[1,14];tmp1$=UDAN$[15,28]
	if Trim$(tmp$)="" Let tmp$="VW UDA1"
	if Trim$(tmp1$)="" let tmp1$="VW UDA2"
	Webstr$=RTrim$(tmp$),fdel$,RTrim$(tmp1$),fdel$
	tmp$=UDAN$[29,42];tmp1$=UDAN$[43,56]
	if Trim$(tmp$)="" Let tmp$="VW UDA3"
	if Trim$(tmp1$)="" let tmp1$="VW UDA4"
	Webstr$=webstr$,RTrim$(tmp$),fdel$,RTrim$(tmp1$),fdel$
	LIST$[ROW]=WEBSTR$ \ ROW=ROW+1
	lIST$[ROW]=ESDEL$
	!
	VWDone: ! finished - Main
  else
    include "src/callsuberr.inc"
  end try
end sub ! VWMain
! 
!--------------------------------------------------------------------
Sub vwhsesave()
! web sending data to PM
  Try
	wh.Vendor=vendid
	wh.Warehouse=WhseNo
	wh.LastChgDate=currdate
	wh.ChgDBy=UCRec
	! clear strings
	wh.Spare$=blank$
	wh.Spare1$[1,24]=blank$ ! not all the way!!
	  Call dxget("SupplCust",tmp$)\wh.suplcust$=tmp$+Blank$
	  call dxget("SupplBillTo",tmp$)\wh.suplbillto$=tmp$+Blank$
	  call dxget("BillToCust",tmp$)\wh.suplbilltocust$=tmp$+Blank$
	  call dxget("SlsmName",tmp$)\wh.slsmname$=tmp$+Blank$
	  call dxget("SlsmAddr",tmp$)\wh.slsmaddress$=tmp$+Blank$
	  call dxget("SlsmCity",tmp$)\wh.slsmcity$=tmp$+Blank$
	  call dxget("SlsmState",tmp$)\wh.slsmstate$=tmp$+Blank$
	  call dxget("SlsmZip",tmp$) ! only 9
	  tmp$=VendZip9$(tmp$) ! check / adjust to 9
	  wh.slsmzip$=tmp$+Blank$
	  call dxget("SlsmCtry",tmp$)\wh.slsmcountry$=tmp$+Blank$

	  Call dxget("VendContact",tmp$)\wh.vendcontact$=tmp$+Blank$
	  Call dxget("VendFax",tmp$)\wh.vendorfax$=tmp$+Blank$
	  Call dxget("ExpdtPhn",tmp$)\wh.expediteph$=tmp$+Blank$
	  Call dxget("VendEMail",tmp$)\wh.vendoremail$=tmp$+Blank$
	  Call dxget("PurchFax",tmp$)\wh.purchasingfax$=tmp$+Blank$
	  call dxget("TruckSize",tmp$)\wh.trucksize=tmp$
	  call dxget("BUYERID",tmp$)\WH.Buyer=tmp$
	  
	  call dxget("BuyerCont",tmp$)\Wh.BuyerContact$=tmp$+Blank$
	  call dxget("BuyerEmail",tmp$)\wh.buyeremailaddr$=tmp$+Blank$
	  call dxget("SlsmPhn",tmp$)\wh.SlsmPhone$=tmp$+Blank$
	  call dxget("SlsmEmail",tmp$)\wh.SlspersonEmail$=tmp$+Blank$
	  ! now screen 2
	  call dxget("RbtName",tmp$)\wh.RebateToName$=tmp$+Blank$
	  call dxget("RbtAttn",tmp$)\wh.RebateToAttn$=tmp$+Blank$
	  call dxget("RbtAddr",tmp$)\wh.RebateToAddress$=tmp$+Blank$
	  call dxget("RbtCity",tmp$)\wh.RebateToCity$=tmp$+Blank$
	  call dxget("RbtState",tmp$)\wh.RebateToState$=tmp$+Blank$
	  call dxget("RbtZip",tmp$)\wh.RebateToZip$=tmp$+Blank$
	  call dxget("RbtPhn",tmp$)\wh.RebateToPhone$=tmp$+Blank$
	  call dxget("RbtFax",tmp$)\wh.RebateToFax$=tmp$+Blank$
	  call dxget("RbtEmail1",tmp$)\wh.RebateToEmail1$=tmp$+Blank$
	  call dxget("RbtEmail2",tmp$)\wh.RebateToEmail2$=tmp$+Blank$
	  call dxget("UDA1",tmp$)\wh.Uda1$=tmp$+Blank$
	  call dxget("UDA2",tmp$)\wh.Uda2$=tmp$+Blank$
	  call dxget("UDA3",tmp$)\wh.Uda3$=tmp$+Blank$
	  call dxget("UDA4No",tmp$)\wh.Uda4=tmp$
	  call dxget("MinDol",tmp$)\wh.MinOrder=tmp$
	  call dxget("MinDolFrt",tmp$)\wh.FrgtPaid=tmp$
	  call dxget("MinWgt",tmp$)\wh.MinWghtOrder=tmp$
	  call dxget("MinWgtFrt",tmp$)\wh.FrgtPaidWght=tmp$
	  call dxget("MinCube",tmp$)\wh.MinCubeOrder=tmp$
	  call dxget("MinCubeFrt",tmp$)\wh.FrgtPaidCube=tmp$
	  call dxget("MinQty",tmp$)\wh.MinQtyOrder=tmp$
	  call dxget("MinQtyFrt",tmp$)\wh.FrgtPaidQty=tmp$

	  call dxget("TrkType",tmp$)\wh.trkSizeInLbCb=tmp$
	  	mode$="c"
		if whrec<=0 let mode$="a"
		aa=fileupdatevendwhse(e$,WHC,mode$,WHREC,WH.) ! ,0,0,-1) ! for audit(not working?!)
		if aa<0 ! bad stat
			  Returnstatus=0
			  Message$=E$
		Else
			Returnstatus=1
			Message$="Record saved"
		Endif
	VSDone: ! finished with get/save
  else
    include "src/callsuberr.inc"
  end try
end sub ! vwhsesave
! 
!--------------------------------------------------------------------
Sub VWhseDel()
 Try
	! that's all - A straight delete - no check
	if whrec<=0 exit sub ! not possible with no record #
		
	tmp3=fileupdatevendwhse(e$,WHC,"d",whrec,wh.) ! ,0,0,-1) ! w/audit(not working!?)
	if tmp3>=0
		returnstatus=1
		Message$="OK - Record deleted"
	Else ! problem
		returnstatus=0
		Message$="Error on delete "+e$
	Endif
	tmp1=0
	if returnstatus=0
		tmp1=1 ! means problem
		tmp1$=Message$ ! problem desc
	Endif
	! in case delete check added, we send back tmp1 & tmp1$
 else
    include "src/callsuberr.inc"
  end try
end sub ! VWhseDel
! 
!--------------------------------------------------------------------
Sub VWhseCopy()
 Try
	if WHRec>0 ! not on existing
		returnstatus=0
		message$="Can only copy in to a new warehouse"
		goto WCDone
	Endif
	call dxget("FRMWHSE",tmp$)
	if tmp$="" goto WCDone
	X2=tmp$
	if x2<1 or x2>99 or fra(x2)
		returnstatus=0
		message$="INVALID WAREHOUSE TO COPY!"
		goto wcdone
	Endif
	key2$=vend$,X2 Using "##"
	Rec=filegetVendwhse(e$,WHC,"=",1,Key2$,WH.)
	if rec<=0
		returnstatus=0
		message$="From Warehouse not on file"
		goto wcdone
	Endif
	! ok now change what's needed
	wh.Vendor=vendid
	wh.Warehouse=WhseNo
	wh.LastChgDate=currdate
	wh.ChgDBy=UCRec
	! clear strings not used!
	wh.Spare$=blank$
	wh.Spare1$[1,24]=blank$ ! not all the way!!
	! finally - ADD Record NOW!!
	let mode$="a";whrec=0
	aa=fileupdatevendwhse(e$,WHC,mode$,WHREC,WH.) ! ,0,0,-1) ! for audit(not working?!)
	if aa<0 ! bad stat
		  Returnstatus=0
		  Message$=E$
	Else
		Returnstatus=1
		Message$="Record added"
		WhRec=aa ! new rec #
	Endif
	WCDone: ! finito
 else
    include "src/callsuberr.inc"
  end try
end sub ! VWhseCopy
! 
!--------------------------------------------------------------------
Sub sndlists()
! send any selection lists needed - Buyer & truck size only?
 Try
	tmpcnt=maxcnt
	! Buyer
	Clear List$[]
	call filedroplistbuyer(e$,list$[],tmpcnt,BYC,"",field$[],"0")  !"0" parameter adds  0 None to drop list)
	call addtostr(e$,rstr$,list$[])
	! Truck Type
	clear List$[]
	List$[0]=bsdel$,"TRUCKTYPE",fdel$
	List$[1]="ID",fdel$,"DESC",fdel$
	!List$[2]="0",fdel$,"NOT USED",fdel$ ! default 0 to 1
	List$[2]="1",fdel$,"LBS",fdel$
	List$[3]="2",fdel$,"CUBES",fdel$
	List$[4]=esdel$
	! callee adds to str
 else
    include "src/callsuberr.inc"
  end try
end sub ! sndlists
! 
!--------------------------------------------------------------------
Function VendZip9$(zip$)
! as only 9 char zip - see if user entered 10
 try
	tmp1$=Trim$(zip$) ! trim front & back
	tmp$=" ",tmp$ ! clear result to send back
	if len(tmp1$)<10 ! only 5 or no dash
		tmp$=tmp1$
		goto VZDone
	Endif
	x1=1 ! first pos
	for x=1 to 10 ! as length checked above!
		if tmp1$[x,x]="-" ! other characters to check?
			rem ! do nothing (do not add)
		Else ! add it to result
			tmp$[x1,x1]=tmp1$[x,x]
			x1=x1+1 ! increment position
		Endif
	Next x
	VZDone: ! finito
 else
    include "src/callsuberr.inc"
  end try
end Function tmp$ ! VendZip9$
! 
!--------------------------------------------------------------------