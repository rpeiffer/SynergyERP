Rem ProgDflt Program default information look-up
Rem    George Doscher  10/12/95
!
! loadsave -w -n 100,10 -o prog/dxport/progdflt src/progdflt.src
!
Rem BEDIT Version: 2.1 OBJECT CODE     Created: JUN 20, 2000  13:36:25
Rem /usr/acct/r97//usr/acct/judy/work/r97/misc/progdflt.src (univ2) [judy] {19974}
Rem  Copyright (c) 2000 Universal Business Systems, Inc.
Rem    **** ALL CHANGES MUST BE MADE IN SOURCE!!! ****
Rem
! syntax:  Call "ProgDflt",ProgID$,SubCat$,Chan,RetVal$
!
!	ProgID$		8 character max program ID.  This would normally be
!				the program name.
!				
!	SubCat$		4 character program-specific sub category.  Ex: order type.
!				
!	Chan		Available channel number.  If negative, then "progdflt"
!				file is already open on that channel.NOT USED IN DL4!!
!
!	RetVal$		Returned program-specific default information, 512 char. max.
!				If no entry found, returns as all "@"
!				If no file found, returns as all "#"
Declare Intrinsic Sub InpBuf,String
! Option Default Dialect IRIS1

include "src/copyright.inc"
! internal files needed
! other external functions / subs
External Lib "ubsfunc.dl4"
Declare External Function OpenFile

External Lib "libgeneral.lib"
Declare External Sub GetSession

Try
Enter PROGID$,SUBCAT$,CHANNL,RETVAL$
Dim KEY1$[20],SPACES$[90],tmp$[200],UserId$[10]
dim e$[500],buttonlist$[5,50],nextlist$[5,100] !error handling variables
dim action$[30],options$[30],b$[200],action1$[30]
Dim bsdel$[10],esdel$[10],rdel$[10],fdel$[10],rstr$[1000]
Dim 1%,UREC,IntCo,Ctlc,3%

FALSE = 0;TRUE = Not(FALSE)
SPACES$ = " ",SPACES$
NOMATCH = FALSE
call GetSession(e$,CTLC,options$,action$,UserID$,intCo,intSls,fdel$,rstr$,bsdel$,esdel$,rdel$,action1$)
If Userid$="" or UserID$[1,2]="  "
		Call DXGet("S_USER.ID",tmp$) ! get from system variable
Else
		tmp$=UserID$
Endif
Let UserID$=UCase$(tmp$)+Spaces$ ! make sure it's UPPERCASE as that's what PM uses
Chan=OpenFile(-9940,IntCo) \ if chan=-1 Error 42 ! progdflt
CHNL=OpenFile(-9988,IntCo) \ if chnl=-1 Error 42 ! usercntrl
Search #CHNL,2,1;UserId$,URec,E \ if e let URec=0
Close #CHNL
If UREC < 0 Let UREC = 0
KEY1$ = PROGID$,SPACES$
KEY1$[9,12] = SUBCAT$,SPACES$
KEY1$[13,16] = UREC Using "####"
Search #CHAN,2,1;KEY1$,REC1,E ! \ If E > 1 Gosub ERR_SEARCH
If E
  KEY1$[13,16] = SPACES$
  Search #CHAN,2,1;KEY1$,REC1,E ! \ If E > 1 Gosub ERR_SEARCH
  If E
    KEY1$[9,12] = SPACES$
    KEY1$[13,16] = UREC Using "####"
    Search #CHAN,2,1;KEY1$,REC1,E ! \ If E > 1 Gosub ERR_SEARCH
    If E
      KEY1$[9,16] = SPACES$
      Search #CHAN,2,1;KEY1$,REC1,E ! \ If E > 1 Gosub ERR_SEARCH
      If E Let NOMATCH = TRUE
    End If 
  End If 
End If 
If NOMATCH
  RETVAL$ = "@",RETVAL$
Else 
  Mat Read #CHAN,REC1;RETVAL$;
End If 
Close #CHAN
OUTEND: Rem

else
 include "src/callmainerrnet.inc"
end try
End 