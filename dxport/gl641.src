! >>> Program gl641
!
! description General Ledger End of Year
! loadsave -w -n 100,10 -o prog/dxport/gl641.dl4 src/gl641.src
!
! 1.0 mm/dd/yyyy change??
!
include "src/copyright.inc"

Rem BEDIT Version: 2.2 OBJECT CODE     Created: AUG 28, 2002  11:44:16
Rem /w/papermaster/r2002//usr/acct/mary/work/r2002/ap/gl641.src (univ2) [mary] {04977}
Rem  Copyright (c) 2002 Universal Business Systems, Inc.
Rem    **** ALL CHANGES MUST BE MADE IN SOURCE!!! ****
Rem
Declare Intrinsic Sub Echo,DateToJulian,FindF,InpBuf,Logic,String,Time,VerifyDate
! Option Default Dialect IRIS1
Declare Intrinsic Sub getglobals
Declare Intrinsic sub programdump,env
Declare Intrinsic function findchannel
! external libs for subs/functions
External Lib "libgeneral.lib"
Declare External Sub suberr,mainerr,GetSession,AddToStr,SetOutput,CreateNetStatus
External Lib "ubsfunc.dl4"
Declare External Function OpenFile
External Lib "dxblockgeneral.lib"
Declare External Function blockbuildsort
Rem GL641  FISCAL YEAR CLOSE
Try
Enter SPEC1[],MESSAGE$,rstr$ 
	dim e$[500],buttonlist$[5,50],nextlist$[5,100],msg$[100],TMP$[50] ! error 
	dim xdate$[50],cmd$[250]
Dim A$[14],C1$[4],C3$[4],E1$[15],F1$[16],F2$[14],H$[4],S$[6],V$[12],Z$[10]
Dim 3%,A,C,D,P[1],Z[300],1%,B,K,2%,C1,G8,I,I1,I2,I3,L8,P8,R,U8,V1,Z9,X
dim 3%,glacctid,totrec,rec_c,rec_nn,pdsp
Dim 4%,H3,T3,TOTAMT,TMP4
Dim 2%,A9,I9,Y9,J$[30]
Dim 1%,L9,archHIST,2%
Dim Y$[2],F$[30],F9$[2],C$[140],U5$[50]
!If Err 0 Gosub ERR_TRAP
!If Spc(13) = 11 Print @0,0;"\177\UWHOTSPOT.\\HOTSPOT\\HOTSPOTIN.hsf\177\";
dIM GROUPID$[1],gltkey$[50]
Dim 3%,tmp3
for ctr=1 to 99
	Try Close #ctr Else Rem
next ctr
returnstatus=1
message$=""
!X2 = Spc(5)
!COMP = Int((X2 - Int(X2 / 16384) * 16384) / 64)
Comp=spec1[0] ! company #
intco=spec1[0]
filetype=spec1[1] ! 1= g/l master , 2= g/l budget, 3= both
totrec=spec1[2] ! total clients to process
shifttype=spec1[3] ! 1 - date only , 2= date/data
rec_uac=spec1[8]
returnstatus=spec1[9]
Rem open files    
F$="cntrl/Cntrl"+str$(comp)
open #1,f$
mat read #1,51,94;archhist;
starthere: ! 
Call DXGet("u_group",groupid$)
if rtrim$(groupid$)="" let groupid$="0"
s$[1,1] = comp Using "#"
s$[2,2] = groupid$ ! V2 Using "#"
E1$ = "#",E1$

C = V$
F2$ = "1/ACF",S$[1,2]
Call FindF(F2$,A9) 
if not(a9)
	returnstatus=0
	let eflag=eflag+1
	let message$="G/L Client Master FIle Not Found "
	goto outend:
endif
Open #2,F2$

call dxget ("clientid",tmp$)
let c=tmp$
V$ = " ",V$
V$[3,6] = C Using "####"
Search #2,2,1;V$,V1,E 
if e<>0
	let eflag=eflag+1
	let returnstatus=0
	let message$="CLIENT "+str$(c1)+" NOT ON FILE!"
	goto outend:
endif
Mat Read #2,V1;C$;
V$ = " ",V$
V$[3,6] = C Using "####"
V$[7,8] = "PG"
Search #2,2,1;V$,V1,E 
if e<>0
	let eflag=eflag+1
	let returnstatus=0
	let message$="CLIENT "+str$(c1)+" Control Record Not Set Up Properly!"
	goto outend:
endif
Read #2,V1,134;B
Try Close #2 Else Rem
L9 = C$[135,136]
If L9 > 9 Let F9$ = L9 Using "##"
If L9 < 10 Let F9$ = L9 Using "#"
S$[3,6] = C Using "####"
For I = 3 To 6
  If S$[I,I] = " " Let S$[I,I] = "."
Next I
F1$ = F9$,"/GLM",S$
Call FindF(F1$,A9)
If Not(A9)
	returnstatus=0
	let eflag=eflag+1
	let message$=message$+"G/L Master File for Client "+str$(c1)+" Not Found "
	goto outend:
End If 
Open #3,F1$
V$ = " ",V$
Search #3,2,1;V$,V1,E
If E <> 0 
	returnstatus=0
	let eflag=eflag+1
	let message$=message$+"G/L Master Control Record for Client "+str$(c1)+" Not Found "
	goto outend:
endif

Mat Read #3,V1,70;H3
H$ = H3 Using "####"
P[0] = H$[1,2]
P[1] = H$[3,4]
R = P[1]
If R <> 14 If R <> 39 Let R = 39
Dim 4%,M3[R],2%
Mat Read #3,V1,70;M3;
V$ = "C"
Search #3,2,1;V$,rec_c,E 
If E <> 0 
	returnstatus=0
	let eflag=eflag+1
	let message$=message$+"G/L Master Alpha Record 'C' for Client "+str$(c1)+" Not Found "
	goto outend:
endif
V$ = "NN" \ Search #3,2,1;V$,rec_nn,E 
If E <> 0 
	returnstatus=0
	let eflag=eflag+1
	let message$=message$+"G/L Master Alpha Record 'NN' for Client "+str$(c1)+" Not Found "
	goto outend:
endif
call dxget ("FYE",tmp$)
LET C1=TMP$
IF C1<0 OR C1>99
	returnstatus=0
	let eflag=eflag+1
	let message$=message$+"Invalid Fiscal Year Submitted"
	goto outend:
endif
let fye=c1 ! 
C1 = C1 * 100 + C1
let il=0
if m3[13]=c1 let i1=13
if m3[26]=c1 let i1=26
if m3[39]=c1 let i1=39
If not(i1)
	returnstatus=0
	let eflag=eflag+1
	tmp$=c1 using "&&&&"
	let message$=message$+"Accounting Periods for Fiscal Year "+tmp$[1,2]+" Not on File"
	goto outend:
endif
A = I1
D = A * 8 + 70
pdisp=A * 8 + 70
Read #3,rec_nn,pdisp;T3;
if t3<>0
	returnstatus=0
	let eflag=eflag+1
	tmp$=c1 using "&&&&"
	let message$=message$+"Fiscal Year "+tmp$[1,2]+" Already Closed"
	goto outend:
endif
L_600: V$ = "N"
Search #3,2,1;V$,V1,E
If E <> 0 
	returnstatus=0
	let eflag=eflag+1
	let message$=message$+"G/L Master Alpha Record 'N' for Client "+str$(c1)+" Not Found "
	goto outend:
endif
Mat Read #3,V1,70;M3
H3 = 0
c2=12
For I2 = (I1 - C2) To (I1 - 1)
  H3 = H3 + M3[I2]
Next I2
If H3 = 0 
	returnstatus=0
	let eflag=eflag+1
	tmp$=c1 using "&&&&"
	let message$=message$+"No Close Required For Fiscal Year "+tmp$[1,2]
	goto outend:
endif
! VERIFY ACCOUNT
call dxget ("TOTREC",tmp$)
let TMP3=TMP$
if tmp3<1 
	returnstatus=0
	let eflag=eflag+1
	let message$=message$+"Invalid Total Records Submitted "
	goto outend:
endif
let totrec=tmp3
let totamt=0
let tmp3=0
for ctr=1 to totrec
	Call Dxget("GLACCTID" + Str$(ctr),tmp$)
	let glacctid=rtrim$(tmp$)
	if glacctid=0
		Call Dxget("AMOUNT" + Str$(ctr),tmp$)
		tmp4=tmp$
		if tmp4<>0
			returnstatus=0
			let eflag=eflag+1
			let message$=message$+"G/L Account Not on Valid "
			goto outend:
		endif
		goto end641ctr: ! bypass no amount and no gl acct
	endif
	let v$=" ",v$
	let v$[1,9]=glacctid using "#########"
	if glacctid<=0 or glacctid=99999999
		returnstatus=0
		let eflag=eflag+1
		let message$=message$+"Invalid G/L Account "+str$(glacctid)+" Submitted "
		goto outend: 
	endif
	Search #3,2,1;V$,V1,E
	If E<>0
		returnstatus=0
		let eflag=eflag+1
		let message$=message$+"G/L Account "+str$(glacctid)+" Not on File "
		goto outend: 
	endif
	Read #3,V1,pdisp;T3;
	IF T3<>0
		returnstatus=0
		let eflag=eflag+1
		let message$=message$+"G/L Account "+str$(glacctid)+" Already Has Closed Amount Posted"
		goto outend: 
	endif	
	MAT  READ #3,V1;F1$;                        
	IF F1$[13,13]<>"0"              
		returnstatus=0
		let eflag=eflag+1
		let message$=message$+"G/L Account "+str$(glacctid)+" Not A Normal Posting Account "
		goto outend: 
	endif
	IF F1$[14,14]<>"C"              
		returnstatus=0
		let eflag=eflag+1
		let message$=message$+"G/L Account "+str$(glacctid)+" Not A Capital Account "
		goto outend: 
	endif
	! check to make sure that it is capital
	Call Dxget("AMOUNT" + Str$(ctr),tmp$)
	tmp4=tmp$
	if fra(tmp4*100)<>0
		let returnstatus=0
		let message$="Amount exceeds 2 decimals places"
		goto outend:
	endif
	let totamt=totamt+tmp4
	end641ctr: ! end
next ctr
! verify total amount close
if totamt<>h3
	returnstatus=0
	let eflag=eflag+1
	let message$=message$+"Amount Posted Must Equal Profit/Loss for the Year"
	goto outend: 	
endif
!Goto L_380
!L_1360: Print @0,23;'CL';"CLOSE IN PROGRESS......"
!Print "PLEASE WAIT . . . . . .";
!Signal 3,0
K = 1
For ctr = 1 To totrec
	Call Dxget("GLACCTID" + Str$(ctr),tmp$)
	let glacctid=tmp$
	if glacctid<>0
		LET V$=" ",V$
		V$[1,9] = glacctid Using E1$[1,9]
		Search #3,2,1;V$,V1,E 
		If E <>0 Gosub ERR_SEARCH
		Call Dxget("AMOUNT" + Str$(ctr),tmp$)
		tmp4=tmp$
		if tmp4<>0
		! update the gl acct
			Read #3,V1,pdisp;T3
			T3 = T3 + tmp4
			Write #3,V1,pdisp;T3;
		! update alpha c
			Read #3,rec_c,pdisp;T3
			T3 = T3 + tmp4
			Write #3,rec_c,pdisp;T3;
		! update NN
			Read #3,rec_nn,pdisp;T3
			T3 = T3 + tmp4
			Write #3,rec_nn,pdisp;T3;
		endif
	endif
next ctr

message$="Year End Close Completed"
if archhist<>0
	LET Y$=FYE USING "&&"
	! archive history
	F$ = F9$,"/GLM",S$[1,6],"..",Y$[1,2]
	Call FindF(F$,A9)
	If (A9)<>0 
		Try Kill F$ Else Rem
	endif
	let cmd$="files/",f9$,"/GLM",S$[1,6]," ","files/",F$
	DUPLICATE CMD$
	! BUDGET FILE
	F$ = F9$,"/GLB",S$[1,6]
	Call FindF(F$,TMP3)
	If (TMP3)<>0 ! BUDGET EXISTS
	    F$ = F9$,"/GLB",S$[1,6],"..",Y$[1,2]
		Call FindF(F$,A9)
		
		If (A9)<>0 
			Try Kill F$ Else Rem 
		endif
		let cmd$="files/",f9$,"/GLB",S$[1,6]," ","files/",F$
		DUPLICATE CMD$	
	ENDIF
ENDIF
goto L_2000:



L_2000: Rem CLOSE HISTORY FILES
LET Y$=FYE USING "&&"
If not(archhist)
	Y9 = Y$[1,2] \ Y9 = Y9 - 7 \ If Y9 < 0 Let Y9 = 100 - Y9 ! less 7 years
else
	Y9 = Y$[1,2] \ Y9 = Y9 - 8\ If Y9 < 0 Let Y9 = 100 - Y9 ! less 8 years	
endif
Y$[1,2] = Y9 Using "&&"
if archhist<>0
	F$ = F9$,"/GLM",S$[1,6],"..",Y$[1,2]
	Call FindF(F$,A9)
	If (A9)<>0 
		Try Kill F$ Else Rem
	endif
	F$ = F9$,"/GLB",S$[1,6],"..",Y$[1,2]
	Call FindF(F$,A9)
	If (A9)<>0 
		Try Kill F$ Else Rem
	endif
ENDIF
F$ = F9$,"/GLH",S$[1,6],"..",Y$[1,2]
Call FindF(F$,A9)
If (A9)<>0 
	Try Kill F$ Else Rem
endif
 F$ = F9$,"/GLI",S$[1,6],"..",Y$[1,2]
Call FindF(F$,A9)
If (A9)<>0 
	Try Kill F$ Else Rem
endif
F$ = F9$,"/GLT",S$[1,6],"..",Y$[1,2]
Call FindF(F$,A9)
If (A9)<>0 
	Try Kill F$ Else Rem
endif
Goto OUTEND

ERR_SEARCH: Rem                         
ENUM = E;ELINE = Spc(16);CTERR = 0           
If E = 5 Let CTERR = Err(8)                  
msg$ = "RETURN STATUS",(E + (CTERR * .001))  
msg$ = msg$,"/ STATEMENT",ELINE," IN PROGRAM "
e$ = msg$ \ Error 10000                      
Return ! End                                 
If Msg$<>"" let e$=msg$ !  Error 10000 ! sent reason
ENUM = E ;ELINE = Spc(16);CTERR = 0
If E = 5 Let CTERR = Err(8)
e$ = "RETURN STATUS",(E + (CTERR * .001))
e$ = e$,"/ STATEMENT",ELINE," IN PROGRAM "
Call programdump()
! goto err_msg ! Error 10000
Error 10000
Return ! End

OUTEND: Rem END
for x=2 to 30
	Try Close #x Else Rem
next x
if message$="" let message$="Process Complete"

let spec1[9]=returnstatus
! set the returnstatus 

else
	include "src/callmainerrnet.inc"
end try


