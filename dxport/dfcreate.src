!    dfcreate.dl4 - builds demo file definitions subsystem files
!
!    "dfcreate.bas" 1.0 8/25/2006
!
!    All rights reserved.    (C) Copyright 2006 by:
!    Dynamic Concepts, Inc.   Aliso Viejo, California USA
!
!    "@(#) dfcreate.bas 1.0@(#)"
!
! loadsave -w -n 100,10 -o dfcreate.dl4 src/dfcreate.src
!
!This source code is free to use and duplicate with purchase of dynamicXport
!license
!
declare intrinsic sub findf
declare intrinsic function findchannel
! *declare external libs & subs & functions
external lib "dfgeneral.lib" !general routines
declare external sub dfsuberr,dfmainerr,readsystemfile
declare external function getprogrampath$
!
include "src/dffiles.bsh"
!
Function CreateDir(dir$,type$)
  Dim 1%,found,itslen,ndir$[60]
  ndir$ = dir$
  itslen = Len(ndir$)
  If ndir$[itslen] = "/" or ndir$[itslen]="\\"
    ndir$[itslen] = ""
  End If
  Call FindF(ndir$,found)
  If Not(found)
    ! We have to build the directory.
    Try
    Build #0,ndir$ As "directory"
      Close #0
    Else
    Print Msc$(2);" building ";ndir$;" directory for ";type$;" files!"
      Errclr
     Error 10000
    End Try
  End If
End Function found ! CreateDir
!-----------------------------------------------------------------
!
External Sub BackUp(FileDir$,BackupDir$,Dataset$,FileType$)
  Dim 1%,foundit
  ! Backs up the dataset if it exists in Filesdir directory
  ! into "old"Filesdir directory after removing older backup copy
  !
  Dim Target$[93],Temp$[93]
  Target$ = BackUpdir$ +  Dataset$
  If FileType$ = "F" let Target$ = Target$ + ".dbf"
  Try
      Kill Target$
  Else
    If Spc(8) <> 42
      Print Msc$(2);" in clearing old ";Target$
      Error 10000
      Exit Sub
    End If
  End Try
  Temp$ = Target$
  Target$ = FileDir$ +  Dataset$
  If FileType$="F" let Target$ = Target$ + ".dbf"
  Call FindF(Target$,foundit)
  If Not(foundit)  Exit Sub ! nothing to copy
  existed=createdir(backupdir$,"backup") !build dir if needed
  Target$ = Target$ + " " + Temp$
  Try
   Duplicate Target$
    Print "Copying ";Target$
  Else
   Print Msc$(2);" in making backup file ";Target$;" on line ";Spc(10)
    Error 10000
  End Try
End Sub
!
!
!
!    Name:     CreateFiledefHdr() - create a new blockhdr file
!    Synopsis: Call CreateFiledefHdr(E$,FileName$,Drvr$)
!              Creates a new Filedefhdr file using the Drvr$ class.
!    Return value:
!        Success: E$=""
!        Failure: E$<>"", Error 10000
!
External Sub CreateFiledefHdr(E$,FileName$,Drvr$)
 !
 Dim FiledefHdr. As FiledefHdr
 Dim Key1. As Key1,Key2. As Key2
 Dim tmp$[95]
 !
 Try
  Chan=findchannel()
  Print "Building ";FileName$
  tmp$=FileName$,"!"
  Build #Chan,tmp$ As Drvr$
  Define Record #Chan;FiledefHdr.
  Add Index #Chan,1;Key1.
  Add Index #Chan,2;Key2.
  close #Chan
 Else
  if spc(8) <> 10000
   call dfsuberr(e$,Spc(8),Spc(10),Msc$(2))
  end if
  error 10000
 End Try
End Sub !CreateFiledefHdr
!
!    Name:     CreateFiledefFields() - create a new filedeffields file
!    Synopsis: Call CreateFiledefFields(E$,FileName$,Drvr$)
!              Creates a new filedeffields file using the Drvr$ class.
!    Return value:
!        Success: E$=""
!        Failure: E$<>"", Error 10000
!
External Sub CreateFiledefFields(E$,FileName$,Drvr$)
 !
 Dim FiledefFields. As FiledefFields
 Dim Key1. As Key1seq
 Dim tmp$[95]
 !
Try
 Chan=findchannel()
  Print "Building ";FileName$
 tmp$=Filename$,"!"
 Build #Chan,tmp$ As Drvr$
  Define Record #Chan;FiledefFields.
  Add Index #Chan,1;Key1.
  close #Chan
 Else
 if spc(8) <> 10000
   call dfsuberr(e$,Spc(8),Spc(10),Msc$(2))
  end if
  error 10000
 End Try
End Sub !CreateFiledefFields
!
!    Name:     CreateFiledefKey() - create a new filedefkey file
!    Synopsis: Call CreateFiledefKey(E$,FileName$,Drvr$)
!              Creates a new filedefkey file using the Drvr$ class.
!    Return value:
!        Success: E$=""
!        Failure: E$<>"", Error 10000
!
External Sub CreateFiledefKey(E$,FileName$,Drvr$)
 !
 Dim FiledefKey. As FiledefKey
 Dim Key1. As Key1seq2
 Dim tmp$[95]
 !
Try
 Chan=findchannel()
  Print "Building ";FileName$
 tmp$=FileName$,"!"
 Build #Chan,tmp$ As Drvr$
  Define Record #Chan;FiledefKey.
  Add Index #Chan,1;Key1.
  close #Chan
 Else
 if spc(8) <> 10000
   call dfsuberr(e$,Spc(8),Spc(10),Msc$(2))
  end if
  error 10000
 End Try
End Sub !CreateFiledefKey
!

!
External Function OpenSystemFile(e$)
! not used 
! returns channel #
 Dim path$[80],filename$[92]
 path$=getprogrampath$()  !current program path, where dxport resides
 syschan=findchannel()
 filename$=path$,"dxsystem.txt"
!open #syschan,filename$
End Function syschan
!
!
!
! **main procedure
!
!
Dim e$[500] !error
Try
 Dim config. as config
 Dim filename$[93],backupdir$[80],ndir$[80]
 ! need to get filenames and drvr from dxsystem.txt
 !
 !read system file parameters
 Call ReadSystemFile(e$,config.)
 !
 !if any parameters not there, add defaults to dxsystem.txt
 !no, all the filenames are hardcoded,not stored in dxsystem.txt,only dir & type
 !
 !syschan=-1 !not open yet
 !if config.filehdr$=""
 ! if syschan<0 let syschan=opensystemfile(e$)
 ! config.filehdr$="dxblockhdr"
 ! write #syschan,-4;config.filehdr$,"=",config.filehdr$
 !end if 
 !if config.filefields$=""
 !if config.filekey$=""
 !
 !backup any existing files first
 ndir$ = config.filedir$
 If ndir$[len(ndir$)] = "/" or ndir$[len(ndir$)]="\\"
    ndir$[len(ndir$)] = ""
 End If
 pos1 = pos(ndir$,="/",-1)
 pos2 = pos(ndir$,="\\",-1)
 if pos1=0 let pos1=pos2
 backupdir$="old",config.filedir$
 if pos1 let backupdir$=config.filedir$[1,pos1],"old",config.filedir$[pos1+1]
 Call backup(config.filedir$,backupdir$,config.filehdr$,config.filetype$)
 Call backup(config.filedir$,backupdir$,config.filefields$,config.filetype$)
 Call backup(config.filedir$,backupdir$,config.filekey$,config.filetype$)
 !
 !build the files
 Call CreateFiledefHdr(e$,config.filedir$+config.filehdr$,config.drvr$)
 Call CreateFiledefFields(e$,config.filedir$+config.filefields$,config.drvr$)
 Call CreateFiledefKey(e$,config.filedir$+config.filekey$,config.drvr$)
 If config.filetype$="F"
 filename$ = config.filedir$ + config.filehdr$ + ".dbf <666>"
 Modify filename$
 filename$ = config.filedir$ + config.filefields$ + ".dbf <666>"
 Modify filename$
 filename$ = config.filedir$ + config.filekey$ + ".dbf <666>"
 Modify filename$
 End If
 !
 !
 Print "DynamicXport File Definitions database files created!"
Else
 ! error routine
  if spc(8)<>10000
   call dfsuberr(e$,spc(8),spc(10),msc$(2))
  end if
   print e$
End Try
chain ""