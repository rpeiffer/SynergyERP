! libcrm.lib
!
! supporting library of file routines for CRM module
!
! loadsave -w -n 100,10 -o prog/dxport/libcrm.lib src/libcrm.src
!
! 1.0 04/29/2011 
!  CALLED FROM "FMMENU" as a program
!  Notes: each action1 will open it's own files
include "src/copyright.inc"
!
! ACTION1's for list library
!  S	GETCUSTS - customer file send with address book
!
External Lib "libgeneral.lib"
Declare External Sub SubErr,GetDelimiters,CreateNetStatus

External Lib "ubsfunc.dl4"
Declare External Function OpenFile,clrtxtc$
Declare External Function GetUIDRec,numericonly$
Declare External Sub UserCntrl

Declare Intrinsic Sub Env,FormatDate,programdump,Stringsearch

Declare Sub SndCusts
dim e$[500],buttonlist$[5,50],nextlist$[5,100] !error handling variables
OPTION GOSUB NESTING 12  ! Default is 8
try
 Option String Redim is legal ! in case subs dim/use same names
	dim action$[30],options$[30],userid$[10],b$[200],Action1$[30],Action2$[30],3%
	dim Message$[200],WebStr$[200],P9$[50],P60$[50],p61$[256]
	Dim 1%,maxcnt \ maxcnt=1000
	DIM 2%,CUSTSEC[1]
	dim List$[maxcnt,500] ! for .net
	Dim SList$[maxcnt,500] ! 2nd section
	Dim bsdel$[10],esdel$[10],rdel$[10],fdel$[10],rstr$[1200],tmp$[1200]
	dim tmp2$[100],tmp3$[100],Custom_customer$[30]
	Dim 3%,R[99],tmpcnt,row,maxrec

	b$=" ",b$
 !
 ! call dxopen() ! here for standalone
 !
 ! call dxsave(0,"tmp/libcrmin.txt!")
 !
 Call GetSession(e$,CTLC,options$,action$,UserID$,intCo,intSls,fdel$,rstr$,bsdel$,esdel$,rdel$,Action1$,Action2$)
 action$=UCase$(action$)
 action$=RTrim$(Action$) ! "trim spaces from end
 action1$=UCase$(action1$)
 action1$=RTrim$(Action1$) ! "trim spaces from end
 action2$=UCase$(action2$)
 action2$=RTrim$(Action2$) ! "trim spaces from end
 Mat read #CTLC,19,50;P9$;
 Mat read #CTLC,60,50;P60$;
 Mat read #CTLC,61,0;P61$;
 mat read #CTLC,115,60;custom_customer$;
 custom_customer$=Trim$(UCase$(custom_customer$))
 returnstatus=0
 message$="Action 1 not found"
 Select case action1$
	case "GETCUSTS" ! customer #,name,slsm,div,slsgrp
	! let's use subroutines
		returnstatus=1
		Message$="OK"
		call Sndcusts()
		! end of custlist
	
 End select
 CLSDone: ! finished
	! status section
	call CreateNetStatus(e$,ReturnStatus,Message$,WebStr$)
	call AddToStr(e$,rstr$,WebStr$)
!
	Call SetOutput(e$,rstr$)
! end of main
else
 include "src/callmainerrnet.inc"
end try
! ---------------------------------------------------------------------
sub sndcusts()
! send every customer in range - we will filter
! we have to use list$[] as 2 sections generated
  Try
	CRC=openfile(-1808,Intco) \ if crc=-1 error 42
	SMC=openfile(-1824,Intco) \ if smc=-1 error 42
	ABC=openfile(-9998,IntCo) \ if abc=-1 error 42
	CNC=openfile(-720,IntCo) \ if cnc=-1 error 42
	! dim what we need!
	dim k1$[60],k2$[50],A0$[68],A1$[142],A5$[50]
	Dim ksl$[50],cnt$[222],a2$[30],a3$[22],wrk$[128],wrk2$[128]
	Dim 1%,A5[10],CNT0,pd1[10]
	Dim 2%,C1[12],X2[9],3%,SGRP,CFIN[8]
	Dim 4%,CNT2[6]
	Dim 3%,s3[9],A4[3]
	
	MAXREC=3000 ! should be around 1MB
	Call dxget("S_USER.USERDEF3",tmp$)
	usegrp=0 \ if UCase$(Trim$(tmp$))="Y" let usegrp=1
	!if usegrp ! use slsm from dxadmin? - let's not!
	!	call dxget("S_USER.USERDEF1",tmp$)
	!	let grpno=tmp$
	!Endif
	! first get the user's security
	Call UserCntrl(userID$,A2$,A5[],ss,intCo)
	! a5[5]=slsm
	SGRP=0
	ksl$=" ",ksl$;ksl$=A5[5] using "###"
	If A5[5]>0 and A5[5]<1000
		Search #smc,2,1;ksl$,R[9],E
	Else
		let e=1
	Endif
	if not(e)
		mat read #smc,r[9],862;s3;
	Else ! SLSM GONE OR ZERO
		s3[9]=0;A5[5]=0
	Endif
	if usegrp and grpno let S3[9]=grpno ! assigned in dynx
	! SGRP=S3[9] ! sgrp used below so we use s3[9]
	R[7]=getuidrec(e$,IntCo,Userid$)
	A4=0
	UCC=openfile(-9988,IntCo) \ if UCC=-1 goto GURecDone
	Mat read #UCC,R[7],300;a4;
	Try close #UCC else rem
	GURecDone: ! port default next
	Div=0
	PDC=OpenFile(-9990,IntCo) \ if pdc=-1 goto PDDone
	If A4>0
	  Try ! incase too high/low or NOT WRITTEN?
		mat read #pdc,a4,0;pd1;
	  Else
		pd1[1]=0
	  End try
	Else
		pd1[1]=0
	Endif
	Div=pd1[1]
	Try close #PDC Else Rem
	PDDone: ! got it all - let's go - get range,etc
	Call DXGet("STCUSTID",tmp$) \ let SCust=tmp$
	call dxget("ENDCUSTID",tmp$) \ if tmp$="" let ECust=999999  else let ECust=tmp$
	Call dxget("ABTYPE",tmp$) \ let abtype=tmp$
	if ecust<scust
		let returnstatus=0
		message$="Invalid Customer Range"
		goto SCLout
	Endif
	if abtype<0 or abtype>3
		returnstatus=0
		message$="Invalid Contact Type (0-3)"
		goto sclout
	Endif
	clear List$[]
	clear SList$[]
	tmpcnt=maxcnt
	stmpcnt=maxcnt ! 2nd list
	LIST$[0]=BSDEL$,"CUSTOMERS",fdel$
	
	webstr$="CUSTID",fdel$,"CUSTNAME",fdel$
	webstr$=webstr$,"ADDRESS1",fdel$,"ADDRESS2",fdel$,"CITY",fdel$
	webstr$=webstr$,"STATE",fdel$,"ZIPCODE",fdel$,"COUNTRY",fdel$
	webstr$=webstr$,"PHONE",fdel$,"FAX",fdel$
	! wanted email,cell,first/last name
	List$[1]=webstr$
	row=2
	SList$[0]=bsdel$,"CONTACTS",fdel$
	Webstr$="CUSTID",fdel$,"CONTID",fdel$
	webstr$=webstr$,"FIRSTNAME",fdel$,"LASTNAME",fdel$
	webstr$=webstr$,"PHONE",fdel$,"FAX",fdel$,"EMAIL",fdel$
	webstr$=webstr$,"ABTYPE",fdel$
	SList$[1]=webstr$
	srow=2
	k1$=" ",k1$ ! we'll use quicker pm way
	K1$=SCust using "######"
	Search #crc,2,1;k1$,R[2],E
	if not(e) goto GFOne ! match of starting
	CCLLoop: ! loop thru CUSTOMER
	Search #crc,3,1;k1$,R[2],E \ if e=2 goto SCLDone
	if e
		returnstatus=0
		message$="Search Error "+Str$(E)+" line "+Str$(SPC(10))+" key "+K1$+" program "+MSC$(4)
		goto SCLDone
	Endif
	GFOne: ! match starting
	X2=k1$[1,6] \ if x2>Ecust goto SCLDone
	Mat read #crc,r[2],0;A1$;
	Mat Read #crc,r[2],142;c1;
	mat read #crc,r[2],348;a2$;
	mat read #crc,r[2],388;a3$;
	mat read #crc,r[2],544;cfin;
	if custom_customer$="SWP" ! southwest Paper
		if c1[0]=12 goto CCLLoop ! closed - do not send
	Endif
	k2$=" ",k2$
	k2$=C1[2] using "###"
	Search #smc,2,1;k2$,R[3],E
	if not(e)
		mat read #smc,r[3],916;sgrp;
	Else
		sgrp=0
	Endif
	If div and cfin[4]<>div goto CCLLoop ! div no match
	if not(usegrp) let sgrp=0;s3[9]=0 ! not using - slsm only
	IF A5[5] or s3[9] ! has slsm or grp default
		IF S3[9] ! grp is main chk - 0=all
			IF SGRP<>S3[9] GOTO CCLLOOP ! grp no match
		Else ! no grp - chk slsm
			CUSTSEC[0] = C1[1]
			CUSTSEC[1] = 1
			IF P9$[21,21]="N" ! MULTI SLSM
				call "custsec.dl4",Custsec[],ctlc,userid$,e$,rstr$
			ELSE
				CUSTSEC[0]=0
			ENDIF
			IF CUSTSEC[0] = -1 goto CCLLoop

			REM	if a5[5] and a5[5]<>c1[2] goto CCLLoop ! slsm no match
		Endif
	Endif ! all test passed - send it
	if allrow>maxrec ! limit reached - cut on new customer only
		let returnstatus=5
		message$="STCUSTID="+Trim$(k1$)
		goto SCLDone
	Endif
	K2$=K1$ ! need custnotes for fax
	search #cnc,2,1;k2$,R[3],E
	if not(e)
		mat read #cnc,r[3],100;a0$;
	Else
		a0$=" ",a0$
	Endif
	! we'll send all phone numbers as digits only - no masking
	tmp2$=RTrim$(a0$[9,28]) 
	tmp3$=numericonly$(tmp2$) ! strip to numerics only to agree with rest
	tmp2$=RTrim$(a1$[21,30])
	tmp$=numericonly$(tmp2$) ! make sure numbers only
	Webstr$=" ",webstr$;wrk$=b$
	wrk$=clrtxtc$(e$,RTrim$(A1$[31,60])) ! chk/strip bad chars
	Webstr$=K1$+fdel$+wrk$+fdel$ ! ID,name
	wrk$=b$;wrk2$=b$ ! clear it
	wrk$=clrtxtc$(e$,RTrim$(A1$[61,90]))
	wrk2$=clrtxtc$(e$,RTrim$(A1$[91,120]))
	webstr$=webstr$+wrk$+fdel$+wrk2$+fdel$ ! add1,2
	wrk$=b$;wrk2$=b$ ! clear it
	wrk$=clrtxtc$(e$,RTrim$(a1$[121,135]))
	wrk2$=clrtxtc$(e$,RTrim$(a1$[136,137]))
	webstr$=webstr$+wrk$+fdel$+wrk2$+fdel$ ! city,st
	wrk$=b$;wrk2$=b$ ! clear it
	wrk$=clrtxtc$(e$,RTrim$(a2$[1,10]))
	wrk2$=clrtxtc$(e$,RTrim$(a3$[7,21]))
	Webstr$=webstr$+wrk$+fdel$+wrk2$+fdel$ ! zip,country
	webstr$=webstr$+RTrim$(tmp$)+fdel$+RTrim$(tmp3$)+fdel$ ! phone,fax
	List$[row]=webstr$
	row=row+1;allrow=allrow+1
	if row>tmpcnt Let tmpcnt = expandarray(e$,List$[])
	! NOW THE CONTACTS - even if goes over limit - send all for customer
	ksl$=" ",ksl$
	KSL$[1,2]="CS" ! all customer contacts start as CS
	KSL$[3]=k1$ ! 3,8 is customer#, 9,10=contact #(99MAX)
	abloop: ! address book for custome
	Search #ABC,3,1;ksl$,R[4],E \ if e goto CCLLoop
	if ksl$[3,8]<>k1$[1,6] goto cclloop
	if ksl$[1,2]<>"CS" goto abloop
	mat read #abc,r[4],16;cnt$;
	mat read #abc,r[4],238;cnt2;
	mat read #abc,r[4],294;cnt0;
	if abtype and abtype<>cnt0 goto abloop ! wrong type
	webstr$=" ",webstr$
	webstr$=K1$+fdel$+Ksl$[9,10]+fdel$ ! Cust#,cont#
	wrk$=b$;wrk2$=b$ ! clear it
	wrk$=clrtxtc$(e$,RTrim$(CNT$[1,16]))
	wrk2$=clrtxtc$(e$,RTrim$(CNT$[17,36]))
	webstr$=webstr$+wrk$+fdel$+wrk2$+fdel$ ! first,last name
	webstr$=webstr$+Str$(CNT2[0])+fdel$+Str$(CNT2[3])+fdel$ ! phone,fax
	wrk$=b$;wrk2$=b$ ! clear it
	wrk$=clrtxtc$(e$,RTrim$(CNT$[173,220]))
	webstr$=webstr$+wrk$+fdel$ ! email
	webstr$=webstr$+Str$(CNT0)+fdel$ ! contact type
	SList$[srow]=webstr$
	srow=srow+1;allrow=allrow+1
	if srow>stmpcnt Let stmpcnt = expandarray(e$,SList$[])
	goto abLoop ! next contact
	SCLDone: ! finito
	List$[row]=esdel$
	call addtostr(e$,rstr$,List$[])
	SList$[srow]=esdel$
	Call addtostr(e$,Rstr$,SList$[])
	webstr$=" ",webstr$
	webstr$=esdel$
	SCLOUT: ! OUT BAD DATA
	try Close #CRC Else rem
	try Close #SMC Else rem
	try close #ABC Else rem
	try close #CNC Else rem
  else
 include "src/callsuberr.inc"
end try
end sub ! sndcusts
!----------------------------------------------------------------------------
