! >>> Program 103jnc
!
! description Customer Printout, Custom Format for Jackson Newell
!
! loadsave -w -n 100,10 -o prog/dxport/103jnc.dl4 src/103jnc.src
!
! 1.0 mm/dd/yyyy change??
!
! select which keyword to use (ONLY 13, 14, 15, 16)
! and is run for 1 salesrep only!
! if that isn't custom, then tell me what is!
!
include "src/copyright.inc" ! dx
!
! custom_customer$="JACKSON" for all companies
!
Declare Intrinsic Sub InpBuf,String,Time
! - Option Default Dialect IRIS1
If Err 0 Gosub ERR_TRAP

!
Dim tmp$[1500],tmp1$[500],tmp2$[300]
Dim action$[30],options$[30],userid$[8],b$[200],3%,fdel$[10],rstr$[500],bsdel$[10],esdel$[10],rdel$[10]
Dim e$[500],buttonlist$[5,50],nextlist$[5,100] ! dx error handling variables
dim Blank$[50] \ Blank$=" ",Blank$
!
Dim 1%,C0,C2[2],SLSMSORT
Dim 2%,C1[12],V2[1],X1,X2,SLSLSM
Dim 3%,V1[1],R2,R[99],E,V3[1]
Dim 3%,C4[5],C12[0],ORDAMT,URECFLAG,PCTR,J1,V9[1],4%,OVERDUE
Dim K1$[50],k2$[50],K5$[50],K6$[50],K9$[50],S1$[118],K4$[40],phn$[15]
Dim A$[30],A1$[142],A2$[30],C1$[1],D$[8],F$[35],A3$[30]
Dim K3$[50],CK$[30],M$[6],N$[30],PKYW$[240],sl1$[142],slphn$[30]
Dim T9$[26],X$[10],Z1$[25],ULN$[130],3%
Dim RETVAL$[512],MSGTXT$[75],Z2$[2]
Dim J$[75],J0$[10],J1$[75],J2$[40],J3$[40],J4$[75],J5$[75],J6$[40],J7$[75],J9$[75]
! dx add xport reporting code
DXSetup:
Call getsession(e$,CTLC,options$,action$,userid$,intCO,intSls,fdel$,rstr$,bsdel$,esdel$,rdel$)
COID=IntCo
F$ = "cntrl/CNTRL"+Str$(COID)
Read #1,88,1808;F$[1,16];
Ropen #3,F$
Read #1,88,1824;F$[1,16];
Ropen #4,F$
Read #1,88,2112;f$[1,16]; ! ship list too
ROpen #5,F$
F$="3/CSLKEYWRD"+STR$(COID)
ROpen #6,f$ ! keywords for cust & shiplist!
B$ = " ",B$
Read #1,3;N$;
Mat Read #1,3,164;D$;
M$ = "########"
V1[0] = 99
If Err 0 Let J8 = Spc(8)
J8 = 0 \ J9 = Chf(0) \ If Err 0 Gosub ERR_TRAP
If J8 If J8 <> 49 Goto ERR_TRAP
If Err 0 Gosub ERR_TRAP
L9 = 60 \ If J8 Let L9 = 19
!
uln$="_",uln$ ! underline
If J8 ! dx xml, setup rstr$
  !<section>
  tmp$ = bsdel$,"103jnc",fdel$,rdel$
  Call addtostr(e$,rstr$,tmp$)
End If 
!
!  dx add xport reporting code
call dxget("slsrep",A$) ! ONE sales rep for report
call dxget("kwnumber",A2$) ! keyword number (13-16 only)
! call dxget("custonly",tmp$) \ if tmp$="" let SLSMSORT=0 else SLSMSORT=1 ! include shiplist?
!  dx end
!
! starting & ending customer
SlsRep=A$ 
if SlsRep<=0 or SlsRep>999 let e$="Invalid salesrep number, must be between 1 and 999!" \ error 10000
kwnum=a2$
if kwnum<13 or kwnum>16 let e$="Invalid keyword number, must be between 13 and 16!" \ error 10000
!
! sort by salesman
!If SLSMSORT = 1 Let CHAN = 9 Else Let CHAN = 3
Let Chan=9 ! always sort it!
!
If J8 = 0 !if pdf or print, new method, after edits,setouput,dxclose
  Call setoutput(e$,rstr$,2) !2 flag puts 0 status section in w/print in process msg, puts </page> in
End If 


!
L_990: Rem let's start to run
! - Window Off 
If CHAN = 3 Goto L_1500
! - Print @0,22;'CE';"Sorting...please wait";
K1$ = " ",K1$
L_1020: Search #3,3,1;K1$,R[4],E
If E Goto L_1500
L_1025: !  If K1$ > A2$ Goto L_1500
Mat Read #3,R[4],142;C1;
if c1[2]<>slsrep goto BPCust ! was L_1020 ! not the one selected
K6$=" ",K6$
LET K6$[1,6]=K1$;K6$[8]="" ! NEEDS CUT OFF?
SEARCH #6,2,1;K6$,R[6],E \ IF E GOTO BPCust ! L_1020 ! SKIP IF NOT FOUND? YES
MAT READ #6,R[6],6;PKYW$;
LET KWD=((KWNUM-1)*15)+1
TMP$=PKYW$[KWD,KWD+14]
TMP$=TRIM$(TMP$) ! STRIP START AND END SPACE
IF TMP$="" GOTO BPCust ! L_1020 ! NOTHING LOADED? SKIP IT
LET X$=TMP$[1] TO ".":POS1 ! SHOULD HAVE FORMAT OF W.D.SSS
LET FWK=X$ \ IF FWK<1 OR FWK>9 GOTO BPCust ! L_1020 ! BAD DATA - SKIP IT!
IF NOT(POS1) GOTO BPCust ! L_1020 ! NOT CORRECT FORMAT - SKIP IT? LETS
POS2=POS1+1 ! Skip dot
x$=tmp$[pos2] to ".":pos1
let fday=x$ \ if fday<1 or fday>7 goto BPCust ! L_1020 ! bad day - skip it
if not(pos1) goto BPCust ! l_1020 ! must have 2 dots - bad format - skip it
let pos1=pos1+1 ! skip dot
let fseq=tmp$[pos1] ! rest is seq
K9$ = " ",K9$ ! is always run for 1 slsrep - so not needed in sort!!
K9$[1,1] = Str$(fwk) ! best be single digit (1-4 normally)
k9$[2,2]=str$(fday) ! single digit (1-7)
k9$[3,6]=fseq using "####" ! allow 4 just in case!
K9$[7] = K6$ ! key from keyword (custs have space, shiplists full key)
Search #9,4,1;K9$,R[4],E 
If E Let e$ = "syserrorfile 103jnc sortfile mode (m)=4 keyno (k)=1 r=",R[4]," e=",e," keyval (k$)=",K9$ \ error 10000
BPCust: ! cust nothing loaded - check shiplist
! now add the shiplists for the customer!
k5$=" ",k5$;k5$[1,6]=k1$
SPL_Loop: ! shiplist loop
search #5,3,1;k5$,r[5],e \ if e goto l_1020
if k5$[1,6]<>k1$[1,6] goto l_1020 ! diff customer
read #5,r[5],390;SLSLSM;
if slslsm and slslsm<>slsrep goto spl_loop
if not(slslsm) and c1[2]<>slsrep goto spl_loop ! no slsrep - and cust<>wanted - bypass 'em
! check slsrep unless zero then use customer slsrep!
K6$=" ",K6$
LET K6$=k5$ ! same key
SEARCH #6,2,1;K6$,R[6],E \ IF E GOTO SPL_Loop ! SKIP IF NOT FOUND? YES
MAT READ #6,R[6],6;PKYW$;
LET KWD=((KWNUM-1)*15)+1
TMP$=PKYW$[KWD,KWD+14]
TMP$=TRIM$(TMP$) ! STRIP START AND END SPACE
IF TMP$="" GOTO SPL_Loop ! NOTHING LOADED? SKIP IT
LET X$=TMP$[1] TO ".":POS1 ! SHOULD HAVE FORMAT OF W.D.SSS
LET FWK=X$ \ IF FWK<1 OR FWK>9 GOTO SPL_Loop ! BAD DATA - SKIP IT!
IF NOT(POS1) GOTO SPL_Loop ! NOT CORRECT FORMAT - SKIP IT? LETS
POS2=POS1+1 ! Skip dot
x$=tmp$[pos2] to ".":pos1
let fday=x$ \ if fday<1 or fday>7 goto SPL_Loop ! bad day - skip it
if not(pos1) goto SPL_Loop ! must have 2 dots - bad format - skip it
let pos1=pos1+1 ! skip dot
let fseq=tmp$[pos1] ! rest is seq
K9$ = " ",K9$ ! is always run for 1 slsrep - so not needed in sort!!
K9$[1,1] = Str$(fwk) ! best be single digit
k9$[2,2]=str$(fday) ! single digit (1-7)
k9$[3,6]=fseq using "####" ! allow 4 just in case!
K9$[7] = K6$ ! key from keyword (custs have space at 7, shiplists the full key)
Search #9,4,1;K9$,R[5],E 
If E Let e$ = "syserrorfile 103jnc sortfile mode (m)=4 keyno (k)=1 r=",R[6]," e=",e," keyval (k$)=",K9$ \ error 10000

Goto SPL_Loop ! L_1020
!
L_1500:
V9[1] = 0
V9[0] = 60
! - Print @0,22;"PRINTING...please wait";
! - If L9 < 30 Print 'CS';
! - Signal 3,0
!
if J8 ! dx xml headings
  !
  let tmp$="SALESREP",fdel$,"SALESREP NAME",fdel$
  tmp$=tmp$,"KEYWORD",FDEL$ ! let them parse it
  tmp$=tmp$,"CUST #",fdel$,"CUSTOMER NAME",fdel$,"ADDRESS",fdel$
  tmp$=tmp$,"CITY",fdel$,"STATE",fdel$,"ZIP CODE",fdel$
  tmp$=tmp$,"CONTACT",fdel$,"PHONE",fdel$
  tmp$=tmp$,"OVERDUE",FDEL$
  !
  call addtostr(e$,rstr$,tmp$+rdel$)
end if
!
! only 1 salesrep used - get them NOW
k4$=" ",k4$
k4$[1,3]=slsrep using "###"
Search #4,2,1;K4$,V3,V2 
If V2=0 Mat Read #4,V3;S1$; else S1$[1,30]="SALESMAN NOT FOUND",Blank$
! loop thru - page break on week and day
CK$ = ""
Let K3$ = " ",K3$
chan=9 ! ALWAYS!!
L_2030: 
Search #CHAN,3,1;K3$,V1,V2 
If V2 Goto L_9100
let shipl=0
if k3$[13,13]="." let shipl=1 ! it's a shiplist!
!
IF CK$ IF CK$[1,2]<>K3$[1,2] ! page break it week or day changed
	Let V9[0] = 100 \ Gosub L_5800
endif
if not(shipl) ! it is a customer!
	R2 = V1
else ! it's a shiplist - need to get customer for AR!
	r[5]=v1
	mat read #5,r[5],0;sl1$; ! name, etc
	mat read #5,r[5],396;SLPHN$;
	let k2$=" ",k2$ ! still need customer (for ar)
	let k2$=k3$[7,12] ! customer
	search #3,2,1;k2$,r2,e \ if e goto L_2030
endif
! get the keword field (as entered!)
K6$=" ",K6$
LET K6$=k3$[7] ! same key
SEARCH #6,2,1;K6$,R[6],E \ IF E GOTO L_2030 ! SKIP IF NOT FOUND? YES
MAT READ #6,R[6],6;PKYW$;
LET KWD=((KWNUM-1)*15)+1
tmp1$=PKYW$[KWD,KWD+14]
tmp1$=TRIM$(tmp1$) ! STRIP START AND END SPACE
Gosub L_4000
Gosub L_5800
Gosub L_5000
CK$[1,2] = K3$[1,2] ! WEEK/STOP
Goto L_2030
!
L_4000: Rem READ CUSTOMER RECORD
if not(shipl)
	Mat Read #3,R2;A1$;
	Mat Read #3,R2,348;A3$;
	phn$=a1$[21,23]+"-"+a1$[24,26]+"-"+a1$[27,30]+"                   "
else ! fill from shiplist
	a1$[1,20]=sl1$[118,137] ! contact
	! phn$=slphn$ ! as entered
	phn$=""
	if len(slphn$) 
		for i=1 to len(slphn$)
			if slphn$[i,i]>" " and slphn$[i,i]<="~" then phn$=phn$+slphn$[i,i]
		next i
		phn$=phn$+"                  "
	endif
	a1$[31,137]=sl1$[1,107] ! name, etc
	a3$[1,10]=sl1$[108,117] ! zip
endif
Mat Read #3,R2,142;C1;
Mat Read #3,R2,194;C2;
Read #3,R2,208;C0;
Mat Read #3,R2,210;C4;

Mat Read #3,R2,412;ORDAMT;
Mat Read #3,R2,668;C12;
Return 
!
L_5000: Rem

If J8 ! dx xml
    tmp$=k4$[1,3],fdel$,rtrim$(S1$[1,30]),fdel$ ! slsrep # / name
	tmp$=tmp$,tmp1$,fdel$ ! keyword as entered
  tmp$=tmp$,k3$[7],fdel$,rtrim$(A1$[31,60]),fdel$ ! cust/shiplist # / name
  tmp$=tmp$,rtrim$(A1$[61,90]),fdel$,rtrim$(A1$[121,135]),fdel$ ! addr1 / city
  tmp$=tmp$,A1$[136,137],fdel$,A3$[1,10],fdel$ ! state / zip
  tmp$=tmp$,a1$[1,20],fdel$,phn$,fdel$ ! contact & phone 
  overdue=C4[2]+c4[3]+c4[4]+c4[5] ! anything past current is over 30?
  IF OVERDUE<0 LET OVERDUE=0
  tmp$=tmp$,LTrim$(overdue Using "------,---,--#.##"),fdel$
  !
  call addtostr(e$,rstr$,tmp$+rdel$)
  return
end if
!
print #0;k3$[3,6]; ! seq
PRINT #0;tab 5; k3$[7]; TAB 20;A1$[31,56]; tab 44;A1$[61,85];
Print #0;tab 68;A1$[121,131]; tab 83;A1$[136,137];" ";A3$[1,10];
Print #0;tab 99;a1$[1,20];phn$[1,13]
overdue=C4[2]+c4[3]+c4[4]+c4[5] ! anything past current is over 30?
if overdue>0 ! only if data is there print
	PRINT #0;  USING "------,---,--#.##"; Tab 4;"** OVER DUE ";OverDue
	let v9[0]=v9[0]+1
endif
print #0;"\15\" ! skip as couple lines
Print #0;"COMMENT: ";uln$[1,120]
Print #0;"\15\" ! skip a couple lines!!
Return 
!
L_5800: Rem NEW PAGE ROUTINE
if J8 return ! dx xml
!
V9[0] = V9[0] + 6 ! # of lines (w/ul and skip 4)
If V9[0] < L9 Return 
If V9[1] = 0 Goto L_5880

L_5870: Print #0;"\14\"; 
L_5880: V9[0] = 10
V9[1] = V9[1] + 1
print #0;""
T9$ = "" \ Call Time(T9$)
Print #0; Tab Int(66 - Len(N$) / 2);N$
Print #0;"\15\";"- 103jnc -"; Tab 10;"DATE"; Tab 15;T9$;
Print #0; Tab 57;"SALESREP CALL LIST";
Print #0; Tab 105;"PAGE # "; Tab 113;V9[1];"\15\";
Print #0;" "
Print #0;"SALESMAN #:";K4$[1,3];" , ";S1$[1,30]
PRINT #0;"WEEK NUMBER: ";K3$[1,1]
Print #0;" "
PRINT #0;"SEQ  CUSTOMER";tab 20;"NAME"; TAB 46;"ADDRESS 1"; TAB 71;"CITY";Tab 83;"ST";
PRINT #0; TAB 86;"ZIP";
PRINT #0; TAB 99;"CONTACT";tab 120;"PHONE"
PRINT #0;""
PRINT #0; TAB 4;"CALL DAY ";K3$[2,2]; ! will do page break upon week or day change
LET X1=K3$[2,2]
IF X1=1 PRINT #0;" SUNDAY"
IF X1=2 PRINT #0;" MONDAY"
IF X1=3 PRINT #0;" TUESDAY"
IF X1=4 PRINT #0;" WEDNESDAY"
IF X1=5 PRINT #0;" THURSDAY"
IF X1=6 PRINT #0;" FRIDAY"
IF X1=7 PRINT #0;" SATURDAY"
Print #0;"\15\";
Return 
!

!
OUTEND: Rem EXIT ROUTINE
! dx close
If j8 ! dx xml
  Call addtostr(e$,rstr$,esdel$) !end section
  Call setoutput(e$,rstr$,1) !1 flag puts 0 status seciton in, puts </page> in
End If 
!
End 
!

! 
L_9100: Rem NO MORE
! - If L9 < 30 Goto L_9140
if J8=0 Print #0;"\15\\15\\15\-- END --"
Goto OUTEND

Rem {begin rtn.error.s}
ERR_TRAP: Rem *Error routine (escape trap)   V3.1 8/94 G.DOSCHER/REK
! dx replace entire error routine
include "src/callmainerrnet.inc"
End 
