! >>> Program 444sw
!
! description Salesman sales by Warehouse
!
! loadsave -w -n 100,10 -o prog/dxport/444sw.dl4 src/444sw.src
!
! 1.0 mm/dd/yyyy change??
!
include "src/copyright.inc" ! dx
!

Rem Based on MX428 - PHILLIPS CUSTOM SLSM/VENDOR REPORT
! Eileen wants something to tie to the 296 report (Columbus Paper)
! 296 uses 3/jsdetl# file - which is updated in the 226 program!
! I advised that inv history is updated in 227 not 226 so may have diff totals
! ERP Version 2.0.1.0 and above ONLY - (has mySQLReport calls)
!
Declare Intrinsic Sub InpBuf,String,Time
External Lib "ubsfunc.dl4"
Declare External Sub GetCostLevels
Declare External Function GetUIDRec
Declare External Function OpenMySQLBrowserOutput,GetMySQLReportHeader
Declare External Sub AddMySQLReportColumnHeadings
Declare External Function AddMySQLReportData
Declare External Sub FinishMySQLReport

If Err 0 Gosub ERR_TRAP
!
Dim e$[500],buttonlist$[5,50],nextlist$[5,100] ! dx error handling variables
!
Dim SL_S1$[30],VNM$[30],PF_A$[156],W1$[32]
Dim 1%,H4[2],L3[3],S2[1],PFU1[20],W1[2],OT19Part,whs[99]
dim 1%,ua5[10],uslsrep[9],Cost_Lev[3],BOYMONTH
Dim 1%,period[1,1,1],2%,range[5,1]
Dim 2%,H0[11],L4[3],B[24],W2[9]
Dim 3%,R[40],T2[25],TOT[5],L5[3],S3[17],A[33],A1[7],PFU2[6]
Dim 3%,SCom[4],W3[24],OREF[5],4%,D3[1]
Dim 1%,J8,J9,V9,E,YR,YEAR_,MTH,MONTH_,WHSE[99],PSLSMN,PART,MONTHTEST
Dim 2%,VENDOR,SLSM,BSLSM,BVEND,J0,H5[16]
Dim 2%,ARDATE,1%,ARMONTH,ARYEAR
Dim 3%,PRCT,R9,PCTR,X3[9],4%,J1
dim 3%,repTotals[2,3] ! [,ytd/mtd/roll,sls/cost/comm/line]
dim 3%,companyTotals[2,3] ! [ytd/mtd/roll,sls/cost/comm/line]
Dim 3%,whseTotals[2,3] ! [ytd/mtd/roll,sls/cost/comm/line]
Dim J$[75],J1$[75],J2$[20],J3$[40],J4$[75],J5$[75],J8$[8],J0$[16]
Dim 1%,F,X1,2%,X2,3%,J,T9[20]
Dim K1$[50],K2$[50],K3$[50],X$[10],T9$[26],P9$[50],P60$[50]
Dim K4$[50],K5$[50],K7$[50],K8$[50]
Dim COMPANYNAME$[30]
Dim 1%,MySQL_ch,reportAborted
Dim 3%,MySQLQueueID
Def FNR(X) = Sgn(X) * (Int(Abs(X) * 100 + .5) * .01)
Def FND(X) = Sgn(X) * (Int(Abs(X) * 10 + .5) * .1)
Def FNI(X) = Sgn(X) * (Int(Abs(X) * 1 + .5) / 1)
DEF FNC(H)=FNR(H-(H*D3[0])) ! not used - include order discount 
!!!! dx add xport reporting code
Dim tmp$[500],tmp1$[300],tmp2$[50]
Dim action$[30],options$[30],userid$[8],b$[200],3%,fdel$[10],rstr$[200],bsdel$[10],esdel$[10],rdel$[10]
Call getsession(e$,CTLC,options$,action$,userid$,intCO,intSls,fdel$,rstr$,bsdel$,esdel$,rdel$)
call GetCostLevels(e$,Cost_Lev[],IntCO,UserID$)
rec_user = GetUIDRec(e$,INTCO,userid$)
! for the determination of ytd start & end!?
ytd = 0 \ mtd = 1 \ rolling = 2
sls = 0 \ cost = 1 \ comm = 2 \ lns=3
beg = 0 \ end_ = 1
mth = 0 \ yre = 1
!call dxsave(0,"tmp/444swin.txt!")
!
For J = 2 To 9
  Read J1
  If J1 = -1 Goto L_830
  Read #1,88,J1;J$;
  Ropen #J,J$
L_830: Next J
Data "1136","1168","1184","2768","1792","1744","1824","-1"
Mat Read #1,0,100;J8$;
Mat Read #1,19,50;P9$;
Mat Read #1,60,50;P60$;
Mat Read #1,0,108;ARDATE;
Mat Read #1,0,120;ARMONTH;
Mat read #1,51,74;BOYMONTH;
ARYEAR = Int(ARDATE / 10000) ! get curr ar year from ardate(yymmdd)
Read #1,3;COMPANYNAME$;
! if userid$[1,4]="JIMS" stop ! for testing/debug
let j$= "4/SORDFLEHHKEY"+str$(intco)
open #10,j$
let j$=" cntrl/usercntrl"
ropen #11,j$
clear ua5
clear uslsrep
if rec_user>0
	mat read #11,rec_user,386;UA5;
	mat read #11,rec_user,630;uslsrep;
endif
If Err 0 Let J8 = Spc(8)
J8 = 0 \ J9 = Chf(0) \ If Err 0 Gosub ERR_TRAP
If Err 0 Gosub ERR_TRAP
If J8 If J8 <> 49 Goto ERR_TRAP
J9 = 60 \ If J8 Let J9 = 20;SCRPRT = 99


!
!!!  dx add xport reporting code
Call dxget("YEAR_",tmp$) \ if tmp$="" let YEAR_=ARYEAR else YEAR_=tmp$ ! calendar year
Call dxget("MONTH_",tmp$) \ if tmp$="" let MONTH_=ARMONTH else MONTH_=tmp$ ! calendar month
!!!  dx end
!
if YEAR_<0 or YEAR_>99 let e$="Invalid year entered, must be the last two digits of the year (YY)!" \ error 10000
!
If MONTH_ < 1 Or MONTH_ > 12 Or Fra(MONTH_) let e$="Invalid month entered, must be between 1 and 12!" \ error 10000
! setup acper

currentyear = Year_ \ if currentYear < 100 let currentYear = currentYear + 2000
currentMonth = Month_
tmp$=currentyear using "&&&&"
tmp$[5]=currentMonth using "&&"
acper=tmp$ ! mtd acct period as YYYYMM
!
for ctr=0 to 99\let WHS[ctr]=0\next ctr
Call dxget("WHSE",tmp$) \ tmp$ = RTrim$(tmp$) !Warehouses
If rtrim$(tmp$)="" or rtrim$(tmp$)="0"
	for ctr=0 to 99 \ let WHS[ctr]=99 \ next ctr
	goto endsetwhs
endif 

NextWHS: ! 
x=-999
X2 = 0 \ Let X$ = tmp$ To "," : X2
X1 = X$ \ If X1 < 1 Or X1 > 99 Let X = 0 \ Goto ExitWHS
WHS[X1] = 99
if X2 let tmp$ = tmp$[X2+1] \ goto NextWHS
ExitWHS:
if X=0 let e$ = "Warehouse!" \ error 10000


endsetwhs: ! 
Call dxget("STTLON",tmp$) ! have only certain totals 
let totonly=0 ! all
If rtrim$(tmp$)="S" let totonly=1
if rtrim$(tmp$)="W" let totonly=2

! ok - let's try to calculate what YTD is for entered month/year! I have no idea on how to do this!
! calculate beginning and ending for ytd ! the ONLY thing I found was in 444i!
	! adjusting for fiscal year
	! ytd = 0 \ mtd = 1 \ rolling = 2
	! beg = 0 \ end_ = 1
	! mth = 0 \ yre = 1
	!
	period[ytd,beg,mth] = BOYMONTH
	period[ytd,beg,yre] = currentYear
	if currentMonth < BOYMONTH period[ytd,beg,yre] = currentYear - 1
	period[ytd,end_,mth] = currentMonth
	period[ytd,end_,yre] = currentYear
	range[ytd,beg] = (period[ytd,beg,yre] * 100) + period[ytd,beg,mth]
	range[ytd,end_] = (period[ytd,end_,yre] * 100) + period[ytd,end_,mth]
	range[mtd,beg] = (currentYear * 100) + currentMonth
	range[mtd,end_] = (currentYear * 100) + currentMonth
	if currentMonth = 1 ! in case monthly breakdown desired
		range[rolling,beg] = ((currentYear - 1) * 100) + 1
		range[rolling,end_] = ((currentYear - 1) * 100) + 12
	else
		range[rolling,beg] = ((currentYear - 1) * 100) + currentMonth
		range[rolling,end_] = (currentYear * 100) + currentMonth - 1
	end if

J1 = 90 \ V9 = 0
!
If J8 ! dx xml, setup rstr$
  !<section>
  MySQL_ch = OpenMySQLBrowserOutput(e$)
  if MySQL_ch <> -1
		MySQLQueueID = GetMySQLReportHeader(e$,MySQL_ch,"444sw")
		if MySQLQueueID = 0
			e$ = "Could not get MySQL report header!"
			error 10000
		end if
  else
	  tmp$ = bsdel$,"444sw",fdel$,rdel$
	  Call addtostr(e$,rstr$,tmp$)
  endif
End If 
!
If J8 = 0  or MySQLQueueID !if pdf or print, new method, after edits,setouput,dxclose
  Call setoutput(e$,rstr$,2) !2 flag puts 0 status section in w/print in process msg, puts </page> in
End If 

START_SORT: 
! - Print @0,22;" Sorting, Please wait...  ";'CE' \ Signal 3,0
J4$ = " ",J4$;K1$ = " ",K1$
L_1120: Search #9,3,1;J4$,R9,E
If E = 2 Goto L_1190
Search #9,5,1;J4$,R9,E
If E Let e$ = "syserrorfile 444sw sortfile mode (m)=5 keyno (k)=1 r=",R9," e=",e," keyval (k$)=",J4$ \error 10000
Goto L_1120
!
L_1190: Rem THRU HEADER
!thru invhkey - dir 1 yyyymm (ar year & month accepted)
LET K1$=" ",K1$
LET K1$[1,4] = period[ytd,beg,yre] - 1 using "&&&&" ! go back an extra 1 year
LET K1$[5,6] = period[ytd,beg,mth] using "&&"
L_1200: ! start the search
Search #10,3,1;K1$,R[2],E
If E Goto L_1800
!IF K1$[1,2]<>"20" GOTO L_1800
!let tmp3=k1$[3,6]
!if tmp3<>acper goto L_1800:
x3=k1$[1,4];x3[1]=k1$[5,6]
if x3 > period[ytd,end_,yre] goto L_1800
if x3 = period[ytd,end_,yre] and x3[1] > period[ytd,end_,mth] goto L_1800
Gosub L_6000 ! read invhdr/totals
! use header whse
Whse=H4[2]
if not(WHS[Whse]) goto L_1200 ! not a selected whse

If H5[7]=19 And OT19Part Goto L_1200: ! "DO NOT INCLUDE PARTIALS OF OT19
! check the sales person /sales group
!!!! VERIFY IF USER HAS ACCESS TO SALESPERSON !!!!
let valid=99 ! yes
if ua5[10]<>0 and ua5[10]<>h0[11] let valid=0
for ctr=0 to 9
	valid1=0
	if uslsrep[ctr]<>0
		if ua5[10]=h0[11]
			let valid1 =99
		endif
	endif
next ctr
if valid=0 and valid1=0 goto L_1200: ! no security -- bypass
! final check is this invoice within the report range ! (only sort valid ones!!)
arYYYYMM = K1$[1,6] ! year month accepted?
svdetl=0 ! save detail?
for i = ytd to mtd ! i=ytd, then i=mtd so it works!
	if arYYYYMM >= range[i,beg] and arYYYYMM <= range[i,end_] ! within YTD or MTD Range
		svdetl=1 ! ok - it's in range
	end if
Next I
if svdetl=0 goto L_1200 ! not in range - why have it in the sort?
SLSM=H0[11];WHSE=h4[2]
If SLSM = 0 Let SLSM = 999
J4$ = " ",J4$
If totonly<>1 let J4$[1,2] = WHSE Using "##" ! only slsrep totals=1
if totonly<>2 let J4$[3,5] = SLSM Using "###" ! only whse totals=2
J4$[6] = K1$
Search #9,4,1;J4$,R[2],E
If E Let e$ = "syserrorfile 444sw sortfile mode (m)=4 keyno (k)=1 r=",R[2]," e=",e," keyval (k$)=",J4$ \error 10000
!
Goto L_1200
!
! *** Process Section
L_1800: Rem PRE
!
! *** Process Section
if j8 ! dx xml headings
	clear tmp$
  If totonly<>2 let tmp$="SALESMAN",fdel$,"SALESMAN NAME",fdel$
  If totonly<>1 let tmp$=tmp$,"WHSE",fdel$
  tmp$=tmp$,"MTD COMM",fdel$
  tmp$=tmp$,"MTD SALES",fdel$,"MTD GR PFT",fdel$,"MTD %PFT",fdel$
  tmp$=tmp$,"MTD LINES",fdel$ 
  tmp$=tmp$,"YTD COMM",fdel$,"YTD SALES",fdel$,"YTD GR PFT",fdel$
  tmp$=tmp$,"YTD %PFT",fdel$,"YTD LINES",fdel$
  !
  if MySQLQueueID
	call AddMySQLReportColumnHeadings(e$,MySQL_ch,MySQLQueueID,fdel$,tmp$)
  else
	call addtostr(e$,rstr$,tmp$+rdel$)
  endif
end if
!
K1$ = " ",K1$;K3$ = ""
PWHSE=0

PSLSMN = 0
L_2000: Search #9,3,1;K1$,R[2],E
If E Goto L_9100
if reportAborted goto OUTEND
  If PSLSMN = 0 Let PSLSMN = K1$[3,5]
  If PWHSE = 0 Let PWHSE = K1$[1,2]
  X2 = K1$[3,5] ! slsrep (blank if whse tot only)
  if totonly<>2 If X2 <> PSLSMN Gosub L_4500
  X2 = K1$[1,2] ! whse (blank if slsrep tot only)
  if totonly<>1 If X2 <> PWHSE Gosub L_4200
  PSLSMN = K1$[3,5];PWHSE = K1$[1,2]

Gosub L_6000 ! read invhdr/totals
! use same parameters as 296 (we want to try to balance to it!)
IF H0[0]=31 LET T2[1]=0-t2[1];T2[17]=0-T2[17];Scom[0]=0-Scom[0]
LET TOT[1]=T2[1];TOT[2]=T2[17] ! "$ & cost 
LET TOT[3]=Scom[0];TOT[4]=0
Gosub Gettotlns: ! needs number of lines on the order! (tot[4])
arYYYYMM = K1$[6,11] ! year month accepted?
prntdetl=0 ! print detail?
for i = ytd to mtd ! i=ytd, then i=mtd so it works!
	if arYYYYMM >= range[i,beg] and arYYYYMM <= range[i,end_] ! within YTD or MTD Range
		repTotals[i,sls] = repTotals[i,sls] + Tot[1]
		repTotals[i,cost]= repTotals[i,cost] + Tot[2]
		repTotals[i,comm] = repTotals[i,comm] + Tot[3]
		repTotals[i,lns] = repTotals[i,lns] + Tot[4]
		prntdetl=1 ! ok - it's in range
		! ADD other possible totals NOW
		whseTotals[i,sls] = whseTotals[i,sls] + TOT[1]
		whseTotals[i,cost] = whseTotals[i,cost] + TOT[2]
		whseTotals[i,comm] = whseTotals[i,comm] + TOT[3]
		whseTotals[i,lns] = whseTotals[i,lns] + TOT[4]
		companyTotals[i,sls] = companyTotals[i,sls] + TOT[1]
		companyTotals[i,cost] = companyTotals[i,cost] + TOT[2]
		companyTotals[i,comm] = companyTotals[i,comm] + TOT[3]
		companyTotals[i,lns] = companyTotals[i,lns] + TOT[4]
	endif
Next I
! if prntdetl
!	gosub L_5000
!Endif
Goto L_2000
!

L_4000: Rem REPORT TOTAL PRINT
if J8 return ! dx xml
!
Gosub L_5800
Print #0;"\15\** REPORT TOTALS **";
		Print #0; Using "--------#.##"; Tab 35;FNR(companyTotals[mtd,comm]);
		Print #0; Using "--------#.##"; Tab 47;FNR(companyTotals[mtd,sls]);
		x3=companyTotals[mtd,sls]-companyTotals[mtd,cost] ! GP
		if cost_lev[1]=0 let x3=0
		Print #0; Using "--------#.##"; Tab 59;FNR(X3);
		PRCT = 0 \ If companyTotals[mtd,sls] Let PRCT = FND(x3 * 100 / companyTotals[mtd,sls])
		if cost_lev[1]=0 let prct=0
		Print #0; Using "---#.#"; Tab 71;PRCT;
		Print #0; Using "-----#"; Tab 77;FNI(companyTotals[mtd,lns]);
		Print #0; Using "--------#.##"; Tab 83;FNR(companyTotals[ytd,comm]);
		Print #0; Using "--------#.##"; Tab 95;FNR(companyTotals[ytd,sls]);
		x3=companyTotals[ytd,sls]-companyTotals[ytd,cost] ! GP
		if cost_lev[1]=0 let x3=0
		Print #0; Using "--------#.##"; Tab 107;FNR(X3);
		PRCT = 0 \ If companyTotals[ytd,sls] Let PRCT = FND(X3 * 100 / companyTotals[ytd,sls])
		if cost_lev[1]=0 let prct=0
		Print #0; Using "---#.#"; Tab 119;PRCT;
		Print #0; Using "-----#"; Tab 125;FNI(companyTotals[Ytd,lns]);
Return 
!
L_4200: ! whse sub total
GOSUB L_4500 ! JUST IN CASE - try a salesrep total!!
printThis = 0
	! if anything in YTD for this whse, print
	for i = 0 to 2
		if whseTotals[ytd,i] <> 0
			printThis = 1
			i = 3
		end if
	next i
! if j8 Return ! not for xml(could be if want whse totals only!!)
if totonly=1 goto whtotexit ! only slsrep TTLS
if totonly=0 and j8 goto whtotexit ! not on browser
if printThis
	if totonly=2 and j8 ! wants browser whse ttls ONLY - send em to browser!
		clear tmp$
		LET tmp$=Str$(PWHSE),fdel$
		tmp$=tmp$,LTrim$(whseTotals[mtd,comm] Using "----------#.##"),fdel$
		tmp$=tmp$,LTrim$(whseTotals[mtd,sls]Using "----------#.##"),fdel$
		x3=whseTotals[mtd,sls]-whseTotals[mtd,cost] ! GP
		if cost_lev[1]=0 let x3=0
		tmp$=tmp$,LTrim$(X3 Using "----------#.##"),fdel$
		PRCT = 0 \ If whseTotals[mtd,sls] Let PRCT = FND(x3 * 100 / whseTotals[mtd,sls])
		if cost_lev[1]=0 let prct=0
		tmp$=tmp$,LTrim$(PRCT Using "-----#.#"),fdel$
		tmp$=tmp$,LTrim$(whseTotals[mtd,lns] Using "-------#"),fdel$
		tmp$=tmp$,LTrim$(whseTotals[ytd,comm] Using "----------#.##"),fdel$
		tmp$=tmp$,LTrim$(whseTotals[ytd,sls] Using "----------#.##"),fdel$
		x3=whseTotals[ytd,sls]-whseTotals[ytd,cost] ! GP
		if cost_lev[1]=0 let x3=0
		tmp$=tmp$,LTrim$(X3 Using "----------#.##"),fdel$
		PRCT = 0 \ If whseTotals[ytd,sls] Let PRCT = FND(X3 * 100 / whseTotals[ytd,sls])
		if cost_lev[1]=0 let prct=0
		tmp$=tmp$,LTrim$(PRCT Using "-----#.#"),fdel$
		tmp$=tmp$,LTrim$(whseTotals[Ytd,lns] Using "-------#"),fdel$
		!
		if MySQLQueueID
			if not(AddMySQLReportData(e$,MySQL_ch,MySQLQueueID,fdel$,tmp$))
				! possible user abort - report deleted exit now
				reportAborted = 1
			end if
		else
			call addtostr(e$,rstr$,tmp$+rdel$)
		endif
		goto whtotexit
	endif ! of whse only
	if j8 goto whtotexit ! browser - never print
	! print version
	Gosub L_5800 ! check for TOF
		Print #0; Using "##"; Tab 1;"WHSE ";PWHSE;" TOTALS";
		Print #0; Using "--------#.##"; Tab 35;FNR(whseTotals[mtd,comm]);
		Print #0; Using "--------#.##"; Tab 47;FNR(whseTotals[mtd,sls]);
		x3=whseTotals[mtd,sls]-whseTotals[mtd,cost] ! GP
		if cost_lev[1]=0 let x3=0
		Print #0; Using "--------#.##"; Tab 59;FNR(X3);
		PRCT = 0 \ If whseTotals[mtd,sls] Let PRCT = FND(x3 * 100 / whseTotals[mtd,sls])
		if cost_lev[1]=0 let prct=0
		Print #0; Using "---#.#"; Tab 71;PRCT;
		Print #0; Using "-----#"; Tab 77;FNI(whseTotals[mtd,lns]);
		Print #0; Using "--------#.##"; Tab 83;FNR(whseTotals[ytd,comm]);
		Print #0; Using "--------#.##"; Tab 95;FNR(whseTotals[ytd,sls]);
		x3=whseTotals[ytd,sls]-whseTotals[ytd,cost] ! GP
		if cost_lev[1]=0 let x3=0
		Print #0; Using "--------#.##"; Tab 107;FNR(X3);
		PRCT = 0 \ If whseTotals[ytd,sls] Let PRCT = FND(X3 * 100 / whseTotals[ytd,sls])
		if cost_lev[1]=0 let prct=0
		Print #0; Using "---#.#"; Tab 119;PRCT;
		Print #0; Using "-----#"; Tab 125;FNI(whseTotals[Ytd,lns]);
		Print #0;"\15\" ! 4 blank lines to next slsrep
		Print #0;"\15\" \ J1=J1+2 ! 5800 does 2, needs 2 more

Endif
whtotexit: ! done
for i=ytd to mtd
	for x=sls to lns
		whseTotals[i,x]=0
	next x
Next i
Return
!
L_4500: Rem SLSM SUB TOTAL (Main print routine)
printThis = 0
		! if anything in YTD for this slsrep, print
	for i = 0 to 2
		if repTotals[ytd,i] <> 0
			printThis = 1
			i = 3
		end if
	next i
if totonly=2 goto srtotexit ! whse totals ONLY - bypass slsrep print
X2 = PSLSMN \ Gosub L_6500 ! get salesman
if printThis
	if J8  ! dx xml
		CLEAR TMP$
		LET tmp$=Str$(PSLSMN),fdel$,RTrim$(SL_S1$[1,30]),fdel$
		if totonly<>1 LET tmp$=tmp$,Str$(PWHSE),fdel$
		tmp$=tmp$,LTrim$(repTotals[mtd,comm] Using "----------#.##"),fdel$
		tmp$=tmp$,LTrim$(repTotals[mtd,sls]Using "----------#.##"),fdel$
		x3=repTotals[mtd,sls]-repTotals[mtd,cost] ! GP
		if cost_lev[1]=0 let x3=0
		tmp$=tmp$,LTrim$(X3 Using "----------#.##"),fdel$
		PRCT = 0 \ If repTotals[mtd,sls] Let PRCT = FND(x3 * 100 / repTotals[mtd,sls])
		if cost_lev[1]=0 let prct=0
		tmp$=tmp$,LTrim$(PRCT Using "-----#.#"),fdel$
		tmp$=tmp$,LTrim$(reptotals[mtd,lns] Using "-------#"),fdel$
		tmp$=tmp$,LTrim$(repTotals[ytd,comm] Using "----------#.##"),fdel$
		tmp$=tmp$,LTrim$(repTotals[ytd,sls] Using "----------#.##"),fdel$
		x3=repTotals[ytd,sls]-repTotals[ytd,cost] ! GP
		if cost_lev[1]=0 let x3=0
		tmp$=tmp$,LTrim$(X3 Using "----------#.##"),fdel$
		PRCT = 0 \ If repTotals[ytd,sls] Let PRCT = FND(X3 * 100 / repTotals[ytd,sls])
		if cost_lev[1]=0 let prct=0
		tmp$=tmp$,LTrim$(PRCT Using "-----#.#"),fdel$
		tmp$=tmp$,LTrim$(reptotals[Ytd,lns] Using "-------#"),fdel$
		!
		if MySQLQueueID
			if not(AddMySQLReportData(e$,MySQL_ch,MySQLQueueID,fdel$,tmp$))
				! possible user abort - report deleted exit now
				reportAborted = 1
			end if
		else
			call addtostr(e$,rstr$,tmp$+rdel$)
		endif
	Else
		!
		Gosub L_5800 ! page check
		Print #0; Using "###"; Tab 1;PSLSMN;" ";SL_S1$[1,30];
		Print #0; Using "--------#.##"; Tab 35;FNR(repTotals[mtd,comm]);
		Print #0; Using "--------#.##"; Tab 47;FNR(repTotals[mtd,sls]);
		x3=repTotals[mtd,sls]-repTotals[mtd,cost] ! GP
		if cost_lev[1]=0 let x3=0
		Print #0; Using "--------#.##"; Tab 59;FNR(X3);
		PRCT = 0 \ If repTotals[mtd,sls] Let PRCT = FND(x3 * 100 / repTotals[mtd,sls])
		if cost_lev[1]=0 let PRCT=0
		Print #0; Using "---#.#"; Tab 71;PRCT;
		Print #0; Using "-----#"; Tab 77;FNI(reptotals[mtd,lns]);
		Print #0; Using "--------#.##"; Tab 83;FNR(repTotals[ytd,comm]);
		Print #0; Using "--------#.##"; Tab 95;FNR(repTotals[ytd,sls]);
		x3=repTotals[ytd,sls]-repTotals[ytd,cost] ! GP
		if cost_lev[1]=0 let x3=0
		Print #0; Using "--------#.##"; Tab 107;FNR(X3);
		PRCT = 0 \ If repTotals[ytd,sls] Let PRCT = FND(X3 * 100 / repTotals[ytd,sls])
		if cost_lev[1]=0 let PRCT=0
		Print #0; Using "---#.#"; Tab 119;PRCT;
		Print #0; Using "-----#"; Tab 125;FNI(reptotals[Ytd,lns]);
		Print #0;"\15\" ! 5800 adds 2
	Endif
end if ! of something added
srtotexit: ! slsrep done
	for i=ytd to mtd ! clear rep totals
		Reptotals[i,sls]=0;reptotals[i,cost]=0
		reptotals[i,comm]=0;reptotals[i,lns]=0
	next i

Return 
!
L_5000: Rem PRINT LINE 
RETURN
! if a detail version is required!?  NOTE: There are no YTD totals so print same 2x?!
BSlsm=k1$[3,5] ! current slsrep
!LET TOT[1]=FNC(T2[1]);TOT[2]=T2[17] ! "$ & cost 
!LET TOT[3]=Scom[0];TOT[4]=lines
arYYYYMM = K1$[6,11] ! year month accepted?
PMTD=0;PYTD=0
for i = ytd to mtd ! i=ytd, then i=mtd so it works!
	if arYYYYMM >= range[i,beg] and arYYYYMM <= range[i,end_] ! within YTD or MTD Range
		IF I=YTD LET PYTD=1
		IF I=MTD LET PMTD=1
	ENDIF
NEXT I
if pmtd=0 and pytd=0 goto PrintDetailExit: ! not within report range!?
if totonly goto PrintDetailExit ! want totals only
X2 = BSLSM \ Gosub L_6500 ! get salesman
if J8 ! dx mxl
  !
  tmp$=BSLSM Using "###",fdel$,rtrim$(SL_S1$[1,30]),fdel$
  IF PMTD
	  tmp$=tmp$,LTrim$(TOT[3] Using "--------#.##"),fdel$
	  tmp$=tmp$,LTrim$(TOT[1] Using "--------#.##"),fdel$
	  x3=TOT[1]-TOT[2]
	  if cost_lev[1]=0 let x3=0
	  tmp$=tmp$,LTrim$(X3 Using "--------#.##"),fdel$
	  If TOT[1] Let PRCT = FND(X3 * 100 / TOT[1]) else PRCT = 0
	  if cost_lev[1]=0 let PRCT=0
	  tmp$=tmp$,ltRIM$(PRCT Using "---#.#"),fdel$
	  tmp$=tmp$,ltRIM$(TOT[4] Using "--------#"),fdel$
  ELSE ! not mtd
	tmp$=tmp$," ",fdel$," ",fdel$," ",fdel$
	tmp$=tmp$," ",fdel$," ",fdel$ ! 5 fields!
  endif
  IF PYTD
	  tmp$=tmp$,LTrim$(TOT[3] Using "--------#.##"),fdel$
	  tmp$=tmp$,LTrim$(TOT[1] Using "--------#.##"),fdel$
	  x3=TOT[1]-TOT[2]
	  if cost_lev[1]=0 let x3=0
	  tmp$=tmp$,LTrim$(X3 Using "--------#.##"),fdel$
	  If TOT[1] Let PRCT = FND(X3 * 100 / TOT[1]) else PRCT = 0
	  if cost_lev[1]=0 let PRCT=0
	  tmp$=tmp$,ltRIM$(PRCT Using "---#.#"),fdel$
	  tmp$=tmp$,ltRIM$(TOT[4] Using "--------#"),fdel$
  ELSE ! not ytd?
	tmp$=tmp$," ",fdel$," ",fdel$," ",fdel$
	tmp$=tmp$," ",fdel$," ",fdel$ ! 5 fields!
  endif
  !
  call addtostr(e$,rstr$,tmp$+rdel$)
  goto PrintDetailExit
endif  
!
Gosub L_5800
Print #0; Using "###"; Tab 1;BSLSM;" ";SL_S1$[1,30];
if pmtd ! it's in MTD range
	Print #0; Using "--------#.##"; Tab 35;FNR(TOT[3]);
		Print #0; Using "--------#.##"; Tab 47;FNR(TOT[1]);
		x3=TOT[1]-TOT[2] ! GP
		if cost_lev[1]=0 let x3=0
		Print #0; Using "--------#.##"; Tab 59;FNR(X3);
		PRCT = 0 \ If TOT[1] Let PRCT = FND(x3 * 100 / TOT[1])
		if cost_lev[1]=0 let PRCT=0
		Print #0; Using "---#.#"; Tab 71;PRCT;
		Print #0; Using "-----#"; Tab 77;FNI(TOT[4]);
endif
IF PYTD ! YTD IS same invoice totals?
	Print #0; Using "--------#.##"; Tab 83;FNR(TOT[3]);
		Print #0; Using "--------#.##"; Tab 95;FNR(TOT[1]);
		x3=TOT[1]-TOT[2] ! GP
		if cost_lev[1]=0 let x3=0
		Print #0; Using "--------#.##"; Tab 107;FNR(X3);
		PRCT = 0 \ If TOT[1] Let PRCT = FND(X3 * 100 / TOT[1])
		if cost_lev[1]=0 let PRCT=0
		Print #0; Using "---#.#"; Tab 119;PRCT;
		Print #0; Using "-----#"; Tab 125;FNI(TOT[4]);
endif
Print #0;""
!
PrintDetailExit: ! 
For I = 0 To 5
  TOT[I] = 0
Next I
Return 
!
L_5800: Rem HEADINGS ROUTINE
if J8 return ! dx xml
!
J1 = J1 + 2 \ If J1 < J9 Return 
If V9 If Not(J8) Print #0;"\14\";
V9 = V9 + 1 \ J1 = 8
If Not(J8) Print #0;"\15\";
T9$ = "" \ Call Time(T9$)
Print #0; Tab 10;J8$; Tab 54;COMPANYNAME$; Tab 120;"PAGE";V9
Print #0;" -444sw- ";T9$;
Print #0; Tab 51;
IF TOTONLY<>2 Print #0;"SALESPERSON SALES "; ! WHSE ONLY - NO SLSREP
IF TOTONLY=0 Print #0;"BY ";
IF TOTONLY<>1 PRINT #0;"WAREHOUSE "; ! SLSREP ONLY - NO WHSE
If totonly=2 PRINT #0;"SALES "; ! only sales 
PRINT #0;"REPORT"
Print #0;"\15\";
Print #0; Using "&&"; Tab 5;"RUN FOR: ";MONTH_;"/";YEAR_;
Print #0; Tab 39;"-------  M O N T H   T O   D A T E ---------";
Print #0; Tab 87;"--------- Y E A R   T O   D A T E ----------"
if totonly<>2 Print #0; Tab 1;"SALESMAN";
Print #0; Tab 42;"COMM";
Print #0; Tab 53;"SALES";
Print #0; Tab 64;"GR PFT";
Print #0; Tab 72;"%PFT";
Print #0; Tab 78;"LINES";
Print #0; Tab 90;"COMM";
Print #0; Tab 101;"SALES";
Print #0; Tab 112;"GR PFT";
Print #0; Tab 120;"%PFT";
Print #0; Tab 126;"LINES";
Print #0;" "
Print #0;"\15\";
Return 
!
L_6000: Rem READ HEADER
Mat Read #2,R[2],0;H0;
Mat Read #2,R[2],78;H4;
MAT  READ #2,R[2],104;H5;
mat read #2,r[2],318;scom;
mat  read #2,R[2],508;OT19Part;
Mat Read #2,R[2],512;OREF;
R[3] = OREF[5]
Mat Read #3,R[3],8;T2;
Return 
!
L_6100: Rem READ LINE
Mat Read #4,R[4],8;L3;
Mat Read #4,R[4],16;L4;
Mat Read #4,R[4],32;L5;
Mat Read #4,R[4],256;S2;
Mat Read #4,R[4],260;S3;
Return 
!
L_6200: Rem READ PROD FILE
Mat Read #6,R[6],0;PF_A$;
Mat Read #6,R[6],156;B;
Mat Read #6,R[6],256;A;
Mat Read #6,R[6],460;A1;
Mat Read #6,R[6],512;PFU1;
Mat Read #6,R[6],554;PFU2;
Return 
!
L_6300: Rem READ PRODWHSE
K7$ = " ",K7$;K7$ = PF_A$[140,151];K7$[13] = WHSE Using "##"
Search #7,2,1;K7$,R[7],E 
If E = 1 Goto L_6380
Mat Read #7,R[7],0;W1$;
Mat Read #7,R[7],32;W1;
Mat Read #7,R[7],38;W2;
Mat Read #7,R[7],78;W3;
Goto L_6390
L_6380: W2[3] = 0
L_6390: Return 
!
Gettotlns: ! needs number of lines on the order
K4$=" ",k4$
K4$=k1$[14,23]
NXTINVL: ! loop lines
SEARCH #4,3,1;K4$,R[4],E \ IF E RETURN
IF K4$[1,10]<>K1$[14,23] RETURN
TOT[4]=TOT[4]+1 ! in 226a - any line counts (looking to match 296 report)
goto nxtinvl
!
L_6500: Rem SALESMAN NAME
K8$ = " ",K8$;K8$ = X2 Using "###"
Search #8,2,1;K8$,R[8],E
If E = 1 Goto L_6560
Mat Read #8,R[8],0;SL_S1$;
Return 
L_6560: Rem NO GOOD
SL_S1$ = " ",SL_S1$
SL_S1$ = "SLSM NOT FOUND!"
Return 
!
NUM2Date: ! x2 to X$ MM/DD/YY
X$ = " ",X$ \ X$[10] = ""
X$[1,3] = Int(X2 / 10 ^ 2 - Int(X2 / 10 ^ 4) * 10 ^ 2) + 10 ^ 2 Using "###"
X$[4,6] = Fra(X2 / 10 ^ 2) * 10 ^ 2 + 10 ^ 2 Using "###"
X$[7,9] = Int(X2 / 10 ^ 4) + 10 ^ 2 Using "###"
X$[4,4] = "/" \ X$[7,7] = "/" \ X$ = X$[2]
Return 
!
OUTEND: Rem EXIT
! dx close
If J8 ! dx xml
	if MySQLQueueID and not(reportAborted)
		call FinishMySQLReport(e$,MySQL_ch,MySQLQueueID)
	else
	  Call addtostr(e$,rstr$,esdel$) !end section
	  !!!!!!print rstr$ ! see if output (manual run ONLY)
	  Call setoutput(e$,rstr$,1) !1 flag puts 0 status seciton in, puts </page> in
	 endif
End If 
!
End 
!
L_9100: Rem END of sorted - print totals
Gosub L_4500 ! sls rep
Gosub L_4200 ! whse
Gosub L_4000 ! report

Goto OUTEND

!
Rem {begin rtn.error.s}
ERR_TRAP: Rem *Error routine (escape trap)   V3.1 8/94 G.DOSCHER/REK
!
! dx replace entire error routine
include "src/callmainerrnet.inc"
End 
