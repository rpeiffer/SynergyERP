! >>> Program mxvaleaddr
!
! description validate email address
!
! loadsave -w -n 100,10 -o prog/dxport/mxvaleaddr.dl4 src/mxvaleaddr.src
!
! 1.0 mm/dd/yyyy change??
!
include "src/copyright.inc" ! dx
! *- This subroutine will validate email address.  Unfortunately there
! *- is really no good way to do this.  Even if we went through the massive
! *- undertaking of making this check for an RFC compliant email address, 
! *- that is no guarantee the mail router would handle it properly, SOOO 
! *- we are going to enforce some very specific rules here - the format of
! *- a valid email address will be:
! *-
! *-                       Free Form Name <user@host.domain>
! *-                                        OR
! *-                                 user@host.domain
! *-
! *-             Where @host.domain is optional.
! *-
! *- PARAMETERS:
! *- 			IN_ADDR - Input Address
!				e$,rstr$ = Web Variables
! *- RETURNS:
! *-			RETVAL = 1 address valid
! *-			RETVAL = 0 address invalid
! *-
! *- MODIFICATION LOG
! *- ----------------
! *- RGP - 05/24/00 - initial development CCT 135482
Rem BEDIT Version: 2.1 OBJECT CODE     Created: JUN  6, 2000  10:50:56
Rem /usr/acct/r97//usr/acct/rgp/work/devl/mx0/mxvaleaddr.src (univ2) [rgp] {17207}
Rem  Copyright (c) 2000 Universal Business Systems, Inc.
Rem    **** ALL CHANGES MUST BE MADE IN SOURCE!!! ****
Rem
Declare Intrinsic Sub InpBuf,String
!Option Default Dialect IRIS1
Rem ***************************************************************
Rem ***************************************************************
Rem *-
Try
	Dim E_ADDR$[254],E_USER$[254],E_DOM$[254] ! expand out the email address to 254 characters
	dim rstr$[300],buttonlist$[5,50],nextlist$[5,100] ! dx error handling variables
	Dim 2%,BIDX,EIDX,I
	Enter IN_ADDR$,RETVAL,e$

	RETVAL = 0
	In_Addr$=Trim$(In_addr$) ! Call "trim",IN_ADDR$
	E_ADDR$ = IN_ADDR$
	If Len(E_ADDR$) = 1
		If Not((E_ADDR$ >= "A" And E_ADDR$ <= "Z") Or (E_ADDR$ >= "a" And E_ADDR$ <= "z")) Goto OUTEND
	End If 
	Search E_ADDR$,"<",BIDX
	If BIDX
		BIDX = BIDX + 1
		Search E_ADDR$[BIDX],"<",EIDX
		If EIDX Goto OUTEND
		Search E_ADDR$[BIDX],">",EIDX
		If EIDX <> Len(E_ADDR$[BIDX]) Goto OUTEND
		E_ADDR$ = E_ADDR$[BIDX,(BIDX - 1) + (EIDX - 1)]
	End If 
	Let E_USER$ = E_ADDR$ To "@" : BIDX
	If BIDX = 1 Or BIDX = Len(E_ADDR$) Goto OUTEND
	For I = 1 To Len(E_USER$)
		If Not((E_USER$[I,I] >= "A" And E_USER$[I,I] <= "Z") Or (E_USER$[I,I] >= "a" And E_USER$[I,I] <= "z"))
			If Not(E_USER$[I,I] >= "0" And E_USER$[I,I] <= "9")
				If Not(E_USER$[I,I] = "." Or E_USER$[I,I] = "_" Or E_USER$[I,I] = "~" Or E_USER$[I,I] = "-")
					Goto OUTEND
				End If 
			End If 
		End If 
	Next I
	If BIDX
		E_DOM$ = E_ADDR$[BIDX + 1]
		Search E_DOM$,".",BIDX
		If Not(BIDX) Goto OUTEND
		If E_DOM$[1,1] = "." Or E_DOM$[Len(E_DOM$)] = "." Goto OUTEND
		For I = 1 To Len(E_DOM$)
			If Not(E_DOM$[I,I] >= "0" And E_DOM$[I,I] <= "9")
				If Not((E_DOM$[I,I] >= "A" And E_DOM$[I,I] <= "Z") Or (E_DOM$[I,I] >= "a" And E_DOM$[I,I] <= "z"))
					If Not(E_DOM$[I,I] = "." Or E_DOM$[I,I] = "_" Or E_DOM$[I,I] = "~" Or E_DOM$[I,I] = "-")
					Goto OUTEND
					End If 
				End If 
			End If 
		Next I
	End If 
	RETVAL = 1
	OUTEND: Rem That's all folks!
	! End 
Else
	include "src/callmainerrnet_call.inc"
EndTry
End ! of whole program