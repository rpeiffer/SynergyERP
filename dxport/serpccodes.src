! 1.0 03/14/2010 - initial conversion
!
! loadsave -w -n 100,10 -o prog/dxport/serpccodes.dl4 src/serpccodes.src
!
! from original code
Rem BEDIT Version: 2.2 OBJECT CODE     Created: DEC 27, 2000  13:22:19
Rem /usr/acct/r97//usr/acct/kag/work/r97/misc/mxccodes.src (univ2) [kag] {29267}
Rem  Copyright (c) 2000 Universal Business Systems, Inc.
Rem    **** ALL CHANGES MUST BE MADE IN SOURCE!!! ****
Rem
Rem     MXCCODES   UNIT CODES FILE MAINTENACE
Rem     AUTHOR:  GEORGE DOSCHER
Rem     LAST UPDATE:
!
!include copyright, common libs, common intrinsics
include "src/copyright.inc"
Declare External Sub CreateNetStatus
!
!include structure,filehandler libs & default structure dims
! include "src/inc/file?.inc"
Declare Intrinsic Sub Echo,InpBuf,String
Option Default Dialect IRIS1
External Lib "ubsfunc.dl4"
Declare External Function openfile

dim e$[500],buttonlist$[5,50],nextlist$[5,100] !error handling variables
!
try
dim action$[30],action1$[30],action2$[30],options$[30],userid$[8],3%
dim bsdel$[20],esdel$[20],rdel$[20],fdel$[20],rstr$[1200] ! .net delimiters
dim tmp$[500],tmp2$[1000],b$[200]
b$=" ",b$
dim message$[150],webstr$[200]
!
! call dxopen()
!
! call getsession(e$,CTLC,options$,action$,userid$,intCO,intSls,fdel$,rstr$,bsdel$,esdel$,rdel$)
Call GetSession(e$,CTLC,options$,action$,UserID$,intCo,intSls,fdel$,rstr$,bsdel$,esdel$,rdel$,Action1$,Action2$)

DIM CCD$[14],cck$[4]
DIM 1%,ccc
DIM 3%,ccr

ccc=OpenFile(1728,intCo)  \ If ccc=-1 Error 42    !u/m ccodes

if action1$ <> "GETLIST"
	call dxget("ID", tmp$) \ cck$ = ucase$(tmp$),b$
	if cck$ = "    "
		e$ = "Please enter a unit of measure code!"
		error 10000
	end if
	if cck$ = "CWT " or cck$ = "LOT "
		e$ = "This code is reserved, cannot add!"
		error 10000
	end if
	gosub GetUOM:
end if

select case action1$
	case "GETLIST"
		gosub BuildDroplist
		! status section
		Message$=""
	case "GETDATA"
		gosub LoadData
		! status section
		Message$=""
	case "SUBMITDATA"
		gosub UnloadDataAndSave
		! status section
		message$ = "Unit of measure updated."
	case else
		e$ = "Unknown action1: "+action1$ \ error 10000
end select
! message should be set.  if we got here - we've got a good status
ReturnStatus=1
CCDDone: ! finished
call CreateNetStatus(e$,ReturnStatus,Message$,WebStr$)
call addToStr(e$,rstr$,webstr$)
goto OutEnd:

! ***********************************************************************
! Nobody but us subroutines and exit code down here
! ***********************************************************************

!----------------------------------------------------------------------------
! build a list of u/m codes to send back to the web
!----------------------------------------------------------------------------
BuildDropList:
	tmp$ = bsdel$+"DropList"+fdel$+rdel$+"ID"+fdel$+"Desc"+fdel$+rdel$
	call AddToStr(e$, rstr$, tmp$)
	cck$ = " ",cck$
	do
		search #ccc,3,1;cck$,ccr,e \ if e>2 gosub err_search
		if e exit do
		Mat Read #ccc,ccr;ccd$;
		tmp$=cck$+fdel$+cck$+" - "+ccd$[1,10]+fdel$+rdel$
		call addToStr(e$, rstr$, tmp$)
	loop
	call addToStr(e$,rstr$,esdel$)
Return

!----------------------------------------------------------------------------
! load data of selected uom
!----------------------------------------------------------------------------
LoadData:
	tmp$ = bsdel$+"Table"+fdel$+rdel$+"ID"+fdel$+"Desc"+fdel$
	call addToStr(e$,rstr$,tmp$+rdel$)
	tmp$ = cck$+fdel$+ccd$[1,10]+fdel$
	call addToStr(e$,rstr$,tmp$+rdel$+esdel$)
Return

!----------------------------------------------------------------------------
! unload data of selected uom
!----------------------------------------------------------------------------
UnloadDataAndSave:
	call DxGet("Desc",tmp$) \ tmp$ = ucase$(tmp$) \ ccd$[1,10]=tmp$+b$
	if Trim$(tmp$)=""
		returnstatus=0
		message$="Please enter a description of the U/M!"
		goto CCDDone
	Endif
	Gosub WriteUOM:
Return


!----------------------------------------------------------------------------
! read the u/m rec based on cck$
!----------------------------------------------------------------------------
GetUOM:
	search #ccc,2,1;cck$,ccr,e \ if e>1 gosub err_search
	if e
		ccr = -1
		ccd$ = " ",ccd$
		ccd$[11,14] = cck$
		return
	end if
	Mat Read #ccc,ccr;ccd$;
Return

WriteUOM:
	if ccr = -1 ! new record
		! allocate new rec and add key
		E = 2 \ Search #ccc,1,0;cck$,ccr,E \ If E Gosub ERR_SEARCH
		Search #ccc,4,1;cck$,ccr,E \ If E Gosub ERR_SEARCH
	end if
	Mat Write #ccc,ccr;ccd$;
Return

ERR_SEARCH: Rem *Error routine (search error)
	Print @0,Msc(34) - 1;'CR RB';"*********** SEARCH  ERROR **********";'CE RB'
	e$="Search error "+str$(E)+" line "+str$(spc(16))+" in program "+msc$(4)
	error 10000
return ! never really returns

OUTEND: Rem EXIT ROUTINE
call setoutput(e$,rstr$)
Close

else ! main try failure
 include "src/callmainerrnet.inc"
end try
end
