! >>> Program 103pcu
!
! description Premier Customer Update
!
! loadsave -w -n 100,10 -o prog/dxport/103pcu.dl4 src/103pcu.src
!
! 1.0 mm/dd/yyyy change??
!
include "src/copyright.inc" ! dx
!
Rem                UNIVERSAL BUSINESS SYSTEMS
!
External Lib "ubsfunc.dl4"
Declare External Function OpenMySQLBrowserOutput,GetMySQLReportHeader
Declare External Sub AddMySQLReportColumnHeadings
Declare External Function AddMySQLReportData
Declare External Sub FinishMySQLReport
!
Declare Intrinsic Sub programdump,InpBuf,String,Time
If Err 0 Gosub ERR_TRAP
!
Dim tmp$[1500],tmp1$[500],tmp2$[300]
Dim action$[30],options$[30],userid$[8],b$[200],3%,fdel$[10],rstr$[500],bsdel$[10],esdel$[10],rdel$[10]
Dim e$[500],buttonlist$[5,50],nextlist$[5,100] ! dx error handling variables
dim Blank$[50] \ Blank$=" ",Blank$
!
Dim 1%,C0,C2[2],RSORT,BOYMONTH,ARMONTH
Dim 2%,C1[12],V2[1],X1,X2,c3[1]
Dim 3%,V1[1],R2,R[99],E,V3[1],CFin[8],S1[12,2],S2[12,2]
Dim 3%,C4[5],C12[0],ORDAMT,URECFLAG,PCTR,J1,V9[1],4%,CREDAVAIL
Dim 1%,MySQL_ch,reportAborted
Dim 3%,MySQLQueueID
Dim K1$[60],K9$[60],S1$[118],K4$[60]
Dim A$[30],A1$[142],A2$[30],C1$[1],D$[8],F$[35],A3$[30]
Dim K3$[60],CK$[30],M$[6],N$[30],CN$[10]
dim CA$[100],ca0$[68],Email$[50]
Dim 1%,CURRID,residential,sendemail,CSSI[99]
Dim 2%,DateOpened,Customer,CommCBTable
Dim 3%,gp12m,tgp12m
Dim T9$[26],X$[10],Z1$[25],NPC$[1]
Dim RETVAL$[512],MSGTXT$[75],Z2$[2]
Dim J$[75],J0$[10],J1$[75],J2$[40],J3$[40],J4$[75],J5$[75],J6$[40],J7$[75],J9$[75]
Dim 3%
!~ dx add xport reporting code
DXSetup:
Call getsession(e$,CTLC,options$,action$,userid$,intCO,intSls,fdel$,rstr$,bsdel$,esdel$,rdel$)
!
X2 = intCO
F$ = "CNTRL"+Str$(Intco)
Read #1,88,1808;F$;
Ropen #3,F$
Read #1,88,1824;F$;
Ropen #4,F$ ! slsm
Read #1,88,720;f$;
Open #5,F$ ! custnotes ! Only file written to!
read #1,88,2064;F$
ROpen #6,f$ ! custsls
B$ = " ",B$
Read #1,3;N$;
Mat Read #1,3,164;D$;
Mat Read #1,51,74;BOYMONTH;
Mat Read #1,0,120;ARMONTH;
M$ = "########"
V1[0] = 99
If Err 0 Let J8 = Spc(8)
J8 = 0 \ J9 = Chf(0) \ If Err 0 Gosub ERR_TRAP
If J8 If J8 <> 49 Goto ERR_TRAP
If Err 0 Gosub ERR_TRAP
L9 = 60 \ If J8 Let L9 = 19
!
!
!  dx add xport reporting code
call dxget("begcust",A$) ! starting customer
call dxget("endcust",A2$) \ if RTrim$(A2$)="" let A2$="999999" ! ending customer
call dxget("audito",tmp$) \ AUDOnly=0;tmp$=UCASE$(RTrim$(tmp$)) ! default is to update
If tmp$="Y" let AUDOnly=1 ! No update - just report
call dxget("begdiv",tmp$) \ bdiv=tmp$
call dxget("enddiv",tmp$) \ if RTrim$(tmp$)="" let tmp$="99"
ediv=tmp$
call dxget("MinMths",tmp$) \ if RTrim$(tmp$)="" let tmp$="12"
MinMths=tmp$
if MinMths<1 or MinMths>12 ! as current month doesn't count!!
	e$="Invalid minimum number of months - range is 1-12!" \ error 10000
endif
call dxget("MINGPD",tmp$) \ MINGPD=tmp$ ! we'll require user entry!! (how do we default it??)
if MinGPD<10 or MinGPD>1000000000 ! 10-1Billion
	e$="Invalid Minimum GP Dollars" \ error 10000
endif
call dxget("NoMths",tmp$) \ if RTrim$(tmp$)="" let tmp$="12"
mosls=tmp$ ! months for sls
if mosls<1 or mosls>12 ! as current month doesn't count!!
	e$="Invalid number of months for GP - range is 1-12!" \ error 10000
endif
call dxget("ADDBILLTO",tmp$) ! use bill to's (need sort of customers now!)
ADBTO=0 \ if UCase$(RTrim$(tmp$))="Y" let ADBTO=1
!  dx end
!
! starting & ending customer
J1=A$ \ A$=J1 using "######"
if A2$="" let A2$="999999"
X2=A2$ \ A2$=X2 using "######"
if J1<0 or J1>999999 let e$="Invalid starting customer, must be between 0 and 999999!" \ error 10000
if X2<J1 or X2>999999 let e$="Invalid ending customer, must be between starting customer and 999999!" \ error 10000
!
if ediv<bdiv let e$="Invalid Division range, please re-enter" \ error 10000


!
If J8 ! dx xml, setup rstr$
  !<section>
  MySQL_ch = OpenMySQLBrowserOutput(e$)
  if MySQL_ch <> -1
		MySQLQueueID = GetMySQLReportHeader(e$,MySQL_ch,"103pcu")
		if MySQLQueueID = 0
			e$ = "Could not get MySQL report header!"
			error 10000
		end if
  else
	  tmp$ = bsdel$,"103pcu",fdel$,rdel$
	  Call addtostr(e$,rstr$,tmp$)
  Endif ! sql or not sql
End If 
!
If J8 = 0  or MySQLQueueID !if pdf or print, new method, after edits,setouput,dxclose
  Call setoutput(e$,rstr$,2) !2 flag puts 0 status section in w/print in process msg, puts </page> in
End If 


L_990: Rem let's start to run
! - Window Off 
If ADBTO=0 goto L_1500 ! NO SORT
! - Print @0,22;'CE';"Sorting...please wait";
K1$ = " ",K1$;K1$ = A$
Search #3,2,1;K1$,R[4],E 
If Not(E) Goto L_1025
L_1020: Search #3,3,1;K1$,R[4],E
If E Goto L_1500
L_1025: If K1$ > A2$ Goto L_1500
Mat Read #3,R[4],142;C1;
! if c1[2]<Bslsm or C1[2]>Eslsm goto L_1020 ! slsrep out
mat read #3,r[4],544;cfin;
if CFIN[4]<Bdiv or CFin[4]>Ediv goto L_1020 ! div out
R2 = r[4]
Gosub L_4000 ! READ CUST FILE & Custsls
k3$=" ",k3$;k3$=k1$ ! same index
search #5,2,1;k3$,r[6],E \ if e=1 goto ATSORT ! not there = we will add it!
if e goto L_1020 ! file index issue - bypass
mat read #5,r[6],436;cn$;
if cn$[1,1]="A"  goto L_1020 ! always prem
! we can't clear due to audit!!
ATSORT: ! 
if adbto=0 goto L_1020 ! no billto sort
let NMSls=0;gp12m=0 ! 2 fields to get
Gosub GetCSData ! cust sls data calc
K9$ = " ",K9$
k9$[1,6]=C1[4] using "######" ! cust billto
k9$[7,12]=K1$ ! customer
k9$[13,14]=NMSls using "##" ! save it for this cust
k9$[15]=gp12m using "---------#.##" ! 999 million max
Search #9,4,1;K9$,R[4],E 
If E Let e$ = "syserrorfile 103pcu sortfile mode (m)=4 keyno (k)=1 r=",R[4]," e=",e," keyval (k$)=",K9$ \ error 10000
! now get/save billto totals!!
k9$=" ",k9$;k9$[1,2]="??"
k9$[3,8]=C1[4] using "######";k9$[9]="" !
search #9,2,1;k9$,r[4],e
if not(E) ! existing - add
	k3$=k9$ ! orig key
	mnmsls=k9$[9,10] ! max mthsls
	tgp12m=k9$[11] ! rest is $$
	Search #9,5,1;k3$,r[9],E
	if e>1 Let e$ = "syserrorfile 103pcu sortfile mode (m)=5 keyno (k)=1 r=",R[9]," e=",e," keyval (k$)=",K3$ \ error 10000
Else
	mnmsls=0
	tgp12m=0 ! rest is $$
Endif
if nmsls>mnmsls let mnmsls=nmsls ! set to max
tgp12m=tgp12m+gp12m ! add curr cust
k9$[9,10]=MNMsls using "##" ! new max mth sls
k9$[11]=tgp12m using "---------#.##" ! 999 million max
search #9,4,1;k9$,r[4],E
If E Let e$ = "syserrorfile 103pcu sortfile mode (m)=4 keyno (k)=1 r=",R[4]," e=",e," keyval (k$)=",K9$ \ error 10000
Goto L_1020
!
L_1500:
V9[1] = 0
V9[0] = 60
! - Print @0,22;"PRINTING...please wait";
! - If L9 < 30 Print 'CS';
! - Signal 3,0
!
if J8 ! dx xml headings
  !
  clear tmp$
  
  let tmp$="DIVISION",fdel$
  let tmp$=tmp$,"SALESMAN",fdel$,"SALESMAN NAME",fdel$ 
  tmp$=tmp$,"CUST #",fdel$,"CUSTOMER NAME",fdel$
  tmp$=tmp$,"BILL-TO NUMBER",fdel$
  tmp$=tmp$,"OLD PREM STATUS",FDEL$
  tmp$=tmp$,"PREMIER STATUS",fdel$ ! (A or S)
  tmp$=tmp$,"MTHS SLS",fdel$,"GP 12",fdel$ ! # of mths sales, gp $ for 12 mths
  if adbto
	tmp$=tmp$,"BTMS",fdel$,"BT GP 12",fdel$
  endif
  !
  if MySQLQueueID
		call AddMySQLReportColumnHeadings(e$,MySQL_ch,MySQLQueueID,fdel$,tmp$)
  else
	call addtostr(e$,rstr$,tmp$+rdel$)
  endif
end if
!
Chan=3 \ If ADBTO let chan=9
CK$ = ""
Let K1$ = " ",K1$
if chan=3 let K1$ = A$ ! not sorted!
Search #CHAN,2,1;K1$,V1,V2 
If Not(V2) Goto L_2035
L_2030: ! LOOP TO END
Search #CHAN,3,1;K1$,V1,V2 \ if k1$[1,2]="}}" let v2=2 ! reached audit
if k1$[1,2]="??" let v2=2 ! in to billto totals
If V2 Goto L_3000
L_2035: ! first cust found!!
if not(ADBTO) if k1$>a2$ goto L_3000
if ck$="" let ck$=k1$ ! [1,12]
if reportAborted goto OUTEND
R2 = V1
Gosub L_4000 ! READ CUST FILE & Custsls
IF NOT(ADBTO) if CFIN[4]<Bdiv or CFin[4]>Ediv goto L_2030 ! div out
let NMSls=0;gp12m=0 ! 2 fields to get
Gosub GetCSData ! cust sls data calc
if ADBTO ! get data from BILLTO Totals!
	!IF ck$[1,6]=K1$[1,6] goto L_2030 ! same billto get next cust!
	k9$=" ",k9$;k9$[1,2]="??"
	k9$[3,8]=C1[4] using "######";k9$[9]="" !
	search #9,2,1;k9$,r[4],e
	if not(E) ! existing - add
		tnmsls=k9$[9,10] ! max mthsls
		tgp12m=k9$[11] ! rest is $$
	Else
		tnmsls=0
		tgp12m=0 ! rest is $$
	Endif
	!IF C1[4]=10002
	!	if c1[1]=10002 call programdump("tmp/103pcu0!","")
	!	if c1[1]=10004 call programdump("tmp/103pcu1!","")
	!endif
Else
	tnmsls=nmsls
	tgp12m=gp12m ! rest is $$
Endif ! not billto
! okay - start the magic!
NPC$="N" ! no is default!!
if TNMSls<MinMths goto FinChgs ! fail minmths!
if TGP12m<MINGPD goto FinChgs ! not enough GP - fail
NPC$="S" ! passed the 2 checks - OK cust
FinChgs: ! finish - record audit/change
! get original cn$
k3$=" ",k3$;k3$=C1[1] using "######"
clear cn$
search #5,2,1;k3$,r[6],E 
if not(e)! on file 
	mat read #5,r[6],436;cn$;
	NEWCNT=0
Else ! NOT OF FILE 
	cn$[1,1]="N" ! not on file = N
	NEWCNT=1
endif
If cn$[1,1]="A" goto L_2030 ! always on - bypass
if cn$[1,1]="" or cn$[1,1]<=" " let cn$[1,1]="N"
let ck$=k1$ ! save key
! if NOT(NEWCNT) IF CN$[1,1]=NPC$[1,1] GOTO L_2030 ! NO CHANGE - ignore (ONLY WANT CHANGED PER SPECS!)
IF CN$[1,1]=NPC$[1,1] GOTO L_2030 ! NO CHANGE - ignore (ONLY WANT CHANGED PER SPECS!)
! audit or change key = "}}CCCCCCON ! just for this customer? I think yes
! if needed for bill to - use the TNMSLS & TGP12M values
k4$=" ",k4$;K4$[1,2]="}}"  ! audit indicator
k4$[3,8]=C1[1] using "######" ! CUSTOMER!!
k4$[9,9]=CN$[1,1];k4$[10,10]=NPC$[1,1] ! orig and new status
k4$[11,12]=NMSls using "##"
k4$[13]=gp12m using "---------#.##" ! 999 million max
Search #9,4,1;k4$,r2,e
if e>1 Let e$ = "syserrorfile 103pcu sortfile mode (m)=4 keyno (k)=1 r=",R2," e=",e," keyval (k$)=",K4$ \ error 10000
! get sls when print? (NO - use sent data!! 
if AUDOnly Goto L_2030 ! no update!!
k3$=" ",k3$;k3$=C1[1] using "######"
clear cn$
search #5,2,1;k3$,r[6],E 
if not(e)! not on file - no premier - NO PRINT
	mat read #5,r[6],436;cn$;
Else
	CN$=" ",cn$;ca$=" ",ca$;ca0$=" ",ca0$
	Email$=" ",EMaiL$;CURRID=0;residential=0;sendemail=0
	Clear CSSI[]
	DateOpened=0;Customer=C1[1];CommCBTable=0
	Let e=2;r=0
	search #5,1,0;k3$,r,e
	if e Let e$ = "syserrorfile 103pcu sortfile mode (m)=1 keyno (k)=0 r=",R," e=",e," keyval (k$)=",K3$ \ error 10000
	Search #5,4,1;k3$,r,e
	if e Let e$ = "syserrorfile 103pcu sortfile mode (m)=4 keyno (k)=1 r=",R," e=",e," keyval (k$)=",K3$ \ error 10000
	R[6]=r
	mat write #5,r[6],0;ca$;
	mat write #5,r[6],100;ca0$;
	mat write #5,r[6],168;DateOpened;
	mat write #5,r[6],172;Customer;
	mat write #5,r[6],176;CSSI;
	mat write #5,r[6],376;email$;
	mat write #5,r[6],426;residential;
	mat write #5,r[6],428;sendemail;
	mat write #5,r[6],430;CommCBTable;
	mat write #5,r[6],434;CURRID;
	mat write #5,r[6],436;cn$;
endif
let cn$[1,1]=NPC$[1,1]
mat write #5,r[6],436;cn$;
goto L_2030 ! next
! 
L_3000: ! now audit or report of changes
K1$=" ",k1$
k1$[1,2]="}}" ! start of audit data
L_3100: ! loop it here
Search #9,3,1;k1$,v1,e \ if e goto L_9100
if k1$[1,2]<>"}}" goto L_9100 ! over
R2 = V1
Gosub L_4000 ! READ CUST FILE & Custsls
! GET ALL DATA FROM INDEX
cn$[1,1]=K1$[9,9] ! k4$[9,9]=CN$[1,1];k4$[10,10]=NPC$[1,1] ! orig and new status
npc$=K1$[10,10]
NMSLS=K1$[11,12] ! k4$[11,12]=NMSls using "##"
GP12M=K1$[13] ! k4$[13]=gp12m using "---------#.##" ! 999 million max
k4$=" ",k4$
K4$ = C1[2] USING "###"
  Search #4,2,1;K4$,V3,V2 
  If V2=0 
	Mat Read #4,V3;S1$; 
  else 
	S1$[1,30]="SALESMAN NOT FOUND",Blank$
  endif
if adbto ! get bill to totals
	k3$=" ",k3$;k3$[1,2]="??"
	k3$[3]=C1[4] using "######"
	search #9,2,1;k3$,r,e \ if e>1 error 11000
	if e let k3$=" ",k3$ ! not found?? zero
	tnmsls=k3$[9,10]		! k9$[9,10]=MNMsls using "##" ! new max mth sls
	tgp12m=k3$[11] !k9$[11]=tgp12m using "---------#.##" ! 999 million max
endif
Gosub L_5800
Gosub L_5000
! CK$[1,3] = K3$[1,3] ! no longer used!!
goto L_3100 ! loop it
!
L_4000: Rem READ CUSTOMER RECORD
Mat Read #3,R2;A1$;
Mat Read #3,R2,142;C1;
Mat Read #3,R2,194;C2;
mat read #3,r2,200;c3;
Read #3,R2,208;C0;
Mat Read #3,R2,210;C4;
Mat Read #3,R2,348;A3$;
Mat Read #3,R2,412;ORDAMT;
mat read #3,r2,544;cfin; ! for div!
Mat Read #3,R2,668;C12;
mat read #6,r2,4;s1; ! custsls ty
mat read #6,r2,238;s2; ! custsls ly
Return 
!
L_5000: Rem
  j$=c1[1] using "######"
If J8 ! dx xml
  clear tmp$
  if audonly let tmp$=Str$(Cfin[4])," AO",fdel$ else tmp$=Str$(Cfin[4]), fdel$ ! div
  tmp$=tmp$,K4$[1,3],fdel$,rtrim$(S1$[1,30]),fdel$ ! slsrep
  tmp$=tmp$,J$,fdel$,rtrim$(A1$[31,60]),fdel$ ! cust & name
  tmp$=tmp$,Str$(C1[4]),fdel$ ! billto #
  tmp$=tmp$,CN$[1,1],fdel$ ! premier status - ORIG
  TMP$=TMP$,NPC$[1,1],fdel$ ! new status
  !NMSls=0;gp12m=0 ! 2 fields to get
  !tmp$=tmp$,"MTHS SLS",fdel$,"GP 12",fdel$ ! # of mths sales, gp $ for 12 mths
  if nmsls>12 let tmp$=tmp$,"12+",fdel$ else let tmp$=tmp$,Str$(nmsls),fdel$
  tmp$=tmp$,LTrim$(gp12m using "----------#.##"),fdel$
  if adbto
	if tnmsls>12 let tmp$=tmp$,"12+",fdel$ else let tmp$=tmp$,Str$(tnmsls),fdel$
	tmp$=tmp$,LTrim$(tgp12m using "----------#.##"),fdel$
  endif
  !
  if MySQLQueueID
		if not(AddMySQLReportData(e$,MySQL_ch,MySQLQueueID,fdel$,tmp$))
			! possible user abort - report deleted exit now
			reportAborted = 1
		end if
  else
	call addtostr(e$,rstr$,tmp$+rdel$)
  Endif
  return
end if
!
! report print
Print #0; using "###";cfin[4]; ! division
PRINT #0; tab 5;J$; TAB 12;A1$[31,60];
Print #0; using "######";tab 43;C1[4]; ! billto
print #0; tab 50;k4$[1,3]; tab 55;s1$[1,30]; ! slsrep & name
print #0; tab 86;cn$[1,1]; ! premier status
print #0; tab 89;npc$[1,1]; ! new status
if nmsls<=12 print #0; using "##";tab 92;nmsls; ! no of mths
if nmsls>12 print #0;Tab 91;"12+"; ! as per spec!
print #0; using "---------#.##"; tab 95;gp12m; ! 12 month gp
if adbto
	if tnmsls<=12 print #0; using "##";tab 109;tnmsls; ! no of mths
	if tnmsls>12 print #0;Tab 109;"12+"; ! as per spec!
	print #0; using "---------#.##"; tab 112;tgp12m; ! 12 month gp
Endif
print #0;"" 
! that's all folks!
Return 
!
L_5800: Rem NEW PAGE ROUTINE
if J8 return ! dx xml
!
V9[0] = V9[0] + 1
If V9[0] < L9 Return 
If V9[1] = 0 Goto L_5880
Print #0;"\14\";
L_5880: V9[0] = 6
V9[1] = V9[1] + 1
T9$ = "" \ Call Time(T9$)
Print #0;"\15\"; Tab Int(66 - Len(N$) / 2);N$
Print #0;"- 103PCU -"; Tab 10;"DATE"; Tab 15;T9$;
Print #0; Tab 57;"PREMIER CUSTOMER UPDATE";
if audonly print #0;" - AUDIT";
Print #0; Tab 105;"PAGE # "; Tab 113;V9[1];"\15\";
Print #0;" "
Print #0;"DIV";
PRINT #0;tab 6;"CUSTOMER NAME";tab 43;"BILLTO";
Print #0;tab 50;"SLS";Tab 55;"NAME";
PRINT #0; Tab 84;"OPS"; tab 88;"NPS"; tab 92;"MS";
Print #0;Tab 100;"12 M G/P";
if adbto
	print #0;tab 109;"BMS"; tab 113;"BTO 12 M G/P";
ENDIF
print #0;""
Print #0;"\15\";
Return 
!
GetCSData: ! no mths sales and 12 month gp
if s2[0,1] 
	let nmsls=13 ! last year has sales totals so >12 months
Endif ! add no mths with sales
X7 = BOYMONTH
For I = 1 To 12 ! check 12 months of GP!
	if nmsls<13 if s1[x7,1] let nmsls=nmsls+1 ! month has sales
	if i<=mosls ! gp is up to number of months entered
		If X7 <> ARMONTH Let gp12m=gp12m+S1[X7,1]-s1[X7,2] ! dol-cost=gp (this year)
		if X7 = ARMONTH Let gp12m=gp12m+S2[X7,1]-s2[X7,2] ! dol-cost=gp (from last yr)
	Endif
	Let X7 = X7 + 1 \ If X7 = 13 Let X7 = 1
Next I
Return
!
NUM2DATE: Rem *UNPACK DATE  X2 TO X$      V1.0  4/04/84  G.DOSCHER
X$ = " ",X$ \ X$[10] = ""
X$[1,3] = Int(X2 / 10 ^ 2 - Int(X2 / 10 ^ 4) * 10 ^ 2) + 10 ^ 2 Using "###"
X$[4,6] = Fra(X2 / 10 ^ 2) * 10 ^ 2 + 10 ^ 2 Using "###"
X$[7,9] = Int(X2 / 10 ^ 4) + 10 ^ 2 Using "###"
X$[4,4] = "/" \ X$[7,7] = "/" \ X$ = X$[2]
If Not(X2) Let X$ = "        "
Return 
!
DATE2NUM: Rem *PACK DATE    X$ TO X2      V1.0  4/04/84  G.DOSCHER
X2 = X$[4,5] \ X1 = X$[1,2] \ X2 = X2 + X1 * 10 ^ 2
X1 = X$[7,8] \ X2 = X2 + X1 * 10 ^ 4
Return 
!
OUTEND: Rem EXIT ROUTINE
! dx close
If j8 ! dx xml
	if MySQLQueueID and not(reportAborted)
		call FinishMySQLReport(e$,MySQL_ch,MySQLQueueID)
	else
	  Call addtostr(e$,rstr$,esdel$) !end section
	  Call setoutput(e$,rstr$,1) !1 flag puts 0 status seciton in, puts </page> in
	Endif
End If 
!
End 
!

L_9100: Rem NO MORE
if J8 GOTO OUTEND ! dx xml
if J8=0 Print #0;"\15\\15\\15\-- END --"
V9=99 \ gosub l_5800 ! ON NEW PAGE
! SPECS SAY SHOW ALL SELECTIONS - SO DO IT NOW!
PRINT #0;"BEGINNING CUST = ";A$; ! STARTING CUSTOMER
PRINT #0;TAB 35;"ENDING CUST = ";A2$ ! ENDING CUSTOMER
TMP$="N" \ IF AUDONLY LET TMP$="Y"
PRINT #0;"AUDIT ONLY  ";TMP$ ! DEFAULT IS TO UPDATE
PRINT #0;USING "##";"BEGINNING DIV = ";BDIV;
PRINT #0;USING "##";TAB 35;"ENDING DIV = ";EDIV
PRINT #0;USING "##";"MINIMUM MTHS ";MINMTHS
PRINT #0;USING "##,###,###,###.##";"MINIMUM GPD";MINGPD
PRINT #0;USING "##";"NUMBER SALES MTHS ";MOSLS
TMP$="N" \ IF ADBTO LET TMP$="Y"
PRINT #0;"ADD BILLTO  ";TMP$
! all done!!
Goto OUTEND
!
Rem {begin rtn.error.s}
ERR_TRAP: Rem *Error routine (escape trap)   V3.1 8/94 G.DOSCHER/REK
! dx replace entire error routine
include "src/callmainerrnet.inc"
End 
