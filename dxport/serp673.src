! 1.0 12/02/2009 - initial conversion
!
! loadsave -w -n 100,10 -o prog/dxport/serp673.dl4 src/serp673.src
!
! From the original code:

Rem BEDIT Version: 2.2 OBJECT CODE     Created: SEP 11, 2006  16:57:00
Rem /w2/papermaster/r2005//usr/acct/ben/work/r2005/mx/mx673.src (univ2) [ben] {64389}
Rem  Copyright (c) 2006 Universal Business Systems, Inc.
Rem    **** ALL CHANGES MUST BE MADE IN SOURCE!!! ****
Rem
Rem MX673
Rem MATERIAL CODE MAINT
Rem     *************
Declare Intrinsic Sub InpBuf,String
Option Default Dialect IRIS1
!
!include copyright, common libs, common intrinsics
include "src/copyright.inc"
Declare External Sub CreateNetStatus
!
!
!
!include structure,filehandler libs & default structure dims
! include "src/inc/file?.inc"
!
! *declare additional dl4 intrinsic subs & functionsDeclare Intrinsic Sub InpBuf,String
!
! *declare additional external libs & subs & functions
!External Lib "ubsfunc.dl4"
!Declare External Function openfile,pdate$,jdate$,Formatdate2$
dim e$[500],buttonlist$[5,50],nextlist$[5,100] !error handling variables
!
try
dim action$[30],action1$[30],action2$[30],options$[30],userid$[8],3%
dim bsdel$[20],esdel$[20],rdel$[20],fdel$[20],rstr$[1200] ! .net delimiters
dim tmp$[500],tmp2$[1000],b$[200]
b$=" ",b$
dim message$[150],webstr$[200]
!
! call dxopen()
!
! call getsession(e$,CTLC,options$,action$,userid$,intCO,intSls,fdel$,rstr$,bsdel$,esdel$,rdel$)
Call GetSession(e$,CTLC,options$,action$,UserID$,intCo,intSls,fdel$,rstr$,bsdel$,esdel$,rdel$,Action1$,Action2$)

!If Err 0 Gosub ERR_TRAP
! If Spc(13) = 11 Print @1,1;"\177\UWHOTSPOT.\\HOTSPOT\\HOTSPOTIN.hsf\177\";
Dim F$[16]

Read #1,88,2272;F$;
Open #2,F$
Dim K1$[25],2%,K,K1
Dim A1$[25]

if action1$ <> "GETLIST"
	call DxGet("ID", tmp$)
	K1$ = tmp$[1,1]
	if k1$ = ""
		e$ = "You must enter a material code ID."
		error 10000
	end if
	If K1$ < "0" Or K1$ > "z" Or (k1$ > "9" And k1$ < "A") Or (k1$ > "Z" And k1$ < "a")
		e$ = "Invalid material code entered!"
	end if
	Search #2,2,1;K1$,K1,E \ if E>1 error 42
	If E
		K1 = -1 ! indicate new record
		A1$=" ",A1$
	else
		Mat Read #2,K1;A1$
	end if
end if

!
! main subroutine dispatcher
!
select case action1$
	case "GETLIST"
		gosub BuildDroplist
		! status section
		Message$=""
	case "GETDATA"
		gosub LoadData
		! status section
		Message$=""
	case "SUBMITDATA"
		gosub UnloadDataAndSave
		! status section
		Message$="Record updated."
	case "DELETE"
		gosub DeleteData
		! status section
		Message$="Record deleted."
	case else
		e$ = "Unknown action1: "+action1$ \ error 10000
endselect
! message should be set.  if we got here - we've got a good status
ReturnStatus=1
call CreateNetStatus(e$,ReturnStatus,Message$,WebStr$)
call addToStr(e$,rstr$,webstr$)
goto OutEnd:

! ***********************************************************************
! Nobody but us subroutines and exit code down here
! ***********************************************************************

!----------------------------------------------------------------------------
! build initial drop list of existing records
!----------------------------------------------------------------------------
BuildDroplist: !
	! Add section header and column defs
	tmp$=bsdel$+"Droplist"+fdel$+rdel$
	tmp$=tmp$+"ID"+fdel$+"Desc"+fdel$+rdel$
	call addToStr(e$,rstr$,tmp$)
	K1$ = " "
	do
		Search #2,3,1;K1$,K1,E \ if E>2 error 42
		If E exit do
		Mat Read #2,K1;A1$
		tmp$=k1$+fdel$+k1$+" - "+rtrim$(a1$)+fdel$+rdel$
		call addToStr(e$, rstr$, tmp$)
	loop
	call addToStr(e$, rstr$, esdel$)
Return

!----------------------------------------------------------------------------
! GETDATA call
!----------------------------------------------------------------------------
LoadData: !
	tmp$=bsdel$+"TableData"+fdel$+rdel$
	tmp$=tmp$+"ID"+fdel$+"Desc"+fdel$+rdel$
	tmp$=tmp$+K1$+fdel$+rtrim$(a1$)+fdel$+rdel$+esdel$
	call addToStr(e$, rstr$, tmp$)
Return

!----------------------------------------------------------------------------
! SUBMITDATA call
!----------------------------------------------------------------------------
UnloadDataAndSave: !
	call DxGet("Desc", tmp$) \ a1$ = tmp$,b$
	if K1 = -1
		! new record
		E = 2 \ Search #2,1,0;K1$,K1,E \ If E error 42
		Search #2,4,1;K1$,K1,E \ if e error 42
	end if
	Mat Write #2,K1;A1$;
Return

!----------------------------------------------------------------------------
! DELETE call
!----------------------------------------------------------------------------
DeleteData: !
	if K1 = -1 Return ! new rec - nothing to actually delete
	Search #2,5,1;K1$,K1,E \ if E error 42
	E = 3 \ Search #2,1,0;K1$,K1,E \ if E error 42
Return


OUTEND: Rem EXIT ROUTINE
call setoutput(e$,rstr$)
Close
else ! main try failure
 include "src/callmainerrnet.inc"
end try
end