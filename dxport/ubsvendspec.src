! ubsvendspec.dl4 
! vendor notes
!
! loadsave -w -n 100,10 -o prog/dxport/ubsvendspec.dl4 src/ubsvendspec.src
!
include "src/copyright.inc"
!
!
!  S   getVendSpecN       gets the list of notes in VendSpec File

include "src/inc/filevendspec.inc" ! "vendor specs(notes)
include "src/inc/filea80vm.inc" ! "vendor master

!
External Lib "libgeneral.lib"
Declare External Sub SubErr,GetDelimiters

External Lib "ubsfunc.dl4"
Declare External Function OpenFile,JDate$

Declare Intrinsic Sub Env,FormatDate,programdump



! -------------------------------------------------------------------------------------

External Sub getVendSpecN(e$,Vend$,List$[],VendRec,maxcnt,CTLChan,VSChan,VendChan,Vend. as a80vm)

  ! Creates a list of the Vendor's notes from Vendspec file
  !
  ! e$         : error message
  ! Vend$      : Vendor #
  ! List$[]    : Vendor notes list
  ! VendRec    : Vendor record #
  ! maxcnt     : max # of lines for list$[]
  ! CTLChan    : control file channel
  ! vsChan     : Vendorspec file channel
  ! VendChan   : Vendor file channel
  ! Vend.      : Vendor data 
 
  try
!
  dim 1%,tmp1,Dir,vsDir
	dim 2%,tmp2,tmpcnt
    dim 3%,tmp3,vsres
	dim bsdel$[20],esdel$[20],rdel$[20],fdel$[20],rstr$[500]
    dim tmp$[200],tmp1$[200],WebStr$[400],Message$[200]
	dim VendKey$[64],vskey$[64],Mode$[2],vsMode$[2],blank$[100]
!
	dim vsn. as Vendspec
!	dim Vend. as A80vm - we're passing it
!
	call GetDelimiters(e$,bsdel$,esdel$,rdel$,fdel$)
!
	call dxget("NEXTLINE",tmp$)
	NSLine=tmp$ ! sent chunk - start for next
	tmpcnt=maxcnt
	X1=0 \ if NSLine Let X1=NSLine-1
	clear List$[]
!
 	List$[0]=bsdel$,"VendorNotes",fdel$
	!
	WebStr$="ID",fdel$,"Line",fdel$,"Note",fdel$
    List$[1]=WebStr$
	!
	row=2 ! starting row for data
	if row>tmpcnt let tmpcnt=expandarray(e$,List$[])
	!
	Vend$[7]=""
	VendKey$=Vend$
	mode$="=" ! "exact match
	Dir=1 ! "of Vendor code
	! VendRec=filegetVend(e$,VendChan,mode$,dir,VendKey$,Vend.) - it's passed
	if VendRec>0
		vskey$=Vend$,"    "  ! "space fill after Vend#
		if nsline let vskey$=Vend$+" "+NSLine using "###"
	  	vsMode$=">=" ! "greater than
	  vsDir=1 ! "only 1 dir
	  Do
	    vsres=filegetVendspec(e$,vsChan,vsMode$,vsDir,vskey$,vsn.)
        if vsres<0 or vskey$[1,len(VendKey$)]<>VendKey$ exit do
		vsMode$=">" ! "get next
		vsDir=1 ! "dir 1
		IF ROW>102 ! 33263 - max 100 per time
			let e$="RS=5  NEXTLINE="+VSKEY$[8,10]
			exit do
		endif
		!
		x1=x1+1 ! printing line #
		x=VSKey$[8,10] ! current line #
		if (x-x1>99) or (x-x1<0) let x=0 ! ignore it (no 99 or negative null lines!!)
		if x and x<>x1 ! something missed (exclude 0)
		  for L0=x1 to x-1 ! stop 1 line before current
			WebStr$=VendKey$,fdel$ ! Vendor key
			WebStr$=WebStr$,Str$(L0),fdel$ ! Line Number
			WebStr$=WebStr$,fdel$ ! Note
			List$[row]=WebStr$
			row=row+1
			if row>tmpcnt let tmpcnt=expandarray(e$,List$[])
		  Next L0
		Endif
		if row>tmpcnt let tmpcnt=expandarray(e$,List$[])
		!		
		WebStr$=VendKey$,fdel$ ! Vendor key
        WebStr$=WebStr$,vskey$[8,10],fdel$ ! Line Number
		WebStr$=WebStr$,RTrim$(vsn.Message$),fdel$ ! Note
		List$[row]=WebStr$
		row=row+1
		if row>tmpcnt let tmpcnt=expandarray(e$,List$[])
		x1=VSKey$[8,10] ! last line sent
	  loop
	  !
    endif

	!
	List$[row]=esdel$ ! end of section
  else
    include "src/callsuberr.inc"
  end try
  !
end sub ! "getVendSpecN
! -------------------------------------------------------------------------------------
