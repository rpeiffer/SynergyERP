! --- serp_msgmnt.dl4
! ERP Header Message Maintenance
!
! loadsave -w -n 100,10 -o prog/dxport/serp_msgmnt.dl4 src/serp_msgmnt.src
!
! From the original code:
!
!include copyright, common libs, common intrinsics
include "src/copyright.inc"
Declare External Sub CreateNetStatus
!
!
!
!include structure,filehandler libs & default structure dims
! include "src/inc/file?.inc"

!
! *declare additional dl4 intrinsic subs & functions
Option Default Dialect IRIS1
!
! *declare additional external libs & subs & functions
External Lib "ubsfunc.dl4"
Declare External Function openfile,ChkBadChars,ClrTxtC$

dim e$[500],buttonlist$[5,50],nextlist$[5,100] !error handling variables

Declare Sub OpenFiles,ReadData,WriteData,GetData,SubmitData

!
try
dim action$[30],action1$[30],action2$[30],options$[30],userid$[8],3%
dim bsdel$[20],esdel$[20],rdel$[20],fdel$[20],rstr$[1200] ! .net delimiters
dim tmp$[900],tmp2$[900],b$[200]
b$=" ",b$
dim message$[150],webstr$[200]

Call GetSession(e$,CTLC,options$,action$,UserID$,intCo,intSls,fdel$,rstr$,bsdel$,esdel$,rdel$,Action1$,Action2$)

Dim 1%,usrPri,sysPri
dim usrMsg$[60],sysMsg$[60],blank$[60]
blank$=" ",blank$

dim 3%

!********************
! M A I N   L O G I C
!********************
call OpenFiles()
call ReadData()

Message$=""
ReturnStatus=1
select case action1$
	case "GETDATA"
		call GetData()
	case "SUBMITDATA"
		call SubmitData()
	case else
		Message$ = "Unknown action1: "+rtrim$(action1$)
		ReturnStatus = 0
end select
! status section
call CreateNetStatus(e$,ReturnStatus,Message$,WebStr$)
call addToStr(e$,rstr$,webstr$)
call setoutput(e$,rstr$)
close
else ! main try failure
 include "src/callmainerrnet.inc"
end try
end

!------------------------------------------------------------------------------
! S U B R O U T I N E   S E C T I O N
!------------------------------------------------------------------------------
sub OpenFiles()
try ! main try
	close #1 ! getsession opens the cntrl file as read only, need write access.
	CH_CNTRL = openfile(9999,intCO)
else ! main try
	include "src/callsuberr.inc"
end try
end sub

sub ReadData()
try ! main try
	mat read #1,115,100;usrPri;
	mat read #1,115,102;usrMsg$;
	mat read #1,115,162;sysPri;
	mat read #1,115,164;sysMsg$;
else ! main try
	include "src/callsuberr.inc"
end try
end sub

sub WriteData()
try ! main try
	mat write #1,115,100;usrPri;
	mat write #1,115,102;usrMsg$;
	mat write #1,115,162;sysPri;
	mat write #1,115,164;sysMsg$;
else ! main try
	include "src/callsuberr.inc"
end try
end sub

sub GetData()
try ! main try
	!
	! section 1 message data
	tmp$=bsdel$+"ERPMessages"+fdel$+rdel$
	tmp$=tmp$+"UserPriority"+fdel$+"UserMessage"+fdel$
	tmp$=tmp$+"SystemPriority"+fdel$+"SystemMessage"+fdel$+rdel$
	call addToStr(e$,rstr$,tmp$)
	!
	! section 1 table data: header
	tmp$=str$(usrPri)+fdel$+rtrim$(usrMsg$)+fdel$
	tmp$=tmp$+str$(sysPri)+fdel$+rtrim$(sysMsg$)+fdel$+rdel$
	call addToStr(e$,rstr$,tmp$+esdel$)
else ! main tryuser
	include "src/callsuberr.inc"
end try
end sub

sub SubmitData()
try ! main try
	usrPri = GetParameter$("USRPRI")
	usrMsg$ = ClrTxtC$(e$,GetParameter$("USRMSG"))+blank$
	if not(ChkBadChars(e$,usrMsg$))
		e$ = "Invalid characters found in user message!"
		error 10000
	end if
	sysPri = GetParameter$("SYSPRI")
	sysMsg$ = ClrTxtC$(e$,GetParameter$("SYSMSG"))+blank$
	if not(ChkBadChars(e$,sysMsg$))
		e$ = "Invalid characters found in system message!"
		error 10000
	end if
	call WriteData()
	Message$="Information Updated"
else ! main try
	include "src/callsuberr.inc"
end try
end sub
