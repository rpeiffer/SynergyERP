! serp122 - SDS maintenance
!
! loadsave -w -n 100,10 -o prog/dxport/serp122.dl4 src/serp122.src
!
! From the original code:

!
!include copyright, common libs, common intrinsics
include "src/copyright.inc"
Declare External Sub CreateNetStatus
!
!
!
!include structure,filehandler libs & default structure dims
include "src/inc/filematcode.inc"
!
! *declare additional dl4 intrinsic subs & functionsDeclare Intrinsic Sub InpBuf,String
!
include "src/inc/filemsdfle.inc"

! *declare additional external libs & subs & functions
External Lib "ubsfunc.dl4"
Declare External Function OpenFile

! internal subroutines
Declare Sub BuildDroplist,LoadData,UnloadDataAndSave,DeleteData
Declare External Function YYMMDDToStr$,StrToYYMMDD

dim e$[500],buttonlist$[5,50],nextlist$[5,100] !error handling variables
!
try
dim action$[30],action1$[30],action2$[30],options$[30],userid$[8],3%
dim bsdel$[20],esdel$[20],rdel$[20],fdel$[20],rstr$[1200] ! .net delimiters
dim tmp$[500],tmp2$[1000],b$[200]
b$=" ",b$
dim message$[150],webstr$[200]

dim sds. as msdfle
dim k$[50],mode$[1]

dim 3%

Call GetSession(e$,CTLC,options$,action$,UserID$,intCo,intSls,fdel$,rstr$,bsdel$,esdel$,rdel$,Action1$,Action2$)

ch = OpenFile(672,intCo) \ if ch<0 error 42

if action1$ <> "GETLIST"
	call DxGet("ID", tmp$)
	if tmp$ = ""
		e$ = "You must enter an SDS sheet number."
		error 10000
	end if
	tmp = tmp$
	k$ = tmp using "#########"
	r = filegetmsdfle(e$,ch,"=",1,k$,sds.)
	If r<0
		clear sds.
		sds.MsdsSheetNo = tmp
	end if
end if

!
! main subroutine dispatcher
!
select case action1$
	case "GETLIST"
		call BuildDroplist()
		! status section
		Message$="OK"
	case "GETDATA"
		call LoadData()
		! status section
		Message$="OK"
	case "SUBMITDATA"
		call UnloadDataAndSave()
		! status section
		Message$="Record updated."
	case "DELETE"
		call DeleteData()
		! status section
		Message$="Record deleted."
	case else
		e$ = "Unknown action1: "+action1$ \ error 10000
endselect
! message should be set.  if we got here - we've got a good status
ReturnStatus=1
call CreateNetStatus(e$,ReturnStatus,Message$,WebStr$)
call addToStr(e$,rstr$,webstr$)
call setoutput(e$,rstr$)
Close
else ! main try failure
 include "src/callmainerrnet.inc"
end try
end

! ***********************************************************************
! Nobody but us subroutines down here
! ***********************************************************************

!----------------------------------------------------------------------------
! build initial drop list of existing records
!----------------------------------------------------------------------------
sub BuildDroplist()
try
	! Add section header and column defs
	tmp$=bsdel$+"Droplist"+fdel$+rdel$
	tmp$=tmp$+"ID"+fdel$+"Desc"+fdel$+rdel$
	call addToStr(e$,rstr$,tmp$)
	K$ = " ",k$
	do
		r = filegetmsdfle(e$,ch,">",1,k$,sds.)
		if r<0 exit do
		tmp$=str$(sds.MsdsSheetNo)+fdel$+str$(sds.MsdsSheetNo)+" - "+rtrim$(sds.Desc$)+fdel$+rdel$
		call addToStr(e$, rstr$, tmp$)
	loop
	call addToStr(e$, rstr$, esdel$)
else
	include "src/callsuberr.inc"
end try
end sub

!----------------------------------------------------------------------------
sub LoadData()
try
	tmp$=bsdel$+"TableData"+fdel$+rdel$
	tmp$=tmp$+"ID"+fdel$+"Desc"+fdel$+"EffDate"+fdel$+rdel$
	tmp$=tmp$+str$(sds.MsdsSheetNo)+fdel$
	tmp$=tmp$+rtrim$(sds.Desc$) !on it's own in case NULL
	tmp$=tmp$+fdel$
	tmp$=tmp$+YYMMDDToStr$(e$,sds.EffDate) !on it's own in case NULL
	tmp$=tmp$+fdel$+rdel$+esdel$
	call addToStr(e$, rstr$, tmp$)
else
	include "src/callsuberr.inc"
end try
end sub

!----------------------------------------------------------------------------
sub UnloadDataAndSave()
try
	call DxGet("Desc", tmp$) \ sds.Desc$ = tmp$,b$
	call DxGet("EffDate",tmp$) \ sds.EffDate = StrToYYMMDD(e$,tmp$)
	mode$ = "c" \ if r<0 mode$ = "a"
	r = fileupdatemsdfle(e$,ch,mode$,r,sds.)
	if (mode$ = "a" and r<=0) or (mode$ = "c" and r)
		e$ = "Error updating msdfle file!"
		error 10000
	end if
else
	include "src/callsuberr.inc"
end try
end sub

!----------------------------------------------------------------------------
sub DeleteData()
try
	if r<0 Return ! new rec - nothing to actually delete
	r = fileupdatemsdfle(e$,ch,"d",r,sds.)
	if r
		e$ = "Error deleting record from msdfle file!"
		error 10000
	end if
else
	include "src/callsuberr.inc"
end try
end sub

External Function YYMMDDToStr$(e$,dateVal)
try
	dim retval$[8],tmp$[100]
	dim 3%
	
	retval$ = ""
	if not(dateVal) exit function retval$
	tmp$ = dateVal using "&&&&&&"
	retval$ = tmp$[3,4]+"/"+tmp$[5,6]+"/"+tmp$[1,2]
else
	include "src/callsuberr.inc"
end try
end Function retval$

External Function StrToYYMMDD(e$,dateVal$)
try
	dim tmp$[100]
	dim 3%,retval
	
	retval = 0
	if len(dateVal$) < 8 exit function retval
	if len(dateVal$) =  8
		tmp$ = dateVal$[7,8],dateVal$[1,2],dateVal$[4,5]
	else
		tmp$ = dateVal$[9,10],dateVal$[1,2],dateVal$[4,5]
	end if
	retval = tmp$
else
	include "src/callsuberr.inc"
end try
end Function retval
